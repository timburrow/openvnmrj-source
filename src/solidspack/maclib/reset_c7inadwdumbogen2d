"macro reset_c7inadwdumbogen2d"
//Set the C7 pulses from the Spin Rate

if (srate>500) then 
   $pwXc7 = 1.0e6/(7.0*srate)
   pwXc7 = 0.1*4.0*trunc($pwXc7/(4.0*0.1) + 0.5)
   srate = 1.0e6/(7.0*pwXc7)
   aXc7 = aX90*(4.0*pwX90)/pwXc7
   if (aXc7 = 4095) then 
      write('error','reset_c7 Error: aXc7 is >= to 4095')
   endif
endif

// Set DUMBO X

aXdumbo = aX90
$pwXdumbo = 3.0*4.0*pwX90  // 6*PI

//find the individual step duration
$pwXdumbostep=$pwXdumbo/qXdcf
$pwXdumbostep=0.1*trunc($pwXdumbostep/0.1 + 0.5)

pwXdumbo=$pwXdumbostep*qXdcf

if (nXdumbo < 1.0) then nXdumbo = 1 endif
// Reset the DUMBO amplitude to match the rounded pwXdumbo pulse.

$aXdumbo = ($pwXdumbo/pwXdumbo)*aXdumbo
aXdumbo = trunc($aXdumbo + 0.5)

// Set wdumboX

aXwdumbo = aX90
$pwXwdumbo = 12.0*pwX90
$pwXwdumbostep=$pwXwdumbo/qXdcf
$pwXwdumbostep=0.0125*trunc($pwXwdumbostep/0.0125 + 0.5)
pwXwdumbo=$pwXwdumbostep*qXdcf

// Reset the amplitude to match the rounded 6pi pulse.

$aXwdumbo = ($pwXwdumbo/pwXwdumbo)*aXwdumbo
aXwdumbo = trunc($aXwdumbo + 0.5)

// set tau with a 4 us window

tauXwdumbo = 0.0125*trunc((pwXwdumbo + 4.0)/0.0125 + 0.5)
sw = 1/(tauXwdumbo*1e-6)
if (npaXwdumbo < 10) then npaXwdumbo = 10 endif

// Synchronize the F1 dwell with the dumboX Cycle

if (pwXdumbo > 0.0) then 
   sw1 = 1.0/(pwXdumbo*1e-6)
endif

pwXtilt = pwX90*54.7/90.0
