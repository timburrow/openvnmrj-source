"macro STgetint"
"H. Bildsoe, Aarhus University, Denmark, march 1995 "
"getint- macro for retrieval of integrals"

"modified - D. Rice 2/17/97 to allow for downsamp > 1 .   "
"modified - D. Rice 11/22/98 add $sidebands_found message."
"           and renamed STgetint.                         "
if (srate=0) then 
   write('error','Error: srate set to Zero')
   abort
endif

exists('downsamp','parameter'):$downsamp
if ($downsamp>0) then
   on('downsamp'):$downsampv
   if ($downsampv=0) then 
      $downsampm=1.0
   else
      $downsampm=downsamp
   endif
else
   $downsampm=1.0
endif

$cd=curexp+'/expint'  "file of ssb integrals"
exists($cd,'file'):$a
if ($a>0.5) then
   rm($cd)
endif
$sites=1
repeat 
   if ($sites=1) then
      $viso=viso 
   else 
      $viso=viso2 
   endif
   $ssbnr=trunc((swsim+spsim-$viso)/srate)       "maximum positive ssb no"
   numreg:$nr
   $i=1
   $sidebands_found=0

   repeat
      getreg($i):$min,$max "end points for region $i"
      $f1=$downsampm*($ssbnr*srate+$viso)        "frequency for ssb no $ssbnr"
      if (($min<$f1) and ($max>$f1)) then        "is the ssb inside the region?"
         integ($min,$max):$amp                   "yes"
         $sidebands_found=$sidebands_found+1
         if (sites=1) then
           "write('file',$cd,' %15.6f %15.6f %15.6f %15.6f',$i,$f1,$min,$max)"
            write('file',$cd,' %10.0f %15.6f',$ssbnr,$amp)
         else 
            write('file',$cd,' %2.0f %10.0f %15.6f',$sites,$ssbnr,$amp)
         endif
         $ssbnr=$ssbnr-1
         $i=$i+1
      else 
         if ($max<$f1) then                   "no, region is at lower frequencies"
            repeat                            "check next ssb at lower frequency"
               $ssbnr=$ssbnr-1
               $f1=$downsampm*($ssbnr*srate+$viso)
            until (($f1<=$max) or ($f1<spsim*$downsampm))
         else
            $i=$i+1  "region is at higher frequencies than the ssb. check next region"
         endif 
      endif
   until (($i>$nr) or ($f1<spsim*$downsampm))
   if ($sidebands_found=0) then
      write('error','Site %d: No Sidebands Found - Check viso(%d) and Integral Regions',$sites,$sites)
   endif   
   $sites=$sites+1
until $sites>sites
isvnmrj:$isvnmrj
if ($isvnmrj=0) then 
   menu('stars2')
endif
