"macro BPmakeBIP"
/* generate BIP shape 
   Evgeny Tishencko, Agilent
    March 2010
   Corrected by Peter Sandor January 2014
*/

if ($# < 3)  then
           
    write('alpha',' usage:  BPmakeBIP(shapeName,referencePower,referencePulseWidth)' )
            return 
	  endif   
 
"make BIP pulse, name, reference power, reference pulsewidth"


//input parameters
$shCBIP=$1
$refClvl=$2
$refCpw=$3

//output file
 
 $bipfile=userdir+'/shapelib/'+$shCBIP+'.RF'
 
 
  write('alpha','generating Shaka\'s  BIP 250-15-1382 pulse shape, JMR v151, pp. 269-283, 2001\n')
  write('alpha','250 percent B1 inversion profile, 15 percent B1 inhomogenity')   
  write('alpha','set pw to 1/(4B1)*1382/90=pw90*1382/90')
  write('alpha','i.e. if B1=20kHz, then pwC=12.5   and pwBIP=1382/90*12.5 =192us')
 


//Shaka's broadband pulse at full power 

 
format(1382.0/90.0*$refCpw+0.5,0,0):$pwCBIP
 

$nsteps=0
format(2.0*$pwCBIP+0.5,0,0):$nsteps 

echo('reference power ', $refClvl,'reference hard pulse width',$refCpw) 
echo('bip shape name  is ',$shCBIP)
echo('bip pulsewidth  is ',$pwCBIP)
echo('set bip power to',$refClvl)
echo('number of bip shape steps  is ',$nsteps)
 
  write('reset',$bipfile)
  write('file',$bipfile,'##   SHAKA\'S BROADBAND INVERSION PULSE')
  write('file',$bipfile,'##   BIP 250-15-1382')
  write('file',$bipfile,'##   250 percent B1 inversion profile')
  write('file',$bipfile,'##   15 percent B1 inhomogenity')
  write('file',$bipfile,'##   set pw to 1/(4B1)*1382/90=pw90*1382/90')
  write('file',$bipfile,'##   i.e. if B1=20kHz, then pwC=12.5   and pwBIP=1382/90*12.5 =192us')
  write('file',$bipfile,'##   JMR v151, pp. 269-283, 2001')
  write('file',$bipfile,'##   generated by macro BPmakeBIP_250_15_1382')
  write('file',$bipfile,'%s %d', '##       N steps',$nsteps)
  write('file',$bipfile,'%s %.1f','##       total pulsewdth',$pwCBIP)
  write('file',$bipfile,'%s %d', '##       power level (dB)',$refClvl)
  write('file',$bipfile,'%s %.3f','##       power corresponds to B1, kHz ',250.0/$refCpw)
  write('file',$bipfile,'%s %.1f','##       inversion width (kHz) ',2*2.5*250/$refCpw)
  write('file',$bipfile,'##   ')
  
//// ACTUAL BIP PULSE SHAPE GENERATION  
$i=0
repeat
    $i=$i+1
    $t=-10.0+20.0*$i/($nsteps+1)
    $t=sqrt($t*$t)
    
    if($t<10.0)   then  $a0=-83165.75   $a1=27391.088   $a2=-3000.4957  $a3=110.56624  endif 
    
    if($t<9.3717) then  $a0=-12358.11   $a1=4472.53     $a2=-528.0852   $a3=21.67046   endif

    if($t<8.8482) then  $a0=-2126.58    $a1=812.761     $a2=-92.9098    $a3=4.46419    endif

    if($t<7.801)  then  $a0=65.09       $a1=-25.3       $a2=13.9075     $a3=-0.07389   endif

    if($t<5.1832) then  $a0=9.68        $a1=-6.109      $a2=12.6895     $a3=-0.15535   endif

    if($t<2.5654) then  $a0=0           $a1=0           $a2=12.3388     $a3=-0.3736    endif

    $phase= $a0 + $a1*$t + $a2*$t*$t + $a3*$t*$t*$t    
    $phase= (($phase*10.0) %3600)/10.0
    
    $ppp='' format($phase,1,1):$ppp
     
    write('file',$bipfile,' %6.1f   1023.0    1.0   1',$ppp)
    
until $i=$nsteps
 
return($pwCBIP,$refClvl)  


