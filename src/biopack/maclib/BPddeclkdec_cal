"macro BPddeclkdec_cal"
"BPddeclkdec_cal:" 
"      Calibrates 2H pw90 using Lock/Decoupler. The 2H rf is routed"   
"      to 2H Automation Relay which sends it to the diplexer box and"
"      then to lock BNC on the probe. Thus the calibration is valid for 2H "
"      pw90 or decoupling rf field when using Lock/Decoupler for 2H"
"      Use BPddec3_cal for channel 3 calibrations and BPddec4_cal for channel 4 calibrations"


"  Either of two methods must be used for this test to run properly "

"  Method #1: Disable Relay
"  In order to use this test, the cable connected to port 14 "
"  on the Magnet Relay Driver Board in the magnet leg has to be disconnected. "
"  This cable drives the Deuterium Automation Relay (K5020) at the back of the "
"  magnet leg (look for the label on the driver cable). The cable connection "
"  is made on a small circuit board mounted next to the manual sample eject "
"  control. This permits the 2H pulse to be delivered by the 2H decoupler, even "
"  when using tn=lk for 2H observe. Otherwise, the relay switches to route the "
"  first broadband amplifier to the lowband module for low band observe. "

"  Method #2: Re-route Decoupler RF
"  Move the Decoupler cable from the top of relay K5022 at the back of the magnet "
"  leg to the bottom position of relay K5022. This will permit the RF to proceed to
"  the lock diplexer box when tn=lk"

"  In either case remember to restore the cable to normal position after the test"

if ($#=0) then

   if (BPplot='file') then
    BPplotdir=userdir+'/AutoTripRes'
   endif

  exists('dofidsave','parameter'):$e
   if $e then dofidsave='n' endif

 
   water
   presat='y' satmode='nnn' watergate='n' jumpret='n' wet='n' shaped='n'
   ssfilter='n'
   $date=''
   BPgetdate:$date

  $dir=userdir+'/AutoTripRes'
  $dir2=userdir+'/BioPack.dir/backups'
  exists($dir,'file'):$e
  if ($e=1) then
    exists(userdir+'/BioPack.dir/backups','directory'):$e
    if not($e) then
        shell('mkdir -p ' +$dir2):$dum
    endif
    mv($dir,$dir2+'/AutoTripRes_'+$date)
  endif
           
   $vtset=''
   shell('showstat > ',curexp+'/stat'):$dummy
   lookup('file',curexp+'/stat','VT','active:','read'):$vtset,$ret
   delete(curexp+'/stat')
   if $ret then
     format($vtset,0,1):temp
   endif
   setfrq 
   text('Finding 2H pulse power on Lock/Decoupler\\for pw90~150usec ')
   r5=lockpower
   gain=10 sw=4000 np=8192 nt=1
   if (sfrq>601) then pw=200 else pw=125 endif
   tn='lk' sethw('lock','off'):$xxx
   seqfil='ddeclkdec'
   d3=numrfch  "d3 used in pulse sequence for numrfch value"
   if numrfch=5 then dpwr4=40 dn4='H2' dn3='' dn2='' dn='' dof4=0 endif
   if numrfch=4 then dpwr3=40 dn3='H2' dn2='' dn='' dof3=0 endif
   if numrfch=3 then dpwr2=40 dn2='H2' dn='' dof2=0 endif
   r4=z0 lockpower=0
   sethw('nowait','lockpower',lockpower):$x
   dn='H2' dof=0
   tpwr=40  rof2=300
   wnt='' av
   wexp='BPddeclkdec_cal(`PART1`)'
   au

elseif ($1='PART1') then

   wft f vsadj(100) ds peak:$ht,cr movetof

   if (numrfch=5) then array('dpwr4',34,30,1) dof4=tof endif
   if (numrfch=4) then array('dpwr3',34,30,1) dof3=tof endif
   if (numrfch=3) then array('dpwr2',34,30,1) dof2=tof endif

   "d1=6*d1   account for 2H relaxation ~6x longer than 1H"
   wnt='wft crl rfl=sw/2 rfp=0 sp=-200 wp=400 BPfind2Hpwr av dssh' 
   write('line3','Determining est. of 2H pw90 power using Channel 4 ')
   vp=0 
   wexp='BPddeclkdec_cal(`PART2`)'
   au

elseif ($1='PART2') then
   wft
   BPsvf(userdir+'/AutoTripRes/ddeclkdec_1')
      pap
      pps(120,90,wcmax-120,90)
      pl('all') BPpage
   BPfind2Hpw90:$best 
   if numrfch=5 then dpwr4=dpwr4[$best] endif
   if numrfch=4 then dpwr3=dpwr3[$best] endif
   if numrfch=3 then dpwr2=dpwr2[$best] endif

   av select($best) 
   write('line3','finding first est. of 2H pw360\\ using Lock/Decoupler')
   text('Finding est. of 2H pw360 using Channel 4')
   array('pw',16,3.0*pw,0.1*pw)
   wnt='wft crl rfl=sw/2 rfp=0 sp=-200 wp=400 av dssh' 
   wexp='BPddeclkdec_cal(`PART3`)'
   au

elseif ($1='PART3') then
   wft
   write('line3','calculating first estimate of pw360\\using Lock/Decoupler')
   BPsvf(userdir+'/AutoTripRes/ddeclkdec_2')
      pap
      pps(120,0,wcmax-120,90)
      pl('all') BPpage
   BP2Hpw90cal('pw'):pw av
   write('line3','finding second estimate of 2H pw360\\using Lock/Decoupler ')
   array('pw',10,3.8*pw,0.04*pw)
   wexp='BPddeclkdec_cal(`PART4`)'
   au

elseif ($1='PART4') then
   sethw('z0',r4):$x
   wft 
   write('line3','calculating final est. of H2 pw360\\using Lock/Decoupler')
   text('2H pw360 using Lock/Decoupler')
   BPsvf(userdir+'/AutoTripRes/ddeclkdec_3')
      pap
      pl('all') BPpage
   seqfil='water'
   sethw('nowait','lockpower',r5):$x
   BP2Hpw90cal('pw'):pw av
   pw=pw  "force 0.1usec stepsize"
   if numrfch=5 then tpwr=dpwr4 dof=dof4 endif
   if numrfch=4 then tpwr=dpwr3 dof=dof3 endif
   if numrfch=3 then tpwr=dpwr2 dof=dof2 endif

   write('line3','H2pw90 (Lock/Decoupler) = %2.2f at power = %2.0f',pw,tpwr)
   tn='H1' sethw('lock','on'):$xxx
  if (sfrq>601) then   "700/750/800/900 probes have different design" 
   ln(500.0/pw):$ln  "set pw90 for decoupling to 500usec (0.5kHz)"
   tpwr=tpwr-20.0*$ln*0.43429
   tpwr=tpwr+0.5
   tpwr=trunc(tpwr)
   dmf=1e6/500.0
   dmfadj
  else
   ln(250.0/pw):$ln  "set pw90 for decoupling to 250usec (1kHz)"
   $tpwr=tpwr-20.0*$ln*0.43429
   tpwr=$tpwr+0.5
   tpwr=trunc(tpwr)
   dmf=1e6/250.0
   dmfadj
  endif


" if BPpwrlimits, limit dpwr3 to BPdpwr3max and adjust dmf3 "
"                       here, tpwr will be used as dpwr3     "
  if BPpwrlimits=1 then
   if BPdpwr3max<tpwr then
    ln(10):$ln10
    $exp=$ln10/20*(tpwr-BPdpwr3max)
    exp($exp):$factor
    dmf=dmf/$factor
    dmfadj
    tpwr=BPdpwr3max
   endif
  endif


   dn='H2'
   dof=dof-2.5*dfrq  "set 2H frequency to ~2.5ppm"

   $dpwr3D='' format(tpwr,2,0):$dpwr3D
   BPsetparams('dpwr3D',$dpwr3D,'H2')
   $dmf3D='' format(dmf,5,1):$dmf3D
   BPsetparams('dmf3D',$dmf3D,'H2')
   $dof3D='' format(dof,5,1):$dof3D
   BPsetparams('dof3D',$dof3D,'H2')

 banner('2H AutoCalibrate and Probefile Update Finished\\Remember to Re-Cable Relay')
endif
