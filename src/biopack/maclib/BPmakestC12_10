"macro BPmakestC12_10"
"BPmakestC12_10 - makes sech-amplitude/tanh-frequency swept pulse with 10 usec   "
"		 steps, 12.5 ppm bandwidth, and 10.0 millisec pulselength      "
" 	           to invert all Ca nuclei between 50-62.5 ppm	       "
"           In macro, units are kHz for bandwidth and ms for pulselength       "

"         		R. Bendall, JCU, Jan 1998		               "

"  The pulse is designed to flip all C13 nuclei from the CO region to 0 ppm    "
"  The output is a file in your personal shapelib named stC12_10.RF	       "

"From Bendall, JMR A116, 46 (1995) and STUD papers:			       "
"   Using Pulsetool RFmax was determined for average -0.995 inversion across   "
"   the effecive bandwidth for Tp = 5.0 ms.				       "

"   Using these values, bwdth/(RFmax)^2 = 3.9   at bwdth = 1.5 kHz             "
"                                       = 3.47   at bwdth = 3  kHz.            "
"   		So use 3.47 where bwdth and RFmax are in kHz	  	       "

"   For a 10.0ms pulse length, inversion changes from -0.9 to 0.9 in 310 Hz    "
"   for bwdth=2kHz.  Slightly better selectivity at lower bwdth, slightly      "
"   worse at higher in absolute frequency units (Hz not ppm), so							       "
"			 Tp = $t = 10.0 ms  below. 			       "
"      effective bandwidth(for -0.9 inversion) = bwdth - 0.31 kHz              "
"                           effective bandwidth = 1.875*sfrq/600               "
"      so,                bwdth = (1.875*sfrq/600) + 0.31 = $b below           "

"   dof will be at 35ppm, and the midpoint of the pulse will be at 56.25ppm,   "
"   so relative to dof at						       "
"                    (56.25 -35)*dfrq 			                       "
"   This is used to BPconvolute the pulse off resonance by $freq in Hz below.    "

"   In pulse sequences, bwdth/(RFmax)^2 = 3.47 so 			       "
"			   RFmax = sqrt[(1.875*sfrq/600+0.31)/3.47] in kHz     "

$a = 5.2982937*2.0    "gives an amplitude threshold of 0.01, ie sech($a/2)=0.01"

$file=userdir+'/shapelib/temp.RF'
shell('rm -f',$file):$dummy
write('line3','Writing data to %s...',$file)
write('reset',$file)

$i=1   repeat
$x=$a*(-0.5 + ($i-1)/(1000-1))   exp($x):$y   $z=($y + (1/$y))/2.0  ln($z):$w
$amp = 1023.0/$z     $amp = trunc($amp + 0.5)

$b = ((1.875*sfrq/600.0)+0.31)   $t = 10.0  "bandwidth and pulselength as above"

$p = 360.0*$b*$t*$w/(2.0*$a)    $phase = (trunc(2.0*$p + 0.5))/2.0
write('file',$file,'%5.1f\t%6.1f\t1.0',$phase,$amp)
$i=$i+1   until $i>1000

$freq = (56.25 - 35.0)*dfrq
BPconvolute('temp','stC12_10',10000,$freq)

BPmoveshape('stC12_10.RF')
shell('rm -f',$file):$dummy
write('line3','The SLP sech/tanh pulse "shapelib/stC12_10.RF" has been created')
