"macro ghn_coca_cb"
BPrtppar('ghn_coca_cb')

setfrq BPsetampmode
if (numrfch>3) then
 getparam('dof3D','H2'):dof3
 getparam('dmm3D','H2'):dmm3
 getparam('dmf3D','H2'):dmf3
 getparam('dpwr3D','H2'):dpwr3
 getparam('dres3D','H2'):dres3
 getparam('dseq3D','H2'):dseq3
else
  exists('ampmode','parameter'):$e
  if ($e=1) then destroy('ampmode') endif
endif
reffrq=sfrq-(sw/2-rfl+rfp)/1e6
 getparam('NHgrad1_time','N15'):gt1 
 getparam('NHgrad1_lvl','N15'):gzlvl1 
 getparam('NHgrad2_lvl','N15'):gzlvl2 
 getparam('NHgstab','N15'):gstab
 getparam('swN','N15'):sw2 
 dm2='nny' sw1=80*dfrq 
 getparam('waltzB1','H1'):waltzB1 
 
 getparam('gt5','N15'):gt5
 getparam('gt0','N15'):gt0
 getparam('gzlvl5','N15'):gzlvl5
 getparam('gzlvl0','N15'):gzlvl0

 dofCO=dof 
 dof=dof-(174-43)*dfrq
  
//Set constant 13C t1 time to no

 CT_c='n'
 TROSY='n' 
 dm3='nyn'
 f1180='n'
 f2180='n'

echo('generating 13C pulse shapes and setting power levels\n')
//Create shapes for 13C, rounding pulse widths to 1usec
//set those shapes, amplitude adjusted internally by Pbox!!!!

/* setup q5 shapes for CACB */
//90 degrees
opx
pbox_rst
pboxpar('ref_pwr', pwClvl) pboxpar('ref_pw90', pwC*compC)
pboxpar('attn','i')  pboxpar('reps', '0') pboxpar('stepsize',0.5) 
shCACB90 ='et_CACB90'  format(320.0*600.0/sfrq,0,0):pwCACB90 
if(pwCACB90<280.0) then  format(280.0,0,0):pwCACB90 endif
$pw='' format(pwCACB90*1e-6,7,7):$pw
pboxpar('name',shCACB90)
$wave1 = 'Q5'+ ' /'+$pw 
setwave($wave1)
shell('Pbox')
$pwr=shCACB90+'.RF' pbox_pwr($pwr):pwCACB90pwr 
pwCACB90_phase_roll=0 //phase roll multiplier

////180 degrees on CACB
opx
pboxpar('ref_pwr', pwClvl) pboxpar('ref_pw90', pwC*compC)
pboxpar('attn','i')  pboxpar('reps', '0') pboxpar('stepsize',0.5) 
shCACB180='et_CACB180' 
format(200.0*600.0/sfrq,0,0):pwCACB180  

//if we don'have enough power at very high fields
// q3 pulse may not be suitable

if(pwCACB180<180.0) then  format(180.0,0,0):pwCACB180 endif
$pw='' format(pwCACB180*1e-6,7,7):$pw pboxpar('name',shCACB180)
$wave1 = 'q3'+ ' /'+$pw 
setwave($wave1)
shell('Pbox')
$pwr=shCACB180+'.RF' pbox_pwr($pwr):pwCACB180pwr 

/// 90 COs
opx
pboxpar('ref_pwr', pwClvl) pboxpar('ref_pw90', pwC*compC)
pboxpar('attn','i')  pboxpar('reps', '0') pboxpar('stepsize',0.5) 
shCO90 ='et_CO90'  format(150.0*600.0/sfrq,0,0):pwCO90  $pw='' format(pwCO90*1e-6,7,7):$pw
pboxpar('name',shCO90)
$wave1 = 'gaus'+ ' /'+$pw 
setwave($wave1)
shell('Pbox')
$pwr=shCO90+'.RF' pbox_pwr($pwr):pwCO90pwr
pwCO90_phase_roll=0.535 //for a 90-degree gaussian pulse phase roll is equivavlent of 0.535*pw

//180 CO
opx
pboxpar('ref_pwr', pwClvl) pboxpar('ref_pw90', pwC*compC)
pboxpar('attn','i')  pboxpar('reps', '0') pboxpar('stepsize',0.5) 
shCO180  ='et_CO180'  format(128.0*600.0/sfrq,0,0):pwCO180  $pw='' format(pwCO180*1e-6,7,7):$pw
pboxpar('name',shCO180)
$wave1 = 'gaus'+ ' /'+$pw +' 0 0 0 180'
setwave($wave1)
shell('Pbox')
$pwr=shCO180+'.RF' pbox_pwr($pwr):pwCO180pwr

//180 CO shifted
opx
pboxpar('ref_pwr', pwClvl) pboxpar('ref_pw90', pwC*compC)
pboxpar('attn','i')  pboxpar('reps', '0') pboxpar('stepsize',0.5) 
$freq=dofCO-dof $offset='' format($freq,9,1):$offset
shCO180offCACB  ='et_CO180offCACB'  
format(128.0*600.0/sfrq,0,0):pwCO180 
$pw='' format(pwCO180*1e-6,7,7):$pw
pboxpar('name',shCO180offCACB)
$wave1 = 'gaus'+ ' /'+$pw +$offset+' 0 0 180 '
setwave($wave1)
shell('Pbox')

//Shaka's broadband pulse at full power

shCBIP='et_CBIP_hncoca_cb' 
format(1382.0/90.0*pwC+0.5,0,0):pwCBIP
$nsteps=0
format(2.0*pwCBIP+0.5,0,0):$nsteps //echo('number of bip shape steps  is ',$nsteps)
$bipfile=userdir+'/shapelib/'+shCBIP+'.RF'

//echo ('bip shape file is',shCBIP,'\n')
 
  write('reset',$bipfile)
  write('file',$bipfile,'##SHAKA\'S BROADBAND INVERSION PULSE')
  write('file',$bipfile,'##BIP 250-15-1382')
  write('file',$bipfile,'##250 percent B1 inversion profile')
  write('file',$bipfile,'##15 percent B1 inhomogenity')
  write('file',$bipfile,'##set pw to 1/(4B1)*1382/90=pw90*1382/90')
  write('file',$bipfile,'##i.e. if B1=20kHz, then pwC=12.5   and pwBIP=1382/90*12.5 =192us')
  write('file',$bipfile,'##JMR v151, pp. 269-283, 2001')
  write('file',$bipfile,'##generated by macro')
  
//// ACTUAL BIP PULSE SHAPE GENERATION  
$i=1
repeat
    
    $t=-10.0+20.0*($i+1.0)/$nsteps
    $t=sqrt($t*$t)
    
    if($t<10.0)   then  $a0=-83165.75  $a1=27391.088  $a2=-3000.4957  $a3=110.56624  endif 
    
    if($t<9.3717) then  $a0=-12358.11 $a1=4472.53    $a2=-528.0852   $a3=21.67046   endif

    if($t<8.8482) then  $a0=-2126.58  $a1=812.761    $a2=-92.9098    $a3=4.46419    endif

    if($t<7.801)  then  $a0=65.09     $a1=-25.3       $a2=13.9075     $a3=-0.07389  endif

    if($t<5.1832) then  $a0=9.68      $a1=-6.109      $a2=12.6895     $a3=-0.15535  endif

    if($t<2.5654) then  $a0=0         $a1=0           $a2=12.3388     $a3=-0.3736   endif

    $phase= $a0 + $a1*$t + $a2*$t*$t + $a3*$t*$t*$t    
    $phase= (($phase*10.0) %3600)/10.0
    $ppp='' format($phase,1,1):$ppp
    write('file',$bipfile, $ppp+'  1023.0    1.0   1')
    $i=$i+1
until $i>$nsteps
//// END BIP Generation 
 
 echo('13C shapes done\n')

ni=0  ni2=0 phase=1 phase2=1
