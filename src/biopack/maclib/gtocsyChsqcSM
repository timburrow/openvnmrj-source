"macro gtocsyChsqcSM"

"check to see if BioPack is installed"
 exists('probe','parameter','global'):$e
 if(probe='') then $e=0 endif
 exists(userdir+'/probes/'+probe,'file'):$e
 if($e) then
   lookup('file',userdir+'/probes/'+probe+'/'+probe)
   lookup('count','C13compC'):$e  
 endif 

 if not $e then
   rtp(userdir+'/parlib/gtocsyChsqcSM')
   banner('all params must be set manually')
 else   
   BPrtppar('gtocsyChsqcSM')
   setfrq BPsetampmode

   if (numrfch>3) then
    getparam('dof3D','H2'):dof3
    getparam('dmm3D','H2'):dmm3
    getparam('dmf3D','H2'):dmf3
    getparam('dpwr3D','H2'):dpwr3
    getparam('dres3D','H2'):dres3
    getparam('dseq3D','H2'):dseq3
   else
     exists('ampmode','parameter'):$e
     if ($e=1) then destroy('ampmode') endif
   endif

   reffrq=sfrq-(sw/2-rfl+rfp)/1e6
    getparam('NHgstab','N15'):gstab
    getparam('gt5','N15'):gt5 gt6=gt5
    getparam('gzlvl5','N15'):gzlvl5 gzlvl6=0.85*gzlvl5
    getparam('CHgrad1_time','C13'):gt1
    gt2=gt1*0.25
    getparam('CHgrad1_lvl','C13'):gzlvl1
    getparam('CHgrad2_lvl','C13'):gzlvl2

    "for simultanous decoupling during acquisition recall"
    "pwC for 3db down in power"
    getparam('pwC3db','C13'):pwClw
    pwNlw=1.4*compN*pwN "reduce power because of simultaneous pulses in t1 evolution"
    dof=dof-(174-100)*dfrq

   BPsetwurstparams
   if dmm<>'cccp' then
     dpwr=pwClvl-15 dmf=1e6/(2*2*1.4*pwC*compC)
     BPcheckdpwr
   endif
endif

sw1=sw 
sw2=200*dfrq
dm2='nnnn' dmm2='cccc'
dm='nnny' wudec='n'
BPspinlock(1)
CNrefoc='n'

spin='n' ni=0  ni2=0 phase=1 phase2=1

wnt='' wexp='' 

dps
man('gtocsyChsqcSM')

