"macro BPmakestC50_5r"
"BPmakestC50_5r - makes sech-amplitude/tanh-frequency swept pulse with 5 u sec   "
"		 steps, 50 ppm bandwidth, and 5.0 millisec pulselength         "
" 	           to invert all C13 nuclei to the right of 44 ppm	       "
"           In macro, units are kHz for bandwidth and ms for pulselength       "

"         		R. Bendall, JCU, Jan 1998		               "

"  The pulse is designed to flip all C13 nuclei from the CO region to 0 ppm    "
"  The output is a file in your personal shapelib named stC50_5r.RF	       "

"From Bendall, JMR A116, 46 (1995) and STUD papers:			       "
"   Using Pulsetool RFmax was determined for average -0.995 inversion across   "
"   the effecive bandwidth for Tp = 5.0 ms.				       "

"   Using these values, bwdth/(RFmax)^2 = 1.65   at bwdth = 12 kHz             "
"                                       = 1.77   at bwdth = 5  kHz.            "
"   		So use 1.65 where bwdth and RFmax are in kHz	  	       "

"   For a 5.0ms pulse length, inversion changes from -0.8 to 0.8 in 490 Hz     "
"   for bwdth=5kHz.  Slightly better selectivity at lower bwdth, slightly      "
"   worse at higher in absolute frequency units (Hz not ppm), so							       "
"			 Tp = $t = 5.0 ms  below. 			       "
"      effective bandwidth(for -0.8 inversion) = bwdth - 0.47 kHz              "
"                           effective bandwidth = 7.5*sfrq/600                 "
"      so,                bwdth = (7.5*sfrq/600) + 0.47 = $b below             "

"   At 600 MHz use same inversion midpoint as 3ms stC50_3r pulse. Thus bwdth/2 "
"   will be at the same place as stC50_3l relative to dof at 56ppm and so at   "
"                    (-56 + 50)*dfrq - 430 - bwdth*1000/2                      "
"  This now makes the 50ppm point 3% inversion overall for two pulses combined."

"   In pulse sequences, bwdth/(RFmax)^2 = 1.65 so 			       "
"			   RFmax = sqrt[(7.5*sfrq/600+0.47)/1.65] in kHz       "

$a = 5.2982937*2.0    "gives an amplitude threshold of 0.01, ie sech($a/2)=0.01"

$file=userdir+'/shapelib/temp.RF'
shell('rm -f',$file):$dummy
write('line3','Writing data to %s...',$file)
write('reset',$file)

$i=1   repeat
$x=$a*(-0.5 + ($i-1)/(1000-1))   exp($x):$y   $z=($y + (1/$y))/2.0  ln($z):$w
$amp = 1023.0/$z     $amp = trunc($amp + 0.5)

$b = ((7.5*sfrq/600.0)+0.47)   $t = 5.0   "bandwidth and pulselength as above"

$p = 360.0*$b*$t*$w/(2.0*$a)    $phase = (trunc(2.0*$p + 0.5))/2.0
write('file',$file,'%5.1f\t%6.1f\t1.0',$phase,$amp)
$i=$i+1   until $i>1000

$freq = (-56.0 + 50.0)*dfrq - 430 - $b*1000.0/2.0
BPconvolute('temp','stC50_5r',5000,$freq)

BPmoveshape('stC50_5r.RF')
shell('rm -f',$file):$dummy
write('line3','The SLP sech/tanh pulse "shapelib/stC50_5r.RF" has been created')
