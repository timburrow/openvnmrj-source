"macro BPN15_T1"
"BPN15_T1.  A macro to set up relaxation parameters"
        clear
        ncyc=2
        $maxpoints=20
        $npoints=1 
        $min_ncyc_step=1s
        $min_T_step=2.0*(4.0*tau_cp)          // minimum relaxT step 
        $max_T_step=0.6        // relaxT step is 600ms max

	write('line3','setting nonlinear array of relaxation delays  up to %.f s, with two repeats',relaxTmax )

        $curr_ncyc=1 $max_ncyc=1
        format(relaxTmax/tau_cp/4.0,1,0):$max_ncyc

	write('alpha','max ncyc is  %d. Generating ncycs... ',$max_ncyc)     
	
        $curr_ncyc=0
        $i=1        
        $curr_T=0.0
	$ncyc_arr[$i] = $curr_ncyc
        $shuffled[$i]= $curr_ncyc

        write('alpha','index %d ncyc is %d relaxT is  %.3f',$i,  $ncyc_arr[$i],$curr_T)
        $i=2
	while ( ($i<$maxpoints) and  ($curr_ncyc < $max_ncyc)   ) do
          $T_step= $min_T_step + ($max_T_step/relaxTmax)*$curr_T // step size goes linear of Trelax
          if ($T_step > $max_T_step) then $T_step = $max_T_step endif      
          $curr_T=$curr_T + $T_step
          format($curr_T/4.0/tau_cp,0,0):$curr_ncyc
          if (  relaxTmax - $curr_T  <  $T_step ) then $curr_ncyc = $max_ncyc endif
          $ncyc_arr[$i] = $curr_ncyc $shuffled[$i] = $curr_ncyc
          $curr_T=$curr_ncyc*4.0*tau_cp
          write('alpha','--index %d ncyc is %d relaxT is  %.3f step is %.3f sec %d',$i,  $ncyc_arr[$i],$curr_T,$T_step,$curr_ncyc)
          $i=$i+1
        endwhile

 // reshuffle array
    write('alpha','reshuffling and adding repeats')
     $j=1 
     while ($j<= $i/2) do
      $shuffled[$j*2]=$ncyc_arr[$j]
      $shuffled[$j*2-1]=$ncyc_arr[$i-$j]
      $j=$j+1
     endwhile
     
     $j=1 
     while ($j< $i) do
      write('alpha','>>index %d ncyc is %d relaxT is  %.3f',$j, $shuffled[$j],$shuffled[$j]*4.0*tau_cp)
      ncyc[$j]=$shuffled[$j]
      $j=$j+1
     endwhile

     // add two repeats
     ncyc=$shuffled[2],$shuffled[$j/2],ncyc

