"macro BP_PRtime"
"BP_PRtime  A macro to calculate time of full PR experiment"

$time=''
substr(seqfil,1,4):$type if $type='PR42' then $1=PRvar1 $2=PRvar2 endif
                         if $type='PR43' then $1=PRvar1 endif

$nt=nt $ni=ni $phase=phase $phase2=phase2    "memorize values"

if $type='PR42' then
 $angle1={PRvar1} $angle2={PRvar2}
 {PRvar1}=90.0 {PRvar2}=90.0 phase=1 phase2=1,2
 nt=PR_ntN ni=PR_niN
 exptime:$time format($time,6,0):$NH_secs
 echo('NH_secs=',$NH_secs)

 {PRvar1}=0.0 {PRvar2}=90.0 phase=1,3 phase2=1
 nt=PR_ntorthog1 ni=PR_ni2
 exptime:$time format($time,6,0):$orthog_secs2
 echo('orthog_secs2=',$orthog_secs2)

 {PRvar1}=90.0 {PRvar2}=0.0 phase=1,2 phase2=1
 nt=PR_ntorthog2 ni=PR_ni3
 exptime:$time format($time,6,0):$orthog_secs3
 echo('orthog_secs3=',$orthog_secs3)

 $size1=size(PR_arrayname1)
 $size2=size(PR_arrayname2)
 {PRvar1}={PR_arrayname1}[$size1]  "last tilted plane"
 {PRvar2}={PR_arrayname2}[$size2]  "last tilted plane"
 phase=1,2,3,4 phase2=1,2
 nt=PR_ntTILT ni=PR_niTilt
 exptime:$time format($time,6,0):$tilted_secs
 echo('tilted_secs=',$tilted_secs)

 nt=$nt ni=$ni {PRvar1}=$angle1 {PRvar2}=$angle2     "restore values"
 phase=$phase phase2=$phase2

 if PR_skipPlanes='y' then
  $totaltime=(PR_nTilts-3)*$tilted_secs
 else                          "doing orthogonal planes"
  $totaltime=(PR_nTilts-3)*$tilted_secs +$orthog_secs2 + $orthog_secs3 +$NH_secs
 endif
endif

if $type='PR43' then
 $angle1={PRvar1}

 {PRvar1}=90.0 phase=1,3 phase2=1,2
 nt=PR_ntN ni=PR_niN
 exptime:$time format($time,6,0):$NH3D_secs

 {PRvar1}=0.0 phase=1,2 phase2=1,2
 nt=PR_ntorthog1 ni=PR_niTilt          
 exptime:$time format($time,6,0):$orthog3D_secs

 phase=1,2,3,4 phase2=1,2
 nt=PR_ntTILT ni=PR_niTilt
 exptime:$time format($time,6,0):$tilted3D_secs

 nt=$nt ni=$ni {PRvar1}=$angle1     "restore values"
 phase=$phase phase2=$phase2

 if PR_skipPlanes='y' then
  $totaltime=(PR_nTilts-3)*$tilted3D_secs
 else                        
  $totaltime=(PR_nTilts-3)*$tilted3D_secs+ $orthog3D_secs +$NH3D_secs
 endif
endif

echo($totaltime)
format($totaltime,8,0):$real
$hrs=$real/3600
$hr=trunc($hrs)
$min=trunc(($hrs-$hr)*60)
return($hr,$min)
