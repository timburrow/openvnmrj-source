"macro BPmakestC80"
"BPmakestC80 - makes sech-amplitude/tanh-frequency swept pulse with 2 microsec  "
"	                steps, 80 ppm bandwidth, and 1.0 millisec pulselength  "
"           In macro, units are kHz for bandwidth and ms for pulselength       "

"         		R. Bendall, JCU, Jan 1998		               "

"  The pulse is designed to flip all C13 nuclei from the CO region to 0 ppm    "
"  The output is a file in your personal shapelib named stC80.RF	       "

"From Bendall, JMR A116, 46 (1995) and STUD papers:			       "
"   Using Pulsetool RFmax was determined for average -0.995 inversion across   "
"   the effecive bandwidth for Tp = 1.0 ms.				       "

"   Using these values, bwdth/Tp(RFmax)^2 = 0.35   at bwdth = 25 kHz           "
"                                         = 0.45   at bwdth = 10 kHz.          "
"   So use 0.35 as for the stC200 pulse.		       "

"   A 1.0ms pulse length was determined suitable for the stC200 pulse so       "
"			 Tp = $t = 1.0 ms  below. 			       "
"      effective bandwidth(for -0.95 inversion) = bwdth - 3.85 kHz             "
"                           effective bandwidth = 12.07*sfrq/600               "
"      so,                bwdth = (12.07*sfrq/600) + 3.85 = $b below           "

"   This ensures that at 400 MHz there is 0.95 retention, or 2.5% inversion at "
"   113.5ppm if dof=35ppm, so aromatics are not inverted.  There is even better"
"   selectivity at higher field strength.				       "

"   In pulse sequences, bwdth/(RFmax)^2 = 0.35 so 			       "
"			   RFmax = sqrt[(12.07*sfrq/600+3.85)/0.35]            "

"   For 500, 600, and 800 MHz systems, reduced constant, Jr, for stC200 varies "
"   from 0.92J on resonance, to 0.89J at s=+/-0.5, to 0.83 at s=+/-0.8.  For   "
"   the stC80 pulse at 600 the nos. are 0.89J, 0.86J and 0.80J and for stC30   "
"   they are 0.87J, 0.84J and 0.78J.  So in pulse sequences assume Jr = 0.86J  "
"   as a compromise and 140us extra J delay required for each 1.0ms sech/tanh  "
"   pulse.								       "

$a = 5.2982937*2.0    "gives an amplitude threshold of 0.01, ie sech($a/2)=0.01"

$file=userdir+'/shapelib/stC80.RF'
shell('rm -f',$file):$dummy
write('line3','Writing data to %s...',$file)
write('reset',$file)

$i=1   repeat
$x=$a*(-0.5 + ($i-1)/(500-1))   exp($x):$y   $z=($y + (1/$y))/2.0   ln($z):$w
$amp = 1023.0/$z     $amp = trunc($amp + 0.5)

$b = ((12.07*sfrq/600.0)+3.85)   $t = 1.0   "bandwidth and pulselength as above"

$p = 360.0*$b*$t*$w/(2.0*$a)    $phase = (trunc(2.0*$p + 0.5))/2.0
write('file',$file,'%5.1f\t%6.1f\t1.0',$phase,$amp)
$i=$i+1   until $i>500

BPmoveshape('stC80.RF')
write('line3','Done!')   
