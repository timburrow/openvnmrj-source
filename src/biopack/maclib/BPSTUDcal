"macro BPSTUDcal"
" BP/STUDcal"

         "Calculates parameters for optimum efficiency STUD+ decoupling"
		            " Robin Bendall Jan 1998"
			" Bendall and Skinner, JMR submitted"

$MSB= (100.0 - r2)/5.7     				"maximum sideband in %"

$b = 1.3 + (0.8/$MSB) + (1.005 + (0.008/$MSB))*r1 			"bwdth"

$tp = $MSB/0.72  $tp = sqrt($tp)  $tp=2.0*$tp   $tp=(trunc($tp*10.0+0.5))/10.0
$tp = $tp/2.0 			        "sech/tanh pulse length to +/- 0.05 ms"

$b1 = 0.235/(1.0 + 0.16*$b)   $b2 = ((1.0/($b1*$b1)) - 1.0)    $b2 = sqrt($b2)
$b2 = $b2 + (1.0/$b1)		     " sech^(-1)y = log(1/y + sqrt(1/y*y - 1) "
ln($b2):$be		     		       "beta truncation factor"

$rf = ($be*$b)/(5.3*$tp)    $rf = sqrt($rf)
r3 = 1000.0*$rf 	        "RFmax in Hz to be read by pulse sequence code"

$ni = 2.5*$tp*$b
if ($ni < 125) then $ni=125 else if ($ni<250) then $ni=250 else $ni=500 endif
endif						"number of increments in pulse"

$a = $be*2.0                        "gives an amplitude threshold = sech($a/2)"

r4 = 1000.0*$ni/($tp*90.0)						" dmf "

$i=1   repeat
$x=$a*(-0.5 + ($i-1)/($ni-1.0))  exp($x):$y  $z=($y + (1/$y))/2.0  ln($z):$w
$amp[$i] = 1023.0/$z     $amp[$i] = trunc($amp[$i] + 0.5)
$p[$i] = 360.0*$b*$tp*$w/(2.0*$a)
$i=$i+1   until $i>$ni

$file=n1 
exists($file,'file'):$e
if $e then
 shell('rm -f',$file):$dummy
endif
write('line3','Writing data to %s...',$file)
write('reset',$file)

$q= 0.0, 150.0, 60.0, 150.0, 0.0   $r = 0.0, 0.0, 180.0, 180.0
$k=1   repeat
 $j=1   repeat
  $i=1   repeat
  $phase = $q[$j] + $r[$k] + (trunc(2.0*$p[$i] + 0.5))/2.0
  write('file',$file,'1.0\t%5.1f\t%6.1f',$phase,$amp[$i])
  $i=$i+1 until $i>$ni
 $j=$j+1  until $j>5
$k=$k+1  until $k>4

$t= 0.0, 15.0, 180.0, 165.0, 270.0, 165.0, 180.0, 15.0, 0.0
$r = 0.0, 0.0, 180.0, 180.0
$k=1   repeat
 $j=1   repeat
  $i=1   repeat
  $phase = $t[$j] + $r[$k] + (trunc(2.0*$p[$i] + 0.5))/2.0
  write('file',$file,'1.0\t%5.1f\t%6.1f',$phase,$amp[$i])
  $i=$i+1 until $i>$ni
 $j=$j+1  until $j>9
$k=$k+1  until $k>4

$s= 0.0, 330.0, 60.0, 330.0, 0.0   $r = 0.0, 0.0, 180.0, 180.0
$k=1   repeat
 $j=1   repeat
  $i=1   repeat
  $phase = $s[$j] + $r[$k] + (trunc(2.0*$p[$i] + 0.5))/2.0
  write('file',$file,'1.0\t%5.1f\t%6.1f',$phase,$amp[$i])
  $i=$i+1 until $i>$ni
 $j=$j+1  until $j>5
$k=$k+1  until $k>4

$u= 0.0, 195.0, 180.0, 345.0, 270.0, 345.0, 180.0, 195.0, 0.0
$r = 0.0, 0.0, 180.0, 180.0
$k=1   repeat
 $j=1   repeat
  $i=1   repeat
  $phase = $u[$j] + $r[$k] + (trunc(2.0*$p[$i] + 0.5))/2.0
  write('file',$file,'1.0\t%5.1f\t%6.1f',$phase,$amp[$i])
  $i=$i+1 until $i>$ni
 $j=$j+1  until $j>9
$k=$k+1  until $k>4
