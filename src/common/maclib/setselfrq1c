"macro setselfrq1c"
"**********************************************************************"
" setselfrqc - sets selective frequency for selective excitation       "
"		Used in glide after tocsy1d and noesy1d selection      "
"		Selected frequencies and widths are stored in the      "
"		parameters selfrqc and selfrqw as arrays to be used    "
"		by suselfrq macro			               "
"**********************************************************************"

$crmode=1
if (crmode='c') then $crmode=0 endif

if ($# = 2) then
 $cr=$1 $delta=$2
else
 if ($crmode=0) then
   write('error','You MUST use 2 cursors - Please make selection again')
   crmode='b'
   return
 endif
    $delta=delta1
    $cr=cr1-($delta/2)
endif

$rflrfp = rfl1-rfp1
$center = sw1/2-$rflrfp
if (refsource1='dfrq') then
   $selfrqc  = dof+$cr-$center
elseif (refsource1 = 'dfrq2') then
   $selfrqc = dof2+$cr-$center
elseif (refsource1 = 'dfrq3') then
   $selfrqc = dof3+$cr-$center
else
   $selfrqc = tof+$cr-$center
endif

write('line3','The band [width: %6.1f] centered at %4.1f ppm selected',$delta,$cr/reffrq1)

on('selfrq1c'):$selfrqcon
if ($selfrqcon) and (customflag='y') then
 selfrq1c = selfrq1c,$selfrqc
 selfrq1w = selfrq1w,$delta
else
 selfrq1c = $selfrqc
 selfrq1w = $delta
endif

showfreq('selfrq1c')
