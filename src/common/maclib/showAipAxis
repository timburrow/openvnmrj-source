"macro showAipAxis"
// aipAxis[1] = 0 or 1, hide or show axises. If 0, the rest of aipAxis values are ignored.
// aipAxis[2] = 0 or 1, hide or show axis on west
// aipAxis[3] = 0 or 1, hide or show axis on east
// aipAxis[4] = 0 or 1, hide or show axis on south
// aipAxis[5] = 0 or 1, hide or show axis on north
// aipAxis[6] = 0 or 1, ticks in or out
// aipAxis[7] = 0 or 1, show or hide crosshair in Point mode.
// aipAxis[8] = 0 use frame borders as boundary, 1 use image FOV as boundary
// aipAxis = 0,1,1,1,1,0,1,1 (default)

// aipAxisGeom[1] = 0.0 to 1.0, fraction of image or frame width as size of horizontal axis.
// aipAxisGeom[2] = 0.0 to 1.0, fraction of image or frame height as size of vertical axis.
// aipAxisGeom[3] = 0.0 to 1.0, fraction of image or frame width as X offset of west axis
// aipAxisGeom[4] = 0.0 to 1.0, fraction of image or frame height as Y offset of west axis
// aipAxisGeom[5] = 0.0 to 1.0, fraction of image or frame width as X offset of east axis
// aipAxisGeom[6] = 0.0 to 1.0, fraction of image or frame height as Y offset of east axis
// aipAxisGeom[7] = 0.0 to 1.0, fraction of image or frame width as X offset of south axis
// aipAxisGeom[8] = 0.0 to 1.0, fraction of image or frame height as Y offset of south axis
// aipAxisGeom[9] = 0.0 to 1.0, fraction of image or frame width as X offset of north axis
// aipAxisGeom[10] = 0.0 to 1.0, fraction of image or frame height as Y offset of north axis
// aipAxisGeom = 1,1,0,0,0,0,0,0,0,0 (default)

$fov=aipAxis[8] // preserve flag for boundary 

if(aipAxis[1]=1 and aipAxis[2]=1 and aipAxis[3]=1 and aipAxis[4]=0 and aipAxis[5]=0 and aipAxisGeom[1]=0.6 and aipAxisGeom[2]=0.6) then
  aipAxisGeom=1,1,0,0,0,0,0,0,0,0 aipAxis=0,1,1,1,1,0,1,$fov
elseif(aipAxis[1]=1 and aipAxis[2]=1 and aipAxis[3]=1 and aipAxis[4]=1 and aipAxis[5]=1 and aipAxisGeom[1]=1 and aipAxisGeom[2]=1) then
  aipAxisGeom=0.6,0.6,0.04,0.0,-0.04,0.0,0.0,-0.04,0.0,0.04 aipAxis=1,1,1,0,0,0,0,$fov
else
  aipAxisGeom=1,1,0,0,0,0,0,0,0,0 aipAxis=1,1,1,1,1,0,1,$fov
endif

aipDisplay(`redisplay`)
