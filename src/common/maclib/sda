"macro sda"

"*********************"
" sda - set decoupler  array "
" sd2a - set second decoupler   array"
" sd3a - set third decoupler   array"
"*********************"
if ($0 = 'sda') then
   $dofx='dof'
   $dnx='dn'
   $tmpdof=dof[1]		"save the first dof value"
   dof[1]=tof
   $dfrq=dfrq
   $decup='first'
else if ($0 = 'sd2a') then
   $dofx='dof2'
   $dnx='dn2'
   $tmpdof=dof2[1]		"save the first dof2 value"
   dof2[1]=tof
   $dfrq=dfrq2
   $decup='second'
else if ($0 = 'sd3a') then
   $dofx='dof3'
   $dnx='dn3'
   $tmpdof=dof3[1]		"save the first dof3 value"
   dof3[1]=tof
   $dfrq=dfrq3
   $decup='third'
else
  write('error','macro sda does not support %s',$0)
  ABORT
endif
endif
endif

exists($dnx,'parameter'):$e
if ($e = 0) then
   write('error','Parameters for %s decoupler do not exist',$decup)
   return
endif

if (tn<>{$dnx}) then
  write('error','Cannot set decoupler this way in heteronuclear experiment')
  return
  endif

{$dofx}[1]=$tmpdof			"Restore first dof value"

"*** calculate Frequency of cursor ***"
" (sfrq*1.0e6 - sw/2) - 	Frequency at far right in Hz"
" (rfl-rfp) - 			referencing"
$abscr=(sfrq*1.0e6 - sw/2) + cr + (rfl-rfp)	"cursor frequency"

"*** Now time to calc dof setting ***"
$dofsize = size($dofx)

{$dofx}[$dofsize+1] = $abscr - $dfrq*1e6 + tof  "set additional dof"
axis('fn'):$label,$frq_scl
write('line3','%s[%d] set to %1.1f, decoupling at %1.2f %s',$dofx,$dofsize+1,{$dofx}[$dofsize+1],cr/$frq_scl,$label)
