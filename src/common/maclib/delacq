"macro delacq"
"delacq: set up delayed acquisition - useful for F19 experiment"

" Usage: delacq<(delay|number_of_points)>, where the delay can "
" specified in usec, or the number of points entered directly. "
" The default is to adjust the number of points to produce a "
" delay of about 100 usec (delaying by an integral multiple of "
" the dwell time). "

if (Console='vnmrs') then return endif
$background='yes'
getparam('background',tn):$background
if ($background='no') then return endif

  if ($# < 1) then
    $ndwell = trunc((1e-4*sw) + 0.5)
  " number of dwell times required to produce approx. 100 usec delay "

  else
    if ($1 < 20) then
      $ndwell = $1
      " assumes that a delay of < 20 usec will never be needed "
    else
      $ndwell = trunc(($1/1e6)*sw + 0.5)
      " $1 is delay after the pulse, in usec "
    endif
  endif

  if $ndwell < 1 then $ndwell = 1 endif

  alfa = 1e6*$ndwell/sw - rof2 

  lsfid = -1*$ndwell

parlp
lpopt='b'
lpfilt=32
lpnupts=128
strtlp=$ndwell+3
lpext=$ndwell+3
strtext=$ndwell+3
proc='lp'

  lp = 0
