"macro HTdec"
" *************************************************************  "
" ***                       HTdec                           ***  "
" ***   create a decoupling waveform for HT sequences       ***  "
" ***                    Eriks Kupce                        ***  "
" ***                Varian, 16/11/2004                     ***  "
" *************************************************************  "
" usage : HTdec(fname, pw, bw, Jxy, stepsize, ref_pw90, ref_pwr) "

if $#<6.5 then
  write('error','usage : HTdec(fname, pw, bw, Jxy, stepsize, ref_pw90, ref_pwr)')
  return(0)
endif

$su = 'n'
$stepsize = ''  
$pw=''
$bw=''
$ref_pwr=''
$ref_pw90=''

$fname = $1
$Tp = $2
format($3,3,1):$bw 
$j = $4
if ($5 < 0.05) then
  $stepsize = 'n'
else
  format($5,5,2):$stepsize
endif
$ref = $6
format($6,5,2):$ref_pw90
format($7,2,0):$ref_pwr
 
$nsu = 5.0*$j*$Tp

if ($nsu < 2.0) then $su = 'n' $n = 1.0
elseif ($nsu < 4.2) then $su = 'm4'  $n = 4.0
elseif ($nsu < 6.0) then $su = 't5'  $n = 5.0
elseif ($nsu < 10.0) then $su = 'm8'  $n = 8.0
elseif ($nsu < 17.0) then $su = 'm16'  $n = 16.0
elseif ($nsu < 21.0) then $su = 't5,m4'  $n = 20.0
elseif ($nsu < 27.0) then $su = 't5,t5'   $n = 25.0
elseif ($nsu < 45.0) then $su = 't5,m8'   $n = 40.0
elseif ($nsu < 85.0) then $su = 't5,m16'  $n = 80.0
elseif ($nsu < 110.0) then $su = 't5,t5,m4' $n = 100.0
else
  write('error','Use manual setup for %s', $fname)
  $su = 'n' $n = 1.0
  return(0)
endif

$Tp = $Tp/$n
format($Tp,1,7):$pw

$shapelib=userdir+'/shapelib'
exists($shapelib,'directory'):$e
if not($e) then
  mkdir($shapelib)
endif

" +--------------------------+ "
" | construct Pbox command   | "
" +--------------------------+ "
$cmd = 'Pbox "'+$fname+'" -v'

$wave = 'wurst2i ' + $pw + '/' + $bw 
$cmd = $cmd + ' -w "'+$wave+'" '

$cmd = $cmd + ' -s '+$stepsize
$cmd = $cmd + ' -sucyc '+$su

if $ref > 0.01 then
  $cmd = $cmd + ' -p '+$ref_pwr
  $cmd = $cmd + ' -l '+$ref_pw90
endif

shell($cmd) 
return(1)
