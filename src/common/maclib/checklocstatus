"macro checklocstatus"
$loc='' $status='' $who=operator $sampname='unknown'
$locdir=''
format($2,'lower'):$loc
substr($loc,1):$loc

$rack='' $zone='' $subtray=''
if (traymax=96) or (traymax=768) then
   format(vrack,'lower'):$rack
   format(vzone,'lower'):$zone
   $subtray='/RACK'+$rack+'ZONE'+$zone
   if ($# > 3) then
      format($3,'lower'):$zone
      format($4,'lower'):$rack
      $subtray='/RACK'+$rack+'ZONE'+$zone
   endif
endif
if ($rack='') or ($zone='') then $rack='' $zone='' $subtray='' endif

$spar=$1+'.macdir/studytray'+$subtray+'/loc'+$loc+'/studypar'
$locdir='loc'+$loc
exists($spar,'file'):$sparex
if ($sparex) then
	rtv($spar,'noabort','studystatus'):$status
	rtv($spar,'noabort','studyowner'):$who
	rtv($spar,'noabort','samplename'):$sampname
	rtv($spar,'noabort','locdir'):$locdir
endif
"******Adjust information if status is Active******"
if ($status='Active') and ($sampname<>'ReFeReNCe') then
	$who2='' $sampname2='' $locdir2=''
   $sg=$1+'.macdir/currentsampleglobal'
   $si=$1+'.macdir/currentsampleinfo'
   exists($sg,'file'):$fileex
   exists($si,'file'):$fileex2
   if ($fileex>0) and ($fileex2>0) then
	rtv($sg,'noabort','studyowner'):$who2
	rtv($sg,'noabort','samplename'):$sampname2
	$MACRO='' $macro=''
	shell('(cat '+$si+' | grep -w MACRO:)'):$MACRO
	substr($MACRO,2):$macro
	$s1='' $s2=''
	strstr($macro,'\''):$ret,$s1,$s2
	strstr($s2,'\''):$ret,$locdir2,$s1
	if ($locdir=$locdir2) then
		$who=$who2
		$sampname=$sampname2
		$locdir=$locdir2
	endif
   endif
endif
"************************************************"
return($status,$who,$sampname,$locdir,$sparex)
