"macro makeBIP_20_10_360"
/* generate BIP shape
   Evgeny Tishencko & Peter Sandor, Agilent 2010-14   */

if ($# < 3)  then
           
    write('alpha',' usage:  makeBIP_20_10_360(shapeName,referencePower,referencePulseWidth)' )
            return 
	  endif   
 
"make BIP pulse, name, reference power, reference pulsewidth"


//input parameters
$shCBIP=$1
$refClvl=$2
$refCpw=$3

//output file
 
 $bipfile=userdir+'/shapelib/'+$shCBIP+'.RF'
 
 
  write('alpha','generating Shaka\'s  BIP 20-10-360 pulse shape, JMR v151, pp. 269-283, 2001\n')
  write('alpha','+/-20 percent B1 inversion profile, 10 percent B1 inhomogenity')   
  write('alpha','set pw to 1/(4B1)*360/90=pw90*360/90')
  write('alpha','i.e. if B1=20kHz, then pwC=12.5   and pwBIP=360/90*12.5 =50us')

//Shaka's broadband pulse at full power 
 
format(360.0/90.0*$refCpw+0.5,0,0):$pwCBIP

$nsteps=0
format(2.0*$pwCBIP+0.5,0,0):$nsteps

echo('reference power ', $refClvl,'reference hard pulse width',$refCpw) 
echo('bip shape name  is ',$shCBIP)
echo('bip pulsewidth  is ',$pwCBIP)
echo('set bip power to',$refClvl)
echo('number of bip shape steps  is ',$nsteps)
 
  write('reset',$bipfile)
  write('file',$bipfile,'##   SHAKA\'S BROADBAND INVERSION PULSE')
  write('file',$bipfile,'##   BIP 20-10-360')
  write('file',$bipfile,'##   20 percent B1 inversion profile')
  write('file',$bipfile,'##   10 percent B1 inhomogenity')
  write('file',$bipfile,'##   set pw to 1/(4B1)*360/90=pw90*360/90')
  write('file',$bipfile,'##   i.e. if B1=20kHz, then pwC=12.5   and pwBIP=360/90*12.5 =50us')
  write('file',$bipfile,'##   JMR v151, pp. 269-283, 2001')
  write('file',$bipfile,'##   generated by macro makeBIP_20_10_360')
  write('file',$bipfile,'%s %d', '##       N steps',$nsteps)
  write('file',$bipfile,'%s %.1f','##       total pulsewdth',$pwCBIP)
  write('file',$bipfile,'%s %.3f','##       power corresponds to B1, kHz ',250.0/$refCpw)
  write('file',$bipfile,'%s %.1f','##       inversion width (kHz) ',2*0.2*250/$refCpw)
  write('file',$bipfile,'##   ')
  
//// ACTUAL BIP PULSE SHAPE GENERATION  
$i=0
repeat
    $i=$i+1
    $t=-10.0+20.0*$i/($nsteps+1)
    $t=sqrt($t*$t)
    
    if($t<10.0)   then  $a0=-684.02     $a1=184.608     $a2=-12.7523    $a3=0.25618    endif 
    
    if($t<8.5827) then  $a0=2468.838    $a1=-1070.420   $a2=151.2993    $a3=-6.80748   endif

    if($t<7.4016) then  $a0=1171.06     $a1=-479.512    $a2=62.6958     $a3=-2.42231   endif

    if($t<6.2205) then  $a0=-16.83      $a1=15.474      $a2=-4.3532     $a3=0.49946    endif

    if($t<3.0709) then  $a0=0           $a1=0           $a2=0.3704      $a3=0.02097    endif

    $phase= $a0 + $a1*$t + $a2*$t*$t + $a3*$t*$t*$t    
    $phase= (($phase*10.0) %3600)/10.0
    
    $ppp='' format($phase,1,1):$ppp
     
    write('file',$bipfile,' %6.1f   1023.0    1.0   1',$ppp)
    
until $i=$nsteps
 
return($shCBIP,$refClvl,$pwCBIP)


