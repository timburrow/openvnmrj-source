"macro vnmrjOptions"
" Installer for passworded options "

exists(systemdir+'/vnmrrev','file','rw'):$perm
$xmlfile=curexp+'/tmpapp.xml'
$logfile=systemdir+'/adm/log/options'
if ($# = 0) then
  destroy('psFile'):$e
  destroy('psLabel'):$e
  destroy('psPass'):$e

  $file=systemdir+'/adm/options/options'
  exists($file,'file'):$e
  if (not $e) then
    write('line3','No install packages are present. ')
    return
  endif

  create('psFile','string')
  create('psLabel','string')
  setgroup('psFile','display')
  setgroup('psLabel','display')
  psFile=''
  psLabel=''
  readfile($file,'psFile','psLabel',''):$num
  if ($num < 1 ) then
    write('line3','Cannot install packages.')
    destroy('psFile'):$e
    destroy('psLabel'):$e
    return
  endif
  create('psPass','string')
  setgroup('psPass','display')
  psPass=''
  exists($logfile,'file','rw'):$perm2
  if ($perm2) then
    lookup('mfile',$logfile,'filekey'):$key
  endif
  $i = 0
  while ($i < $num) do
    $i = $i + 1
    psPass[$i]= ''
    if ($perm2) then
      lookup('mfile',$key,'countcs',psFile[$i]):$found
      if ($found) then
        psPass[$i]='Installed'
      endif
    endif
  endwhile
  $ysize = 50 + $num*20
  $ys = ''
  format($ysize,0,0):$ys
  write('reset',$xmlfile)

  write('file',$xmlfile,`<?xml version="1.0" encoding="UTF-8" standalone="yes"?> \n\
<template name="Flags" element="pages" type="acquisition" >  \n\
  <group size="450 `+$ys+`"  \n\
    style="Heading2"  \n\
    label="Appdir"  \n\
    vq="panellevel"  \n\
    bg="transparent"  \n\
    border="None"  \n\
    side="Top"  \n\
    justify="Left"  \n\
    tab="yes"  \n\
    enable="no"  \n\
    reference="Flags"  \n\
    useref="no"  \n\
    subtype="Convenience"  \n\
    expanded="yes"  \n\
    >  \n\
    <label loc="15 20" size="160 20" \n\
      style="Heading3" \n\
      label="Option" \n\
      justify="Left" \n\
      /> \n\
   <label loc="300 20" size="180 20" \n\
      style="Heading3" \n\
      label="Option Key" \n\
      justify="Left" \n\
      /> \n`)
  $i = 0
  $yp=''
  $numch=''
  while ($i < $num) do
    $i=$i + 1
    $ypos = 30 + $i * 20
    format($ypos,0,0):$yp
    format($i,0,0):$numch
    if (psPass[$i]='Installed') then
      write('file',$xmlfile,`\n
    <label loc="300 `+$yp+`" size="125 20"
      style="PlainText"
      label="Installed"
      justify="Left"
      />
    `)
    elseif (not $perm) then
      write('file',$xmlfile,`\n
    <label loc="300 `+$yp+`" size="125 20"
      style="PlainText"
      label="Not Installed"
      justify="Left"
      />
    `)
    else
      write('file',$xmlfile,`\n
    <entry loc="300 `+$yp+`" size="125 20"
      style="PlainText"
      vc="`+$0+`('set',`+$numch+`,'$VALUE')"
      set="`+$0+`('get',`+$numch+`):$VALUE"
      disable="Grayed out"
      />
    `)
    endif
    write('file',$xmlfile,`\n
    <label loc="15 `+$yp+`" size="250 20"
      style="PlainText"
      label="`+psLabel[$i]+`"
      justify="Left"
      />
    `)
  endwhile
  write('file',$xmlfile,'  </group>\n</template>')
  
  vnmrjcmd('popup', 'mode:modal', 'file:'+$xmlfile, 'rebuild:yes', \
   'location:topMenuBar', 'help:/vnmr/jhelp/dialog/'+$0+'.html', \
   'pnewupdate:true', 'cancel:'+$0+'(`cancel`)', 'ok:'+$0+'(`ok`)', \
   'title:VnmrJ Options Installer')

elseif ($1 = 'set') then
    psPass[$2] = $3

elseif ($1 = 'get') then
  return(psPass[$2])

elseif ($1 = 'ok') then
  delete($xmlfile)
  $num=size('psPass')
  $i=0
  while ($i < $num) do
    $i=$i+1
    if (psPass[$i]<>'') and (psPass[$i]<>'Installed') then
      write('line3','Install %s',psLabel[$i])
      shell('vnmr_options '+psPass[$i]+' '+psFile[$i]):$ret
      if ($ret=0) then
         write('line3','%s installation completed',psLabel[$i])
         write('file',$logfile,'%s',psFile[$i])
      else
         write('error','%s installation failed',psLabel[$i])
      endif
    endif
  endwhile
  destroy('psFile'):$e
  destroy('psLabel'):$e
  destroy('psPass'):$e
elseif ($1 = 'cancel') then
  delete($xmlfile)
  destroy('psFile'):$e
  destroy('psLabel'):$e
  destroy('psPass'):$e
endif
