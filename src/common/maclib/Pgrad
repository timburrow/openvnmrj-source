"macro Pgrad"
"********************************************************"
"Pgrad - Carbon/proton G ratio Autocalibration       "
"********************************************************"

mkpar('string','execprocess','','execplot','')
setgroup('execprocess','display')
setgroup('execplot','display')

execprocess='wft dssh darray(\'CalibrationResults\')'
execplot='execplot=\'\''

if ($# < 1) then $1 = 1 endif

IF ($1 = 1) THEN

    $file='' $asdir=''
    cplog('PROTON','first'):$file,$asdir
    if ($file<>'') then rt($asdir+'/'+$file) else return endif
    wft
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    setref
    f

    getparam('tpwr',tn):tpwr
    getparam('pw90',tn):pw90
    getparam('pwxlvl',dn):pwxlvl
    getparam('pwx',dn):pwx
    getparam('gradient','Probe'):PFGflg
    pw=pw90
    dm='nnn' dmm='ccc'

    if (PFGflg = 'n') then
 	write('error','This probe is not a PFG probe.  Check probes table')
 	printon 
  	    write('alpha','This probe is not a PFG probe.')
  	    write('alpha','Check probes table')
  	    write('alpha','Carbon gradient calibration aborted')
 	printoff
 	return
    endif

    seqfil='gXHCAL'
    ds ph vsadj vp=10 dm='nnn' dmm='ccc'
    if (macro='P31_calibration') then
	setsw(4.5,10) setoffset(-23,dn):dof wp=2p sp=6p j1xh=1.1 d1=25
    else
	setsw(3,5) setoffset(27.5,dn):dof wp=1.5p sp=3p j1xh=10 d1=1
    endif

    fixgrd(30):gzlvl1
    $gradtype3=''
    substr(gradtype,3,1):$gradtype3
    if ($gradtype3<>'l') and (gzlvl1<25000) then
   	gzlvl1=25000
    endif

//    $ratio=3.98
    $ratio=sfrq/dfrq
"getparam('gHratio',dn):$ratio "
    gzlvl3=trunc(gzlvl1*2/$ratio + 0.5)
    gt1=.002 gt3=.001

    if ($gradtype3 = 'l') then
 	array('gzlvl3',26,gzlvl3-20,2)
    else
 	array('gzlvl3',26,gzlvl3-80,8)
    endif

    ai fn=8k at=.2 sinebell
    av
    wnt='wft(`acq`) select(1) vsadj(10) dssh'
    alock='n' wshim='n' r1=1
    caliblist=$0
    xmhaha_calib('synonym',$0):caliblist
    calibnode='_iteration'
    write('line3','_gzlvl1%d_posarray',gzlvl1):calibnode
    clear(2)
    xmhaha_calib('addNode','Pgrad(2)')

ENDIF

"********************************************************"

IF ($1 = 2) THEN
    cplog('gXHCAL','last'):$file,$asdir
    rt($asdir+'/'+$file)
    sinebell wft select(1) av 
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
    select(1)
    $counter=1
    $maxspec=1
    repeat
  	select($counter)
  	peak(sp,wp+sp):$ht
  	if $counter=1 then $maxht = $ht $gzlvl3=gzlvl3[1] 
	else
	    if $ht>$maxht then 
		$maxht=$ht $maxspec=$counter $gzlvl3=gzlvl3[$counter] 
     	    endif
  	endif
  	$counter=$counter+1
    until ($counter>arraydim)

    select($maxspec) vsadj(0.8*wc2max) vp=0.1*wc2max
    dssh
    r1=$gzlvl3
    $posratio=2*gzlvl1/r1 $strposratio=''
    format($posratio,0,5):$strposratio
    n1='P/H pos. gradient ratio: +'+$strposratio
    CalibrationResults=CalibrationResults,n1
    writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')
    atext(n1)
    execplot='cpplss'

    $diff=gzlvl3[1]-gzlvl3[2]
    $start=-gzlvl3[1]
    array('gzlvl3',arraydim,$start,$diff)
    write('line3','_gzlvl1%d_negarray',gzlvl1):calibnode
    execprocess='Pgrad(3)'

ENDIF

"********************************************************"

IF ($1 = 3) THEN

    sinebell wft select(1) av 
    select(1)
    $counter=1
    $maxspec=1
    repeat
  	select($counter)
  	peak(sp,wp+sp):$ht
  	if $counter=1 then $maxht = $ht $gzlvl3=gzlvl3[1] 
	else
     	    if $ht>$maxht then 
		$maxht=$ht $maxspec=$counter $gzlvl3=gzlvl3[$counter] 
     	    endif
    	endif
  	$counter=$counter+1
    until ($counter>arraydim)

    r2=-$gzlvl3
    $negratio=2*gzlvl1/r2 $strnegratio=''
    format($negratio,0,5):$strnegratio

    select($maxspec) vsadj(0.8*wc2max) vp=0.1*wc2max dssh
    n1='P/H neg. gradient ratio: -'+$strnegratio
    CalibrationResults=CalibrationResults,n1
    atext(n1)
    execplot='cpplss'

    execprocess='wft dssh darray(\'CalibrationResults\')'

    gzlvl3=(r1+r2)/2
    $ratio = 2*gzlvl1/gzlvl3
    $strratio=''
    format($ratio,0,5):$strratio
    setparams('gHratio',$strratio,dn)
    
ENDIF

"********************************************************"
