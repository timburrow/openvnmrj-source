"macro makeBIP_75_15_720"
/* generate BIP shape
   Evgeny Tishencko & Peter Sandor, Agilent 2010-14   */

if ($# < 3)  then
           
    write('alpha',' usage:  makeBIP(shapeName,referencePower,referencePulseWidth)' )
            return 
	  endif   
 
"make BIP pulse, name, reference power, reference pulsewidth"


//input parameters
$shCBIP=$1
$refClvl=$2
$refCpw=$3

//output file
 
 $bipfile=userdir+'/shapelib/'+$shCBIP+'.RF'
 
 
  write('alpha','generating Shaka\'s  BIP 75-15-720 pulse shape, JMR v151, pp. 269-283, 2001\n')
  write('alpha','+/-75 percent B1 inversion profile, 15 percent B1 inhomogenity')   
  write('alpha','set pw to 1/(4B1)*720/90=pw90*720/90')
  write('alpha','i.e. if B1=20kHz, then pwC=12.5   and pwBIP=720/90*12.5 =100us')
 


//Shaka's broadband pulse at full power 

 
format(720.0/90.0*$refCpw+0.5,0,0):$pwCBIP
 

$nsteps=0
format(2.0*$pwCBIP+0.5,0,0):$nsteps 

echo('reference power ', $refClvl,'reference hard pulse width',$refCpw) 
echo('bip shape name  is ',$shCBIP)
echo('bip pulsewidth  is ',$pwCBIP)
echo('set bip power to',$refClvl)
echo('number of bip shape steps  is ',$nsteps)
 
  write('reset',$bipfile)
  write('file',$bipfile,'##   SHAKA\'S BROADBAND INVERSION PULSE')
  write('file',$bipfile,'##   BIP 75-15-720')
  write('file',$bipfile,'##   75 percent B1 inversion profile')
  write('file',$bipfile,'##   15 percent B1 inhomogenity')
  write('file',$bipfile,'##   set pw to 1/(4B1)*720/90=pw90*720/90')
  write('file',$bipfile,'##   i.e. if B1=20kHz, then pwC=12.5   and pwBIP=720/90*12.5 =100us')
  write('file',$bipfile,'##   JMR v151, 269-283, 2001')
  write('file',$bipfile,'##   generated by macro makeBIP_75_15_720')
  write('file',$bipfile,'%s %d', '##       number of steps',$nsteps)
  write('file',$bipfile,'%s %.1f','##       total pulsewdth',$pwCBIP)
  write('file',$bipfile,'%s %d', '##       power level (dB)',$refClvl)
  write('file',$bipfile,'%s %.3f','##       power corresponds to B1, (kHz) ',250.0/$refCpw)
  write('file',$bipfile,'%s %.1f','##       inversion width (kHz) ',2*0.75*250/$refCpw)
  write('file',$bipfile,'##   ')
  
//// ACTUAL BIP PULSE SHAPE GENERATION  
$i=0
repeat
    $i=$i+1
    $t=-10.0+20.0*$i/($nsteps+1)
    $t=sqrt($t*$t)
    
    if($t<10.0)   then  $a0=92450.55   $a1=-29218.39   $a2=3078.6586   $a3=-107.7423  endif 
    
    if($t<9.6994) then  $a0=180224.27  $a1=-59270.107  $a2=6476.3165   $a3=-234.79515 endif

    if($t<9.3988) then  $a0=-2669.23   $a1=2481.179    $a2=-452.7443   $a3=23.67746   endif

    if($t<8.9980) then  $a0=-9053.38   $a1=3321.951    $a2=-403.0683   $a3=16.53544   endif

    if($t<7.9960) then  $a0=1190.22    $a1=-529.823    $a2=79.7074     $a3=-3.63460   endif

    if($t<5.9920) then  $a0=-35.81     $a1=26.687      $a2=-3.6014     $a3=0.46765    endif

    if($t<3.9880) then  $a0=11.08      $a1=-14.099     $a2=8.0081      $a3=-0.61827   endif

    if($t<1.9840) then  $a0=0.0        $a1=0.0         $a2=2.2393      $a3=0.12622    endif

    $phase= $a0 + $a1*$t + $a2*$t*$t + $a3*$t*$t*$t    
    $phase= (($phase*10.0) %3600)/10.0
    
    $ppp='' format($phase,1,1):$ppp

    write('file',$bipfile,' %6.1f   1023.0    1.0   1',$ppp)
    
until $i=$nsteps

return($shCBIP,$refClvl,$pwCBIP) 


