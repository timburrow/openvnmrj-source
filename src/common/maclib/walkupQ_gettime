"macro walkupQ_gettime"
"****************************************************************"
"   walkupQ_gettime - get total time from EXPLIST/LONGLIST    "
"****************************************************************"

"****The following are common for all walkupQ_xxxx macros*****"
        $expfile=cursqexp+'/EXPLIST'
        $lngfile=cursqexp+'/LONGLIST'
        $timefile=cursqexp+'/EXPTIME'
        $expdeledit=cursqexp+'/EXPdeledit'
        $lngdeledit=cursqexp+'/LONGdeledit'
        $curfidlist=cursqexp+'/FIDlist'
        $maclibdir=cursqexp+'/macdir'
        $longQ=longQ $resume=resumeQ
"****************************************************************"

   walkupQ('readlist'):$parflg,$parlng,$exp2,$lng2
   $parflgsize=0 $parlngsize=0
   if ($parflg[1]<>'') then $parflgsize=size('$parflg') endif
   if ($parlng[1]<>'') then $parlngsize=size('$parlng') endif
   if ($parflgsize=0) and ($parlngsize=0) then
      write('reset',$timefile)
      newdg
      return(0,0,0,0)
   endif
   exists($maclibdir+'/CMDnodes','file'):$cmdex
   $cmd='' $cmdx=''
   if ($cmdex) then
       readfile($maclibdir+'/CMDnodes','$cmd','$cmdx','','local')
   endif
   $procplottime=60
   getadminfo('prpltime'):$procplottime
   $overnite=0
   if ($parlngsize) then $overnite=1 endif

   $changetime=0 $sctime=''
   getPreAcqTime:$changetime,$sctime

   $i=1 $dtime=0 $dpptime=0
   $cmdct=0
   if ($parflg[1]<>'') then
     repeat
	$cmdex=0
	$rtime=0
	teststr('$cmd',$parflg[$i],'local'):$cmdex
	if ($cmdex) then $cmdct=$cmdct+1 endif
	substr($exp2[$i],1):$rtime
        $dtime=$dtime+$rtime
        $i=$i+1
     until $i > $parflgsize
     $dpptime=($parflgsize-$cmdct)*$procplottime
//     $dtime=$dtime+$dpptime+$changetime
     $dtime=$dtime+$dpptime
   endif

   $i=1 $ltime=0 $lpptime=0
   $cmdct=0
   if ($overnite) then
     repeat
        $cmdex=0
	$rtime=0
        teststr('$cmd',$parlng[$i],'local'):$cmdex
        if ($cmdex) then $cmdct=$cmdct+1 endif
	substr($lng2[$i],1):$rtime
	$ltime=$ltime+$rtime
        $i=$i+1
     until $i > $parlngsize
     $lpptime=($parlngsize-$cmdct)*$procplottime
//     $ltime=$ltime+$lpptime+$changetime
     $ltime=$ltime+$lpptime
   endif
   newdg
   return($dtime,$ltime,$dpptime,$lpptime)

"****************************************************************"
