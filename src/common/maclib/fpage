"macro fpage"
isnewexp:$isnew
exists('CPpage','maclib'):$cppex
if ($# < 1) then $1='' endif

if ($isnew) then
   write('line3','Current data is not saved yet. Cannot use fpage')
  if ($cppex) then
    CPpage('clear')
  else
    page('clear')
  endif
  return
endif

  shell('dirname '+file):$archive
  shell('basename '+file+' .fid'):$expname

  pwd:$curdir
  $plf='pcx'
  getadminfo('plotformat'):$plf

if ($1 <> '') then $plf=$1 endif

if ($# < 2) then
  Svfname($archive+'/plots/'+$expname+'_plot','','.'+$plf):$plotname
  shell('basename '+$plotname+' .'+$plf):$name
else
  $name=$2
endif

  write('plotter',0,-20,'Plotname:  %s',$name)
  exists('CPpage','maclib'):$cppex
  cd('/vnmr/tmp'):$dum
  if ($cppex) then
    CPpage($name)
  else
    page($name)
  endif
  write('line3','Please wait...')
  if ($plf = 'pdf') then
        shell('ps2pdfwr '+$name+' '+$name+'.'+$plf):$dum
  else
        shell('convert -density 300 '+$name+' '+$name+'.'+$plf):$dum
  endif

exists($name+'.'+$plf,'file'):$isok
if ($isok=0) then
        write('error','Generation of %s plot Failed!',$plf)
	$return=''
else
    shell('cp /vnmr/tmp/'+$name+'.'+$plf+' '+$archive+'/plots'):$dum
    write('line3','Plot saved as %s.%s',$name,$plf)
    cplog($name,'plot',$plf)
    datamirror($archive+'/plots/'+$name+'.'+$plf,'plot')
    $return=$archive+'/plots/'+$name+'.'+$plf
endif

shell('rm -f /vnmr/tmp/'+$name):$dum
shell('rm -f /vnmr/tmp/'+$name+'.'+$plf):$dum

cd($curdir):$dum
return($return)
