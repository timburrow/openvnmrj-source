"macro AuCgrad"
"********************************************************"
"AuCgrad - Carbon/proton G ratio Autocalibration       "
"********************************************************"
if ($# < 1) then $1 = 1 endif

IF ($1 = 1) THEN

$fileex=0 $file=''
AutoCheck('H1ref'):$fileex,$file
$explist=explist
if ($fileex) then RT($file) else return endif
wft
        setref
        f peak(1p,3p):$ht,cr
        wp=2p sp=1p
        $thresh=0.9 $peaks=0
        repeat
         th=$thresh*$ht nll:$peaks
         $thresh=$thresh-0.1
        until ($peaks > 2)
        cr=llfrq[1] wp=100 sp=cr-50

$solv=''
format(solvent,'lower'):$solv
explist=$explist

getparam('tpwr',tn):tpwr
getparam('pw90',tn):pw90
getparam('pwxlvl',dn):pwxlvl
getparam('pwx',dn):pwx
getparam('gradient','Probe'):PFGflg
pw=pw90
dm='nnn' dmm='ccc'

if (PFGflg = 'n') then
 write('error','This probe is not a PFG probe.  Check probes table')
 printon 
  write('alpha','This probe is not a PFG probe.')
  write('alpha','Check probes table')
  write('alpha','Carbon gradient calibration aborted')
 printoff
 Autocalnext
 return
endif

seqfil='gXHCAL'
exists('setvch','maclib'):$macex
if ($macex) then
  setvch('tn','dn','z')
  numvch=3,2,1,0
endif
ds ph vsadj vp=10 dm='nnn' dmm='ccc'
if ($solv='d2o') then
 f peak(3p,4p):$ht,cr wp=150 sp=cr-75
 j1xh=142 dof=-45*dfrq
endif
if ($solv='cdcl3') then
 j1xh=151 dof=-120*dfrq
endif
fixgrd(30):gzlvl1
$gradtype3=''
substr(gradtype,3,1):$gradtype3
if ($gradtype3<>'l') and (gzlvl1<25000) then
   gzlvl1=25000
endif

$ratio=3.98
"getparam('gHratio',dn):$ratio "
gzlvl3=trunc(gzlvl1*2/$ratio + 0.5)
gt1=.002 gt3=.001

if ($gradtype3 = 'l') then
 array('gzlvl3',51,gzlvl3-25,1)
else
 array('gzlvl3',51,gzlvl3-100,4)
endif

ai fn=8k at=.2 sinebell
av
wnt='wft(`acq`) select(1) vsadj(10) dssh'
wexp='AuCgrad(2)'
alock='n' wshim='n' r1=1
clear(2)
au('wait','next') 

ENDIF

"********************************************************"

IF ($1 = 2) THEN

sinebell wft select(1) av 
select(1)
$counter=1
$maxspec=1
repeat
  select($counter)
  peak(sp,wp+sp):$ht
  if $counter=1 then $maxht = $ht $gzlvl3=gzlvl3[1] else
     if $ht>$maxht then $maxht=$ht $maxspec=$counter $gzlvl3=gzlvl3[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

select($maxspec) vsadj(0.8*wc2max) vp=0.1*wc2max
dssh
r1=$gzlvl3
$posratio=2*gzlvl1/r1 $strposratio=''
format($posratio,0,5):$strposratio
n1='C/H pos. gradient ratio: +'+$strposratio
atext(n1)
wexp='cpplss'
Autocalsave('Cgrad_pos')
if (pltopt = 'y') then exec(wexp) endif

$diff=gzlvl3[1]-gzlvl3[2]
$start=-gzlvl3[1]
array('gzlvl3',arraydim,$start,$diff)
wexp='AuCgrad(3)'
au('next','wait') 

ENDIF

"********************************************************"

IF ($1 = 3) THEN

sinebell wft select(1) av 
select(1)
$counter=1
$maxspec=1
repeat
  select($counter)
  peak(sp,wp+sp):$ht
  if $counter=1 then $maxht = $ht $gzlvl3=gzlvl3[1] else
     if $ht>$maxht then $maxht=$ht $maxspec=$counter $gzlvl3=gzlvl3[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

r2=-$gzlvl3
$negratio=2*gzlvl1/r2 $strnegratio=''
format($negratio,0,5):$strnegratio

select($maxspec) vsadj(0.8*wc2max) vp=0.1*wc2max dssh
n1='C/H neg. gradient ratio: -'+$strnegratio
atext(n1)
wexp='cpplss'
Autocalsave('Cgrad_neg')
if (pltopt='y') then exec(wexp) endif

gzlvl3=(r1+r2)/2
$ratio = 2*gzlvl1/gzlvl3
$strratio=''
format($ratio,0,5):$strratio
setparams('gHratio',$strratio,dn)

Autocalnext

ENDIF

"********************************************************"
