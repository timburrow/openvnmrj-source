"macro hetero2dht"
// hetero2dht - apptype macro to setup generic Heteronuc 2D experiments
//              using Hadamard transforms

$action = 'setup'
$do = ''
if ($# > 0) then
  $action = $1
  if ($# > 1) then
    $do = $2
  endif
endif

if ($action = 'setup') then

// assume starting from Proton/Presat/Wet parameters

  $solvent=solvent
  if ($do = '') then
    $do = macro
  endif
  exists($do,'parlib'):$e,$path

  xm1('studydir'):$sdir
  if ($e) then
    if (auto='y' or sqmode='go') then
// retrieve parameters for automation
      $id=actionid
      $svfile=$sdir+'/info/'+$id+'/htpar'
      exists($svfile,'file'):$es
      if ($es>0.5) then
        fread($svfile)
      else
        if spintype='tach' then $spin=spin endif
        rtx($path,'current','rt','clear')
        if spintype='tach' then spin=$spin else spin=0 endif
      endif
      $dfile=$sdir+'/info/'+$id+'/diffmacro'
      exists($dfile,'file'):$ed
      if ($ed>0.5) then
        macrold($dfile):$x
        exec('diffmacro')
        purge($dfile)
      endif
      dg
// check if htfrq1 set, otherwise Skip
      on('htfrq1'):$on
      if (not $on or (htfrq1[1]=0 and size('htfrq1')<1.5)) then
        xmsetatts($id,'status','Skipped')
      endif
      return
    else
      if spintype='tach' then $spin=spin endif
      rtx($path,'current','rt','clear')
      if spintype='tach' then spin=$spin else spin=0 endif
    endif
  else
    write('error','parameters for %s do not exist',macro)
    return
  endif
  solvent=$solvent

  exists('PFGflg','parameter'):$e
  if ($e=0) then create('PFGflg','flag') PFGflg='n' endif
  $PFGflg=''      
  getparam('gradient','Probe'):$PFGflg   
  if ($PFGflg='') then $PFGflg='n' endif 
  PFGflg=$PFGflg 

  if (PFGflg='y') then
    exists('gzlvl1','parameter'):$e
    if ($e) then fixgrd(10):gzlvl1 endif
    exists('hsglvl','parameter'):$e
    if ($e) then fixgrd(10):hsglvl endif
  endif

  setfrq
  setdecpars refsource1='dfrq'

// get Proton, solvent suppression parameters
  getparam('tpwr',tn):tpwr
  getparam('pw90',tn):pw90
  pw=pw90
  celem=0
  sw=sw

  proc1='ht'

  exists('phase','parameter'):$phased
  if $phased>0 then
    if size('phase')>1 then
      $phased=1
    else
      $phased=0
      if phase=0 then
        $ptype=1
      endif
    endif
  endif

  if (dn = 'C13') then
    if ($phased=1) then
       setsw1(dn,160,-10):dof
       j1xh=140
    else
       setsw1(dn,225,-15):dof
    endif

  elseif (dn = 'N15') then
    if ($phased=1) then
      setN15sw('sw1'):dof
      j1xh=90
    else
      getrefcmpd('N15'):$ref
      strstr($ref,'NH3'):$r
      if ($r) then
         setsw1(dn,320,40):dof
      else
         strstr($ref,'MeNO2'):$r
         if ($r) then
            setsw1(dn,-60,-340):dof
         else
            strstr($ref,'(CH3)4N'):$r
            if ($r) then
               setsw1(dn,275,-5):dof
            endif
          endif
       endif
    endif
  endif

// get htfrq1 sw1 dof, set ni fn1
  editht('getni',dn)
  htfrqdisp('line3','htfrq1')

  axis='pd'
  $size=size('phase')
  pmode='full'

  exists('spinlock','parameter'):$e
  if ($e) then
    spinlock=sw*1.4
  endif

// set wet parameters
  $wet='n'
  exists('wet','parameter'):$e
  if $e>0.5 then $wet=wet endif
  gain='y'
  $gain_is_up = ((axis = 'pd') and (PFGflg = 'y') and ($wet <> 'y'))

  if ($gain_is_up) then
    gain = gain - 20
  endif

// exists(apptype,'parlib'):$e,$path
// if ($e > 0.5) then
//   rtx($path,'current','keep','clear')
// endif

  on('wrtp'):$e
  if ($e > 0.5) then
    exec(wrtp)
  endif
  setref('setup')

  execseq('setup')  // Execute any sequence specific setup

  exists($sdir+'/study.xml','file'):$d
  if (appmode='walkup' and studyid='tmpstudy' and $d>0.5) and not (auto='y' or sqmode='go') then
// save parameters for automation
    $id=actionid
    $j=0 teststr('sqattr','id','global'):$j
    if $j>0.5 then $id=sqval[$j] endif
    if ($id <> 'null') then
      $svfile=$sdir+'/info/'+$id+'/htpar'
      exists($svfile,'file'):$es
      if ($es>0.5) then
        rm('-f',$svfile):$x
      endif
      fsave($svfile)
    endif
  endif
  dg newdg
  dps

elseif ($action = 'process') then

  xm1('studydir'):$sdir
  if ((auto='y' or sqmode='go') and size('prescan')>2.5) then
    if (prescan[3]='n') then
      return
    endif
  endif

  setref('setup')
// setref1(tn,'setup')
  exists('f1coef','parameter'):$f1coefex
  $coef=0
  if ($f1coefex > 0) and (f1coef <> '') then
    $coef=1
  endif
  $seqsub=''
  $dot2dc=0
  substr(seqfil,1,1):$seqsub
  if ($seqsub='g') then
    if (nt=1) then
      $dot2dc=1
    endif
  endif
  $ptype = 0
  exists('phase','parameter'):$phased
  if $phased>0 then 
    if size('phase')>1 then
      $phased=1
    else
      $phased=0
      if phase=0 then
        $ptype=1
      endif
    endif
  endif

  proc1='ht'

  fullt
  if ($dot2dc) then
    wft2da('t2dc')
  else
    wft2da
  endif

  if (dmg <> 'av') then
    $intsize=size('lifrq')
    if ((Console = 'mercury') and ($intsize > 2)) then
      bc2d('f2'):$x
    endif
  endif

  f ai
  nm2d
  if auto='n' then dconi endif	// display

  execseq('proc') // Execute any sequence specific processing

elseif ($action = 'plot') then

  $jex=0
  isvnmrj:$j
  exists('studyid','parameter','global'):$e1
  exists('activestudy','parameter','global'):$e2
  if (($j>0.5 or auto='y') and $e1>0.5 and $e2>0.5) then
    if ((auto='y' or activestudy[1]<>'null') and studyid<>'null' and studyid<>'') then
      $jex=1
    endif
  endif

  exists(userdir+'/dialoglib/'+seqfil+'/'+seqfil+'plt','file'):$macex
  if ($macex = 0) then
    exists(systemdir+'/dialoglib/'+seqfil+'/'+seqfil+'plt','file'):$macex
  endif

  if ($jex) then
    cqplot2D
  elseif ($macex) then
    Autoplot2D
  elseif trunc(sw)=trunc(sw1) then  // homonuclear correlation 2D
    plcosy
  endif

  execseq('plot')

endif
