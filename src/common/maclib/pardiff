"macro pardiff"
" pardiff - display difference between parameter sets "

" 1995-03-26 - r.kyburz, started "
" 2007-03-15 - r.kyburz, adjusted for VnmrJ 2.2C "


" check arguments "
if ($#<1)or($#>3) then
  write('error','Usage:  pardiff(set1<,set2<,parameter_group>>)')
  return
endif

$diffparam='' $isok=0
getbinpath('diffparam'):$isok,$diffparam
if ($isok=0) then
  write('error','%s:  UNIX executable \'diffparam\' not found, aborting.', $0)
  return
endif

jexp:$expno,$exp
if typeof('$1') then
  length($1):$len
  substr($1,1,3):$char3
  if ($len=4)and($char3='exp') then
    $file1=userdir+'/'+$1+'/curpar'
    if $1=$exp then
      flush
    endif
  else
    $dir=$1
    exists($dir,'directory'):$e
    if not($e) then
      $dir=$1+'.fid'
      exists($dir,'directory'):$e
      if not($e) then
	$dir=$1+'.par'
	exists($dir,'directory'):$e
      endif
    endif
    if $e then
      $substr=''
      substr($dir,$len-4,4):$substr
      if $substr='/exp' then
        $subfile='/curpar'
      else
        $subfile='/procpar'
      endif
      $file1=$dir+$subfile
    else
      $file1=$1
    endif
  endif
else
  $file1=''
  format($1,1,0):$file1
  $file1=userdir+'/exp'+$file1+'/curpar'
  if $1=$expno then
    flush
  endif
endif
exists($file1,'file'):$e
if not($e) then
  write('error','pardiff: file %s not found',$file1)
  return
endif

if $#=1 then
  $file2=$file1
  flush
  $file1=curexp+'/curpar'
  if $file1=$file2 then
    write('error','pardiff: cannot compare current experiment with itself!')
    return
  endif
else
  if typeof('$2') then
    length($2):$len
    substr($2,1,3):$char3
    if ($len=4)and($char3='exp') then
      $file2=userdir+'/'+$2+'/curpar'
      if $2=$exp then
        flush
      endif
    else
      $dir=$2
      exists($dir,'directory'):$e
      if not($e) then
        $dir=$2+'.fid'
        exists($dir,'directory'):$e
        if not($e) then
	  $dir=$2+'.par'
	  exists($dir,'directory'):$e
        endif
      endif
      if $e then
        $substr=''
        substr($dir,$len-4,4):$substr
        if $substr='/exp' then
          $subfile='/curpar'
        else
          $subfile='/procpar'
        endif
        $file2=$dir+$subfile
      else
        $file2=$2
      endif
    endif
  else
    $file2=''
    format($2,1,0):$file2
    $file2=userdir+'/exp'+$file2+'/curpar'
    if $2=$expno then
      flush
    endif
  endif
  exists($file2,'file'):$e
  if not($e) then
    write('error','pardiff: file %s not found',$file2)
    return
  endif
endif

if $file1=$file2 then
  write('error','pardiff: two DIFFERENT files must be specified')
  return
endif

if $#<3 then
  $group='acquisition'
else
  if not(typeof('$3')) then
    write('error','pardiff: third argument (parameter group) must be a string')
    return
  endif
  $group=$3
endif

clear
echo('comparing '+$file1+' with '+$file2)

shell($diffparam,$file1,$file2,$group,'2>&1')
