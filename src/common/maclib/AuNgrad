"macro AuNgrad"
"********************************************************"
"AuNgrad - Carbon/proton G ratio Autocalibration       "
"********************************************************"
if ($# < 1) then $1 = 1 endif

$bio=0    // Check for Bio-NMR referencing (liquid NH3)
getrefcmpd('N15'):$ref
if $ref='NH3 (liquid), ext.' then $bio=1 endif

IF ($1 = 1) THEN

$fileex=0 $file=''
AutoCheck('H1ref'):$fileex,$file
$explist=explist
if ($fileex) then RT($file) else return endif
wft

dn='N15'
setref

$solv=''
format(solvent,'lower'):$solv
explist=$explist

getparam('tpwr',tn):tpwr
getparam('pw90',tn):pw90
getparam('pwxlvl',dn):pwxlvl
getparam('pwx',dn):pwx
getparam('gradient','Probe'):PFGflg
pw=pw90
dm='nnn' dmm='ccc'

if (PFGflg = 'n') then
 write('error','This probe is not a PFG probe.  Check probes table')
 printon 
  write('alpha','This probe is not a PFG probe.')
  write('alpha','Check probes table')
  write('alpha','Nitrogen gradient calibration aborted')
 printoff
 Autocalnext
 return
endif

seqfil='gXHCAL'
exists('setvch','maclib'):$macex
if ($macex) then
  setvch('tn','dn','z')
  numvch=3,2,1,0
endif
ds ph vsadj vp=0 dm='nnn' dmm='ccc'

if solvent='dmso' then        // ID2 sample
  setsw(0,9)
  peak(7.95p,8.15p):$ht,cr
  if $bio then setoffset(105.4,dn):dof else setoffset(-275,dn):dof endif
  wp=0.2p sp=7.95p
  j1xh=90
elseif solvent='d2o' then     // Autotest sample
  peak(1.80p,2.2p):$ht,cr
  setsw(0,3.8)
  if $bio then setoffset(234.4,dn):dof else setoffset(-146,dn):dof endif
  wp=1p sp=1.4p
  j1xh=2.8
  lb=2 vs=vs*10
else
  write('error','Wrong solvent and/or sample? Solvent must be DMSO or D2O!')
  return
endif

fixgrd(30):gzlvl1
$gradtype3=''
substr(gradtype,3,1):$gradtype3
if ($gradtype3<>'l') and (gzlvl1<25000) then
   gzlvl1=25000
endif

$ratio=10.0
"getparam('gHratio',dn):$ratio "
gzlvl3=trunc(gzlvl1*2/$ratio + 0.5)
gt1=.002 gt3=.001

if ($gradtype3 = 'l') then
 array('gzlvl3',51,gzlvl3-20,1)
else
 array('gzlvl3',51,gzlvl3-80,4)
endif

ai fn=8k at=.2 sinebell av
wnt='wft(`acq`) select(1) vsadj(10) vp=20 dssh vp=0 dssl(`value`,`reverse`)'
wexp='AuNgrad(2)'
alock='n' wshim='n' r1=1
clear(2)
au('wait','next') 

ENDIF

"********************************************************"

IF ($1 = 2) THEN

sinebell wft select(1) av 
select(1)
$counter=1
$maxspec=1
repeat
  select($counter)
  peak(sp,wp+sp):$ht
  if $counter=1 then $maxht = $ht $gzlvl3=gzlvl3[1] else
     if $ht>$maxht then $maxht=$ht $maxspec=$counter $gzlvl3=gzlvl3[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

select($maxspec) vsadj(0.8*wc2max) vp=0
vp=20 dssh vp=0 dssl('value','reverse')
r1=$gzlvl3
$posratio=2*gzlvl1/r1 $strposratio=''
format($posratio,0,5):$strposratio
n1='N/H pos. gradient ratio: +'+$strposratio
atext(n1)
wexp='pl(`all`) pltext pssl(`value`) page'
Autocalsave('Ngrad_pos'):$dum
if (pltopt = 'y') then exec(wexp) endif

$diff=gzlvl3[1]-gzlvl3[2]
$start=-gzlvl3[1]
array('gzlvl3',arraydim,$start,$diff)
wexp='AuNgrad(3)'
au('next','wait') 

ENDIF

"********************************************************"

IF ($1 = 3) THEN

sinebell wft select(1) av 
select(1)
$counter=1
$maxspec=1
repeat
  select($counter)
  peak(sp,wp+sp):$ht
  if $counter=1 then $maxht = $ht $gzlvl3=gzlvl3[1] else
     if $ht>$maxht then $maxht=$ht $maxspec=$counter $gzlvl3=gzlvl3[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

r2=-$gzlvl3
$negratio=2*gzlvl1/r2 $strnegratio=''
format($negratio,0,5):$strnegratio

select($maxspec) vsadj(0.8*wc2max) vp=20 dssh vp=0 dssl('value','reverse')
n1='N/H neg. gradient ratio: -'+$strnegratio
atext(n1)
wexp='pl(`all`) pltext pssl(`value`,`reverse`) page'
Autocalsave('Ngrad_neg'):$dum
if (pltopt='y') then exec(wexp) endif

gzlvl3=(r1+r2)/2
$ratio = 2*gzlvl1/gzlvl3
$strratio=''
format($ratio,0,5):$strratio
setparams('gHratio',$strratio,dn)

Autocalnext

ENDIF

"********************************************************"
