"macro rtcmx"
"rtcmx - convert and read a cmx file into the current experiment."
"        Call with the name of the cmx data 'file'.  The cmx file may be"
"        specified as an absolute pathname or as one relative to the current directory."

$cmxname=''
if $# < 1 then
   input('File name (enter name and <return>)? '):$cmxname
   $l=0
   length($cmxname):$l
   if ($l < 1) then
    write('error','No file name given, command aborted')
    abort
   endif
else
  $cmxname=$1
endif
exists($cmxname,'directory'):$e
if ($e < 0.5) then
   write('error','data directory %s does not exist',$cmxname)
   abort
endif
exists($cmxname+'/acq','file'):$e
if ($e < 0.5) then
   write('error','file %s does not contain SpinSight data',$cmxname)
   abort
endif


"Generate the basename for the converted version of the file."
$basename = ''
shell('basename','"'+$cmxname+'"'):$basename

"Convert the file, creating basename.cv."
exists(curexp+'/'+$basename+'.cv','file'):$exists
if $exists then
   rm('-rf',curexp+'/'+$basename+'.cv')
endif
shell('convertcmx -cs -os','"'+$cmxname+'"','"'+curexp+'/'+$basename+'.cv"'):$dum

"Read it and fill in missing parameters from /parlib/cmx."
sread(curexp+'/'+$basename+'.cv',systemdir+'/parlib/cmx')
rm('-rf',curexp+'/'+$basename+'.cv'):$dum
setvalue('file',$cmxname,'current')
setvalue('file',$cmxname,'processed')
fixpar
exists('phase','parameter'):$e
if ($e) then
  if (phase = 10) then
    setvalue('phase',1,'processed')
    setvalue('phase',2,2,'processed')
    setvalue('array','phase','processed')
    setvalue('phase',1)
    setvalue('phase',2,2)
    setvalue('array','phase')
  endif
endif
exists('console','parameter'):$e
if $e then
  console='cmx' 
  create('console','string','processed',''):$dum
  setvalue('console','cmx','processed')
else
  return
endif

flush
// The sfrq/dfrq/dfrq2 should be based on ch1/ch2/ch3
//  values in the cmx data.
// One easy solution is to make rtcmx call fixcmx But....

// The following should really done by the convertcmx
//   script upto the last 2 lines
getvalue('sfrq','processed'):$sfrq
getvalue('dfrq','processed'):$dfrq
$origsfrq=$sfrq
exists('ch1','parameter'):$e
if $e then
    if ch1=2 then 
	getvalue('dfrq','processed'):$sfrq
    elseif ch1=3 then 
	getvalue('dfrq2','processed'):$sfrq
    endif
endif
exists('ch2','parameter'):$e
if $e then
   if ch2=1 then 
	getvalue('sfrq','processed'):$dfrq
   elseif ch2=3 then 
	getvalue('dfrq2','processed'):$dfrq 
   endif
endif
setvalue('sfrq',$sfrq,'current')
setvalue('dfrq',$dfrq,'current')
setvalue('sfrq',$sfrq,'processed')
setvalue('dfrq',$dfrq,'processed')

// The following 2 lines should be ideally in rtcmx macro
setref
fn=np expfilt
