"macro Augcal"
"********************************************************"
"Augcal - Probe gcal Autocalibration                   "
"********************************************************"

if ($# < 1) then $1 = 1 endif

IF ($1 = 1) THEN

$fileex=0 $file=''
AutoCheck('H1ref'):$fileex,$file
$explist=explist
if ($fileex) then RT($file) else return endif
wft
explist=$explist

gain='y'
ds av 
getparam('tpwr',tn):tpwr
getparam('pw90',tn):pw90
pw=pw90
d2=.02 av sw=100000
np=512 lb=10 sb=0.001 sbs=0.000237
p1=pw90 pw=2*pw90 f full av
seqfil='profile'

$pfgon3=''
substr(pfgon,3,1):$pfgon3
if ($pfgon3='n') then pfgon='nny' endif
$gradtype3=''
substr(gradtype,3,1):$gradtype3
if ($gradtype3 = 'l') then
 gzlvl1=400
else
 gzlvl1=2000
endif

if ($gradtype3 = 'h') then
   setparams('gradient','n','Probe')
   Autocalnext
   return
endif

wnt='wft f full dc vsadj(100) ds'
wexp='Augcal(2)'
alock='n' wshim='n' r1=1
clear(2)
au('wait','next')

ENDIF

"********************************************************"

IF ($1 = 2) THEN

wft av f full dc vsadj(100) ds
peak:$ht,cr
dres(cr,.2):$width
if ($width < 40000) then
 gzlvl1=1.5*gzlvl1 wexp='Augcal(3)'
 au('next','wait')
else
  $size=16
  getparam('rfsize','Probe'):$size
  $size=$size/10
  $gammabar=(sfrq*1e6)/(234.94*h1freq)
  $gcal=$width/($gammabar*gzlvl1*$size)
  if $gcal < 0 then $gcal = -1.0*$gcal endif
  $strgcal=''
  format($gcal,0,5):$strgcal
  setparams('gradient','y','Probe')
  setparams('gcal',$strgcal,'Probe')
  $text='Gradient Calibration for the probe: '+probe
  $atext = '   '+$strgcal+' G/cm/dac unit'
  atext($text)
  atext($atext)
  Autocalsave('Probegcal') 
  if (pltopt = 'y') then
    vsadj(100) pl pltext page
  else
    wexp='wft f full dc vsadj(100) ds pl pltext page'
  endif
  Autocalnext
endif

ENDIF

"********************************************************"

IF ($1 = 3) THEN

wft av f full dc vsadj(100) ds
peak:$ht,cr
dres(cr,.2):$width
if ($width < 15000) then
  write('error','gradient calibration failed on second try -- check PFG amplifier')
  setparams('gradient','n','Probe')
  printon
  write('alpha','Gradient calibration failed')
  write('alpha','Check PFG amplifier and pfgon parameter')
  write('alpha','gcal calibration aborted')
  printoff
  Autocalnext
  return
else
  $size=16
  getparam('rfsize','Probe'):$size
  $size=$size/10
  $gammabar=(sfrq*1e6)/(234.94*h1freq)
  $gcal=$width/($gammabar*gzlvl1*$size)
  if $gcal < 0 then $gcal = -1.0*$gcal endif
  $strgcal=''
  format($gcal,0,5):$strgcal
  setparams('gradient','y','Probe')
  setparams('gcal',$strgcal,'Probe')
  $text='Gradient Calibration for the probe: '+probe
  $atext = '   '+$strgcal+' G/cm/dac unit'
  atext($text)
  atext($atext)
  Autocalsave('Probegcal') 
  if (pltopt = 'y') then
    vsadj(100) pl pltext page
  endif
  Autocalnext
endif

ENDIF

"********************************************************"
