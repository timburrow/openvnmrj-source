"macro csiSel"
// csiSel is initiated by mouse clicking CSI gird when aipMode=11.
// this macro implements what to do when voxels are selected
// Example:
//
// csiSel('vox',i) 
//	voxel i is selected, neither fid nor spec is displayed in voxels. 
// csiSel('fid vox',i) 
//	voxel i is selected, fid is displayed in voxels. 
// csiSel('spec vox',i) 
//	voxel i is selected, spec is displayed in voxels. 
// csiSel('spec vox',n,'i j ...') 
//	n voxels i, j, ... are selected: set dsSelect and call dss('select'). 
//	note, i,j,... are in a space separated string. 
// csiSel('spec roi',i) 
//	start to select ROI: do nothing.
// csiSel('spec roi',iv,iv2,iv3,jv,jv2,jv3) 
//	ROI defined by iv,iv2,iv3,jv,jv2,jv3 is selected. 
//	calculate map?

if($#<2 or typeof('$2')) then return endif

exists('dsSelect','parameter'):$e
if(not $e) then
    create('dsSelect','integer')
    setgroup('dsSelect','display')
    dsSelect=1
endif
exists('showarray','parameter'):$e
if(not $e) then
    create('showarray','string')
    setgroup('showarray','display')
    showarray='vert'
endif

strstr($1,'fid'):$isFid
if(not $isFid) then
  strstr($1,'FID'):$isFid
endif
strstr($1,'vox'):$isVox
strstr($1,'roi'):$isRoi
if($isFid) then
  if($#=2) then
    $n=$2
  else
    substr($3,$2):$n
  endif
  dsSelect=$n
  full df($n) 
elseif($isVox and $#=2) then
  dsSelect=$2
  full ds($2)
  csiCmd('updateVoxelInfo',$2)
elseif($isVox) then

  setprotect('dsSelect','set',0)
  dsSelect=1

  if(typeof('$3')) then // a string containing space separated numbers
    $i=0
    while($i<$2) do
      $i=$i+1
      substr($3,$i):$n
      dsSelect[$i]=$n
    endwhile 
  else
    $i=1
    while($i<$#) do
      $s=''
      format($i+1,0,0):$s 
      $s='$'+$s
      dsSelect[$i]={$s}
      $i=$i+1
    endwhile
  endif
  // set vo, ho
  if(showarray='vert') then
    if($i>0) then
      vo=wc2max/$i
    endif
    dss('select')
  else
    dssh('select')
  endif
  
elseif($isRoi and $#=2) then
  write('line3','select ROI')
elseif($isRoi and $#=7) then
  write('line3','ROI %d %d %d %d %d %d selected',$2,$3,$4,$5,$6,$7)
endif
