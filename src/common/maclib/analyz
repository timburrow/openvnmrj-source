"macro analyz"
" *********************************************************************** "
" *analyz($option,$title) - analyzes for peak height and phase deviation* "
" *                                                                     * " 
" *$option='n' for amp and phase, 'a' for amp only, or 'p'for phase only* "
" *$title is optional title for plots                                   * "
" *********************************************************************** "

if $#=0 then $option='n'
" **** "
echo('macro=',$0)
echo('$option=',$option)
" ****** "
 else $option=$1 endif
if array='' then
  write('error','Analysis not possible for non-arrayed data set') 
  return
endif 

$index=1 $htsum=0 $htsumsq=0 $rpsum=0 $rpsumsq=0
$ht=0 $freq=0 $11=0 $fsum=0 $fsumsq=0 ai
select(1) dc ai ph aph0
peak:$ht,$freq
$maxht=$ht  "keep track of min, max ht for maximum deviation calculation"
$minht=$ht
if $option='p' then
  peak:$ht,$freq
  $htav=$ht $rp=rp
endif  

"********this junk is leftover from vxr-4000 version. What does it do?****"
"   if rp<0 then rp=-rp endif "
"   if $rp>150 then "
"     $11=1 "
"   endif "


"****Find peak ht, phase, freq, and do sums for leastsquares***********"

clear
repeat
  select($index)
  if $option<>'a' then aph0 endif
  if $option<>'p' then
    peak:$ht,$freq
    if $ht>$maxht then $maxht=$ht endif
    if $ht<$minht then $minht=$ht endif
    " ***** "
    echo($index,'ht=',$ht,'phase',rp,'freq=',$freq)
    " ****** "
  endif
  if ($11=1)and(rp<0)and($option='p') then rp=360+rp endif
  $htsum=$htsum+$ht $htsumsq=$htsumsq+$ht*$ht
  $rpsum=$rpsum+rp $rpsumsq=$rpsumsq+rp*rp
  $fsum=$fsum+$freq $fsumsq=$fsumsq+$freq*$freq
  $index=$index+1
until $index>arraydim

"**********Calc averages ans std dev*************************************"

$index=arraydim
$htvar=($htsumsq-($htsum*$htsum)/$index)
if $htvar<0 then $htvar=-$htvar endif
$htsd=SQRT($htvar/($index-1))
$rpvar=($rpsumsq-($rpsum*$rpsum)/$index)
if $rpvar<0 then $rpvar=-$rpvar endif
$rpsd=SQRT($rpvar/($index-1))
$fvar=($fsumsq-($fsum*$fsum)/$index)
if $fvar<0 then $fvar=-$fvar endif
$fsd=SQRT($fvar/($index-1))
$htdev=$maxht-$minht
$htav=$htsum/$index
"  *** "
echo('$htav=',$htav)
" **** "
$vsinit=vs   "save initial vs"
vs=.8*wc2max*vs/$htav vp=0

"***********************************************************************"
"******************output to graphics and/or plotter********************"

$device='graphics'    "graphics first"

repeat
  if $device='plotter' then pl('all') else dssh endif
  if $#=2 then
    $title=$2
    write($device,10,wc2max-10,'%s',$title) $incr=10
    else $incr=0
  endif
  if $option<>'p' then
   write($device,10,wc2max-10-$incr,'ave ht=%6.2f',$htav)
   write($device,70,wc2max-10-$incr,'   std dev(percent)=%6.2f',100*$htsd/$htav)
  endif
  if $option<>'a' then 
    if macro='phstab' then
      write($device,10,wc2max-20-$incr,'calculated phase stability=%6.4f',14*$htsd/$htav) else
        write($device,10,wc2max-20-$incr,'ave phase=%6.2f',$rpsum/$index) 
        write($device,70,wc2max-20-$incr,'   std dev (deg)   =%6.2f',$rpsd) 
    endif
  endif 
  if $option='n' then 
    write($device,10,wc2max-30-$incr,'ave freq =%6.2f',$fsum/$index) 
    write($device,70,wc2max-30-$incr,'   std dev (hz)    =%6.2f',$fsd) 
  endif 
  write($device,10,wc2max-40-$incr,'pw =%6.1f',pw) 
  on('spin'):$spinon
  if $spinon=1 then
    write($device,10,wc2max-50-$incr,'sequence=%s  tn=%s   spin=%2.0f',seqfil,tn,spin) 
    else
      write($device,10,wc2max-50-$incr,'sequence=%s  tn=%s  spin=n',seqfil,tn) 
  endif
  exists('obsch','parameter'):$exists
  if $exists=1 then write($device,130,wc2max-50-$incr,'obsch=%3.0f',obsch) endif
  if seqfil='decpul' then
    write($device,50,wc2max-40-$incr,'dpwr= %3.0f',dpwr) else
      if seqfil='dec2pul' then
        write($device,50,wc2max-40-$incr,'dpwr2= %3.0f',dpwr2) else
          write($device,50,wc2max-40-$incr,'tpwr= %3.0f',tpwr)
  endif endif       
  if d2<>0 then write($device,85,wc2max-40-$incr,'d2=%4.3f',d2) endif
  write($device,120,wc2max-40-$incr,'d1= %6.3f',d1)
  if (dm='yyy')OR(dm='nny') then
    if (amptype='aa')OR(amptype='aaa') then
      write($device,10,wc2max-60-$incr,'dm=%s  dmm=%s  dpwr=%4.0f  dmf=%6.0f',dm,dmm,dpwr,dmf)
      else
        write($device,10,wc2max-60-$incr,'dm=%s  dmm=%s  dhp=%4.0f  dmf=%6.0f',dm,dmm,dhp,dmf)
    endif
  endif

  if $device='graphics' then  "ask about plotting"
    if n1<>'noplot' then   
      if n3='' then input('Plot Results? (n/y)'):$plotny
               else $plotny='y' 
      endif "if auto, dont ask"
      if $plotny='y' then $device='plotter'
        	     else $device='done'
      endif
      else
        $device='done'
    endif
    else
    exists('customer','parameter','global'):$exists
    if $exists=1 then
    write('plotter',10,-10,'Customer: %s  Cons SN: %s %s',customer,cons_sn,date)
  write('plotter',10,-20,'Probe:%s  Ser num:%5.0f %s',probe,probe_sn,operator)
  else
    write('plotter',10,-10,'NO ID INFORMATION.  USE id MACRO')
  endif
      page $device='done'
  endif
until $device='done'

vs=$vsinit  "restore original vs"
  
"***********************************************************************"
"******************output to results file if automated******************"
if n3<>'' then
  format($htav,6,2):$htstr format(100*$htsd/$htav,5,2):$sdhtstr
  format($rpsum/$index,6,2):$phstr format($rpsd,5,2):$sdrpstr
  format($fsum/$index,6,2):$fstr format($fsd,5,2):$sdfstr

  write('file',userdir+'/results/results.'+n3,'average ht=%s   per cent std dev=%s',$htstr,$sdhtstr)
  
  if macro='phstab' then
    format(14*$htsd/$htav,5,2):$phasestr
    write('file',userdir+'/results/results.'+n3,'calculated phase stability=%s',$phasestr) else
    write('file',userdir+'/results/results.'+n3,'average phase=%s    std dev=%s',$phstr,$sdrpstr)
    write('file',userdir+'/results/results.'+n3,'average freq=%s   std dev=%s',$fstr,$sdfstr)
  endif
endif
