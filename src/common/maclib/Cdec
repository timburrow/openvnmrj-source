"macro Cdec"

$probe='' shell('basename '+probe):$probe

mkpar('string','execprocess','','execplot','')
setgroup('execprocess','display')
setgroup('execplot','display')

execprocess='wft dssh darray(\'CalibrationResults\')'
execplot='execplot=\'\''

getlimit('pwxlvl'):$max,$min,$dbstep
if (console='inova') or (console='mercury') then $dbstep=1 endif
// For now force dbstep to integer or 0.5
if $dbstep>0.5 then $dbstep=trunc($dbstep+0.5) else $dbstep=0.5 endif

$arg=''
is_IP:$isIP
if ($# < 1) then
    if $isIP then
	$arg='1'
    else
	$pwxlvl=0
	getparam('pwxlvl','C13'):$pwxlvl
	if ($pwxlvl=0) then $arg='1' else $arg='init' endif
    endif
else
    if not typeof('$1') then
    	format($1,0,0):$arg
    else
	$arg=$1
    endif
endif

"********************************************************"
IF ($arg = '1') THEN

	$fileex=0 $file='' $asdir=''
	cplog('PROTON','first'):$file,$asdir
	if ($file<>'') then rt($asdir+'/'+$file) else return endif
	wft
	execprocess='wft dssh darray(\'CalibrationResults\')'
	execplot='execplot=\'\''
	$solv=''
	format(solvent,'lower'):$solv

	getparam('tpwr',tn):tpwr
	getparam('pw90',tn):pw90
	pw=pw90
	nt=4

	seqfil='pwxcalib'
	ds ph dm='nnn' dmm='ccc' pwxlvl=C13pwxlvl vp=wc2max/2
	if ($solv='d2o') then
 	    f vsadj peak(3p,4p):$ht,cr wp=150 sp=cr-75
 	    j1xh=142 //dof=-45*dfrq
	    setoffset(dn,49):dof
	elseif ($solv='cdcl3') then
	    cr=llfrq[1]-rfl+rfp wp=100 sp=cr-50
 	    vsadj j1xh=151 //dof=-120*dfrq
	    setoffset(dn,-23):dof
	endif

	array('pwx',30,2,2)
	ai fn=8k at=.2 gaussian av sinebell
	wnt='wft(`acq`) select(1) vsadj(20) dssh'
	caliblist=$0
        xmhaha_calib('synonym',$0):caliblist
	calibnode='_iteration'
	write('line3','_pwxlvl%g',pwxlvl):calibnode
	chkname(calibnode,'alnum-'):calibnode
	xmhaha_calib('addNode','Cdec(2)')
	alock='n' wshim='n' r1=1 r3=1
	clear(2)

"********************************************************"
ELSEIF ($arg = '2') THEN

	$file='' $asdir=''
	cplog('pwxcalib','last'):$file,$asdir
	rt($asdir+'/'+$file)
	wft select(1) aph0 av 
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	$counter=1
	$stop=0
	repeat
  	    select($counter)
 	    if ($stop < 4) then
  		peak(sp,wp+sp):$ht
  		if $counter=1 then $minht = $ht $pwx=pwx[1] $maxht=$ht
		else
     		    if $ht<$minht then 
			$stop=0
			$minht=$ht $pwx=pwx[$counter] 
		    else 
			$stop=$stop+1 
     		    endif
  		endif
 	    endif
  	    $counter=$counter+1
	until ($counter>arraydim)

	if $minht/$maxht > 0.5 then
	    write('error','Autocalibration failed (min/max > 0.5) - determine manually')
	    if ($isIP) then
                write('line3','13C pw90 (indirect) calibration failed, target pulse width not achievable'):$str
		atext($str)
	        VPlog('ExpError',$str)
	        IPdoemail('FAIL')
	    endif
	    abort
	endif
        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.1f',caliblist,pwxlvl,$pwx):$line
        CalibrationResults=CalibrationResults,$line
        writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

	select(1) 

	if ($pwx > 2*C13pwx) then
  	    if (r1 < 2) and (pwxlvl < 57) then
    		r1 = r1+1
    		pwxlvl=pwxlvl+6
		write('line3','_pwxlvl%g',pwxlvl):calibnode
        	chkname(calibnode,'alnum-'):calibnode
		xmhaha_calib('addNode','Cdec(2)')
    		return
  	    else
    		write('error','Autocalibration failed on second try -- determine manually')
	        if ($isIP) then
                    write('line3','13C pw90 (indirect) calibration failed, target pulse width not achievable'):$str
		    atext($str)
	            VPlog('ExpError',$str)
	            IPdoemail('FAIL')
	        endif
    		abort
  	    endif
	endif
	PWRcalib($pwx,C13pwx,'pwxlvl'):$pwxlvldiff,$adim,$pwinc
	if $pwxlvldiff=0 then
	    Cdec(3,$pwx)
	else
	    pwxlvl=pwxlvl+$pwxlvldiff
	    array('pwx',$adim/2,1,$pwinc)
	    clear(2) 
            write('line3','_pwxlvl%g',pwxlvl):calibnode
            chkname(calibnode,'alnum-'):calibnode
	    xmhaha_calib('addNode','Cdec(4)')
	endif

"********************************************************"
ELSEIF ($arg = '3') THEN

	$pwx=$2
	gaussian ph ds(1)
	array('pwx',31,0.7*$pwx,0.02*$pwx)
	clear(2) full
	da
        write('line3','_pwxlvl%g',pwxlvl):calibnode
        chkname(calibnode,'alnum-'):calibnode
	xmhaha_calib('addNode','Cdec(5)')

"********************************************************"
ELSEIF ($arg = '4') THEN

	if $# < 2 then $2='' endif

	cplog('pwxcalib','last'):$file,$asdir
	rt($asdir+'/'+$file)
	clear(2)
	sinebell wft select(1) av
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	$counter=1
	$stop=0
	repeat
  	    select($counter)
 	    if ($stop < 4) then
  		peak(sp,wp+sp):$ht
  		if $counter=1 then $minht = $ht $pwx=pwx[1] 
		else
     		    if $ht<$minht then $minht=$ht $pwx=pwx[$counter] 
		    else $stop=$stop+1
     		    endif
  		endif
 	    endif
  	    $counter=$counter+1
	until ($counter>arraydim)

        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.1f',caliblist,pwxlvl,$pwx):$line
        CalibrationResults=CalibrationResults,$line
        writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

	select(1) 
        PWRcalib($pwx,C13pwx,'pwxlvl'):$pwxlvldiff,$adim,$pwinc

// if this is the last iteration, STOP !
	if $2='last' and $pwxlvldiff>0 then $pwxlvldiff=0  endif
	if $pwxlvldiff=0 then
	    	Cdec(3,$pwx)
	else
  		pwxlvl = pwxlvl+$pwxlvldiff
		array('pwx',$adim/2,1,$pwinc)
  		clear(2)
        	write('line3','_pwxlvl%g',pwxlvl):calibnode
	        chkname(calibnode,'alnum-'):calibnode
// if pwrdiff is negative 1 dbstep
// This would be the last iteration
		if $pwxlvldiff>=-1*$dbstep and $pwxlvldiff<0 then
		    $2='last'
		endif
		$arg2='Cdec(4,`'+$2+'`)'
		xmhaha_calib('addNode',$arg2)
	endif

"********************************************************"
ELSEIF ($arg = '5') THEN

	cplog('pwxcalib','last'):$file,$asdir
	rt($asdir+'/'+$file)
	clear(2)
	sinebell wft select(1) av
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	$counter=1
	repeat
  	    select($counter)
  	    peak(sp,wp+sp):$ht
  	    if $counter=1 then $minht = $ht $pwx=pwx[1] 
	    else
     		if $ht<$minht then $minht=$ht $pwx=pwx[$counter] endif
  	    endif
  	    $counter=$counter+1
	until $counter>arraydim

        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.1f',caliblist,pwxlvl,$pwx):$line
        CalibrationResults=CalibrationResults,$line
        writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

	if ($pwx > C13pwx) then
 	    if (pwxlvl < 63) then
  	    	pwxlvl = pwxlvl+$dbstep
  		array('pwx',31,0.7*$pwx,0.02*$pwx)
        	write('line3','_pwxlvl%g',pwxlvl):calibnode
	        chkname(calibnode,'alnum-'):calibnode
		xmhaha_calib('addNode','Cdec(5)')
  		return
 	    endif
	endif

	$diff = pwx[2] - pwx[1]
	if ($diff > 0.25) then
 	    array('pwx',31,$pwx-3,0.2)
            write('line3','_pwxlvl%g',pwxlvl):calibnode
            chkname(calibnode,'alnum-'):calibnode
	    xmhaha_calib('addNode','Cdec(5)')
 	    return
	endif

	C13pwx=$pwx
	select(1)
	clear(2)
	pwxlvl=pwxlvl-12 $pwx=C13pwx*4
	array('pwx',41,0.5*$pwx,0.025*$pwx)
	clear(2) full
	da
        write('line3','_pwxlvl%g',pwxlvl):calibnode
        chkname(calibnode,'alnum-'):calibnode
	xmhaha_calib('addNode','Cdec(6)')

"********************************************************"
ELSEIF ($arg = '6') THEN

	cplog('pwxcalib','last'):$file,$asdir
	rt($asdir+'/'+$file)
	clear(2)
	sinebell wft select(1) av
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	$counter=1
	repeat
  	    select($counter)
  	    peak(sp,wp+sp):$ht
  	    if $counter=1 then $minht = $ht $pwx=pwx[1] 
	    else
     		if $ht<$minht then $minht=$ht $pwx=pwx[$counter] endif
  	    endif
  	    $counter=$counter+1
	until $counter>arraydim

        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.1f',caliblist,pwxlvl,$pwx):$line
        CalibrationResults=CalibrationResults,$line
        writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

	array('pwx',31,$pwx-6,0.4)
        write('line3','_pwxlvl%g',pwxlvl):calibnode
        chkname(calibnode,'alnum-'):calibnode
	execprocess='Cdec(7)'

"********************************************************"
ELSEIF ($arg = '7') THEN

	clear(2)
	sinebell wft select(1) av
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	$counter=1
	repeat
  	    select($counter)
  	    peak(sp,wp+sp):$ht
  	    if $counter=1 then $minht = $ht $pwx=pwx[1] 
	    else
     		if $ht<$minht then $minht=$ht $pwx=pwx[$counter] endif
  	    endif
  	    $counter=$counter+1
	until $counter>arraydim

	select(1) 
	$ref_pwx90=$pwx
	$ref_pwxlvl=pwxlvl
	$pwxlvl_cf = $pwx/4/C13pwx
        pwxlvl=pwxlvl+12
        pwx=C13pwx

	$strpwx='' $strpwxlvl='' $strref_pwxlvl='' $strref_pwx90=''
	$strdmf='' $strdpwr='' $sdres='' $strpwxlvl_cf=''
	write('line3','%0.1f',C13pwx):$strpwx
	write('line3','%g',pwxlvl):$strpwxlvl
	write('line3','%g',$ref_pwxlvl):$strref_pwxlvl
	write('line3','%0.1f',$ref_pwx90):$strref_pwx90
	format($pwxlvl_cf,0,4):$strpwxlvl_cf
	setparams('pwx',$strpwx,'C13')
	setparams('pwxlvl',$strpwxlvl,'C13')
	setparams('pwxlvl_cf',$strpwxlvl_cf,'C13')

        // Test for VeriPulse environment
        is_IP:$isIP
        if ($isIP) then
           VPgettarget('C13pwx90'):$target
           $targets=''
           format($target,1,1):$targets
           $IPteststatus='FAIL'
           if (C13pwx<=$target) then $IPteststatus='PASS' endif

	   //Add the sample info to the text file
	   VPtext
           write('line3','13C 90-degree pulse width \(indirect\)'):$line
           atext($line)
           write('line3','pwx90: %.1f us at power level %.1f dB',C13pwx,pwxlvl):$line
           atext($line)
           CalibrationResults=CalibrationResults,$line
           write('line3','Target <= %.1f us',$target):$line
           atext($line)
           CalibrationResults=CalibrationResults,$line
           write('line3','Test status: %s',$IPteststatus):$line
           atext($line)
           CalibrationResults=CalibrationResults,$line

	   //Add amp power information if calibration table exists
	   exists('VPgetpower','maclib'):$vpex
	   if ($vpex) then
	      VPgetpower('check'):$pwrtabex
	      if ($pwrtabex) then
   		   VPgetpower(VPpowertabpath,'C13','indirect',pwxlvl,C13pwx,$target):$atext
	           atext($atext)
	      endif
	   endif
	else
	   write('line3','pwx90: %.1f us at power level %.1f dB',C13pwx,pwxlvl):$line
           atext($line)
           CalibrationResults=CalibrationResults,$line
        endif

        $line=''
        write('line3','%s - refpwxlvl:%g  refpwx:%0.1f',caliblist,$ref_pwxlvl,$ref_pwx90):$line
        CalibrationResults=CalibrationResults,$line
        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.1f',caliblist,pwxlvl,pwx):$line
        CalibrationResults=CalibrationResults,$line
        $line=''
        write('line3','%s - compression factor: %0.4f',caliblist,$pwxlvl_cf):$line
        CalibrationResults=CalibrationResults,$line
	wnt='wft(`acq`) select(1) ph aph0 dssh' 
	
	calcdecshape
	Cdec('finish')

"********************************************************"
"Cdec: C13 decoupler Autocalibration macro "
" options/arguments: "
" (none) : initial setup "
" 'pwxcal' : calculate/refine pwx;  save data when complete "
" 'decset' : initial set up of decoupling calibration "
" 'deccal' : process/save decoupling calibration result, continue "
"	with remaining calibration(s) "

ELSEIF ($arg = 'init') then
  "initial setup of C13 decoupler calibration "

	$fileex=0 $file='' $asdir=''
	cplog('PROTON','first'):$file,$asdir
	if ($file<>'') then rt($asdir+'/'+$file) else return endif
	wft
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	setref

"set up solvent subtraction for CH3I sample"
	$solv = ''  format(solvent,'lower'):$solv
	if $solv = 'cdcl3' then
 	    addpar('ss') ssfilter=40  ssntaps=201
   	    setref
   	    f peak(1p,3p):$ht,cr
   	    wp=2p sp=1p
   	    $thresh=0.9 $peaks=0
   	    repeat
    		th=$thresh*$ht nll:$peaks
    		$thresh=$thresh-0.1
   	    until ($peaks > 2)
   	    $c12h=llfrq[2]
  	    sslsfrq = rfp - rfl + sw/2 - $c12h
	endif

	getparam('tpwr',tn):tpwr
	getparam('pw90',tn):pw90
	pw=pw90

	seqfil='pwxcalib'
	ds ph dm='nnn' dmm='ccc' pwxlvl=C13pwxlvl vp=wc2max/2

	if ($solv = 'd2o') then
 	    f vsadj sp = 2.7p wp = 1p
 	    j1xh=142 // dof=-45*dfrq
 	    setoffset('C13',49):dof

	elseif ($solv='cdcl3') then
 	    vsadj j1xh=151 sp = $c12h-100  wp = 200
 	    setoffset('C13',-23):dof
	endif

	$step = trunc(2*C13pwx + 0.5)/10
	array('pwx',15,0,$step)
	ai  fn = 4*np
	wnt='wft(`acq`) dssh'
	caliblist=$0
        xmhaha_calib('synonym',$0):caliblist
	calibnode='_iteration'
	write('line3','_pwxlvl%g',pwxlvl):calibnode
        chkname(calibnode,'alnum-'):calibnode
	xmhaha_calib('addNode','Cdec(\'pwxcal\')')
	alock='n' wshim='n' r1=1 r3=1
	clear(2)
	return

ELSEIF ($arg = 'pwxcal') then
"********************************************************"

	cplog('pwxcalib','last'):$file,$asdir
	rt($asdir+'/'+$file)
	wft select(1) vsadj th=40 nll
        execprocess='wft dssh darray(\'CalibrationResults\')'
        execplot='execplot=\'\''
	fp pw360cal:$pwx
	if ($pwx < 1) then  " unreliable result! "
    	    write('error','Autocalibration failed -- determine manually')
    	    abort
	endif

        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.3f',caliblist,pwxlvl,$pwx):$line
        CalibrationResults=CalibrationResults,$line
        writeparam($asdir+'/'+$file+'.fid/procpar','CalibrationResults','current','add')

	if ($pwx > 2*C13pwx) then
  	    if (pwxlvl < 56) then
    		pwxlvl=pwxlvl+6
        	write('line3','_pwxlvl%g',pwxlvl):calibnode
	        chkname(calibnode,'alnum-'):calibnode
		xmhaha_calib('addNode','Cdec(\'pwxcal\')')
    		write('alpha','calibrating decoupler pulses at power %d',pwxlvl)
    		return
  	    else
    		write('error','Autocalibration failed -- determine manually')
    		abort
  	    endif
	endif
	PWRcalib($pwx,C13pwx,'pwxlvl'):$pwxlvldiff

 	if ($pwxlvldiff) then
   	    pwxlvl=pwxlvl+$pwxlvldiff
            write('line3','_pwxlvl%g',pwxlvl):calibnode
            chkname(calibnode,'alnum-'):calibnode
	    xmhaha_calib('addNode','Cdec(\'pwxcal\')')
   	    write('alpha','calibrating decoupler pulses at power %g',pwxlvl)
   	    return
 	else
   	    $strpwx='' $strpwxlvl=''  $strc13pwx = ''
   
	    write('line3','%0.1f',$pwx):$strpwx
	    write('line3','%g',pwxlvl):$strpwxlvl
   	    format(C13pwx,0,1):$strc13pwx
   	    setparams('pwx',$strpwx,'C13')
   	    setparams('pwxlvl',$strpwxlvl,'C13')

   	    Cdec('decset',$pwx)  "continue with dpwr/dmf/pwxlvl_cf calibrations"
   	    return
 	endif

ELSEIF ($arg = 'decset') then
"********************************************************"

 	seqfil = 's2pul' pwx = $2  dm = 'nny'  
 	dpwr = pwxlvl-18  "Set dpwr to be 18 dB below full power"
 	dof = dof+25*dfrq, dof-25*dfrq
 	da
 	full vp=0 
        write('line3','_dpwr%d',dpwr):calibnode
        chkname(calibnode,'alnum-'):calibnode
	execprocess='Cdec(\'deccal\')'

 	return

ELSEIF ($arg = 'deccal') then
"********************************************************"

  	select(1) vsadj(100) th=50 nll:$num
  	if ($num < 2) then  " unreliable result! "
    	    write('error','Autocalibration failed. Less than 2 lines found.')
    	    abort
  	endif
  	$difflf = llfrq[1] - llfrq[2]

  	select(2) vsadj(100) th=50 nll:$num
  	if ($num < 2) then  " unreliable result! "
    	    write('error','Autocalibration failed. Less than 2 lines found.')
    	    abort
  	endif
  	$diffhf = llfrq[1] - llfrq[2]

	h2cal($difflf,$diffhf,j1xh):$gb2,$pwx90,$freq
	$dpwr=dpwr
	$cf=$pwx90/8/pwx  " Since dpwr is 18 dB below pwxlvl...."
	$strcf = ''
	format($cf,0,4):$strcf
	setparams('pwxlvl_cf',$strcf,dn)

        $line=''
        write('line3','%s - dpwr:%d  gammaB2:%0.1f',caliblist,$dpwr,$gb2):$line
        CalibrationResults=CalibrationResults,$line
        $line=''
        write('line3','%s - pwxlvl:%g  pwx:%0.3f',caliblist,pwxlvl,pwx):$line
        CalibrationResults=CalibrationResults,$line
        $line=''
        write('line3','%s - compression factor: %s',caliblist,$strcf):$line
        CalibrationResults=CalibrationResults,$line

	calcdecshape
	Cdec('finish')

"*****************************************************************"
ELSEIF ($arg = 'profile') THEN

	select(1) ph
        seqfil='s2pul'
        pwx=C13pwx
        vp=0
        dm='nny' dmm='ccw' wp=600 sp=sp-250
        setdecpars
        wnt='wft(`acq`) select(1) ph aph0 vsadj(20) dssh'
        $dof=dof+(125*dfrq)
        array('dof',51,$dof,-5*dfrq)
        at=.2 d1=2 gaussian
        $strdmf='' $strdpwr=''
	write('line3','%g',dpwr):$strdpwr
//        format(dpwr,0,0):$strdpwr

        getparam('pwxlvl_cf','C13'):$attcf
        getparam('pwx','C13'):$pwx
        getparam('pwxlvl','C13'):$pwxlvl
        exp(($pwxlvl-dpwr)*2.303/20):$mult
        $pwx=$pwx*$mult*$attcf
        format(250000/$pwx,0,0):$strdmf

        cplog('PROTON','first'):$file,$asdir
        gettxt($asdir+'/'+$file)
        $text=dseq+' C13 decoupling profile:'
        $atext='  +/- 125ppm from on-resonance'
        $atext0='    in 5ppm steps'
        $atext1='C13 dec. gammah2: '+$strdmf
        $atext2 ='   at power level: '+$strdpwr
        atext($text)
        atext($atext) atext($atext0) atext($atext1) atext($atext2)

"****************************************************************"
ELSEIF ($arg='finish') THEN

	$CalibrationResults=CalibrationResults
        tmpsave('Cdec')

            cplog('PROTON','first'):$file,$asdir
            rt($asdir+'/'+$file)
            wft
            execprocess='wft dssh darray(\'CalibrationResults\')'
            execplot='execplot=\'\''
            xmhaha_calib('synonym',$0):caliblist

            getparam('tpwr'):tpwr
            getparam('pw90'):pw90
            pw=pw90
            setdecpars
            setref
	    $solv='' format(solvent,'lower'):$solv
            if ($solv='cdcl3') then
                f peak(1p,3p):$ht,cr
                wp=2p sp=1p
                $maxpeak=3
 	        j1xh=151
		setoffset(dn,-23):dof
            else
                f peak(2p,4p):$ht,cr
                wp=2p sp=2p
                $maxpeak=2
 	        j1xh=142
		setoffset(dn,49):dof
            endif
            $thresh=0.9 $peaks=0
            repeat
                th=$thresh*$ht nll:$peaks
                $thresh=$thresh-0.1
            until ($peaks > $maxpeak-1)
            cr=llfrq[1]-rfl+rfp wp=100
            sp=llfrq[1]-rfl+rfp-50
            seqfil='pwxcalib'
            vp=wc2max/2
//            setoffset(dn,-23):dof
            wnt='wft(`acq`) select(1) vsadj ph aph0 dssh'
	    calibnode='_PWXnutation'
            gaussian ph
	    $solv=''
	    format(solvent,'lower'):$solv
	    is_IP:$isIP
	    if (($isIP) and ($solv='cdcl3')) then
		getparam('pwx','C13'):C13pwx
	        profileflg='y'
		at=1 nt=4
		d1=1 fn=4*np lb=5
                substr(ap,'find','pwx(pwx):3','delimiter',',;'):$e
                if ($e=0) then
                  substr(ap,'find','p1(p1):3','delimiter',',;'):$e,$index,$len
                  substr(ap,$index,$len,'p1(p1):3,pwx(pwx):3'):ap
                endif
		$pwinc=(pwx/11)
   		format($pwinc,2,1):$pwinc
   		array('pwx',111,0,$pwinc)
	        calibnode='_pwx90_RFhomo'
	        $ep='' write('line3','wft wp=%0.1f sp=%0.1f select(1) vsadj dssh C13rfhomo_process(\'activestudy\')',wp,sp):$ep
  	        wstart='eOptions=IPeOptions'
	    else
                $pwinc= trunc((pwx/6)+0.5)
                array('pwx',50,$pwinc,$pwinc)
	        $ep='' write('line3','wft wp=%0.1f sp=%0.1f select(1) vsadj dssh',wp,sp):$ep
	    endif

            execprocess=$ep+' darray(\'CalibrationResults\')'
            execplot='calibplot'
	    CalibrationResults=$CalibrationResults

            xmhaha_calib('savePar','C13pwx')

            Cdec('profile')
	    wstart=''
            if (dseq='g') then $dseq='garp1' 
	    else 
		strstr(dseq,'W40'):$wok
		if ($wok) then $dseq='WURST40'
		else $dseq=dseq endif
	    endif

            $ep='' write('line3','wft wp=%0.1f sp=%0.1f select(21) vsadj(100) dssh',wp,sp):$ep
            execprocess=$ep+' darray(\'CalibrationResults\')'
            execplot='cpplss'
            CalibrationResults=$CalibrationResults

	    calibnode='_'+$dseq+'_Profile'
            xmhaha_calib('savePar','C13dec_profile')

        tmpreturn('Cdec')

ENDIF
