"macro sccheck"
if ($# < 0.5) then
   $seq = seqfil
else
   $seq = $1
endif
if ($# < 1.5) then
   $errFile = ''
else
   $errFile = $2
endif
if ($seq <> seqfil) then
   write('line3',`set seqfil='%s' for additional checking`,$seq)
   if ($# > 2.5) then
      write('file',$3,'done')
   endif
   return
endif
fixparvch
$info = userdir+'/spincad/info/'+$seq
exists($info,'file'):$e
if ($e < 0.5) then
   $info = systemdir+'/spincad/info/'+$seq
   exists($info,'file'):$e
endif
if ($e > 0.5) then
   lookup('file',$info,'read','read','read'):$dim,$chans,$obsch
   $siz = size('vchannelmap')
   if $siz <> $chans then
      vchannelmap=''
      $i = 1
      while ($i <= $chans) do
         vchannelmap[$i]=''
         $i=$i + 1
      endwhile
   endif
   $chan = '1','2','3','4','5','6','7','8'
   $chanval = 'tn','dn','dn2','dn3','dn4','dn5','dn6','dn7'
   $i = 1
   $rf=0
   $grad=0
   $gen=0
   $type='' $label=''
   while ($i <= $chans) do
      lookup('read','readline'):$type,$label
      $vch = 'vch' + $chan[$i]
      $val = vchannelmap[$i]
      $tval = 0
      if ($val = '0') or ($val = '1') or ($val = '2') or ($val = '3') then
         $tval = 1
      elseif ($val = '4') or ($val = '5') or ($val = '6') or ($val = '7') then
         $tval = 1
      endif
      if ($type = 'RF') then
         if ($tval = 0) then
            {$vch} = $chanval[$i]
         endif
         $rf=$rf+1
      elseif ($type = 'Gradient') then
         if ($tval = 1) or ($val = '') then
            {$vch} = 'z'
         endif
         $grad=$grad+1
      else
         if ($val <> '0') then
            {$vch} = 'tn'
         endif
         $gen=$gen+1
      endif
      $i=$i + 1
   endwhile
   $vch = 'vch' + $chan[$obsch]
   {$vch} = 'tn'
   numvch=$chans,$rf,$grad,$gen
   scpar($seq)
endif
if ($# < 0.5) then
   return
endif
shell('rm -f ' + userdir + '/psg.error'):$e
ABORTOFF
go('check')
ABORTON
exists(userdir+'/psg.error','file'):$e
if ($e > 0.5) then
   copy(userdir+'/psg.error',userdir + '/seqlib/'+$seq+'.error')
endif
if ($# > 2.5) then
   write('file',$3,'done')
endif
