"macro plotfile"
"*********************************"
" plotfile - plot into a PDF, Postscript, PCL, HPGL or JPEG file "
"*********************************"

if $#=0 then $arg='auto' else $arg=$1 endif

shell('which ps2pdfwr'):$ps2pdf
if $ps2pdf='' then
  write('error','No PDF converter found - please install Ghostscript first!')
  return
endif
$pldir=userdir+'/plot'
exists($pldir,'directory'):$exd
if not $exd then 
   shell('mkdir '+$pldir) 
endif
resetplotter('setup')

exists('plotfile','parameter'):$exf
if not $exf then
   create('plotfile','string')
endif
exists('plotfileformat','parameter','global'):$exff
if not $exff then 
   create('plotfileformat','string','global')
   plotfileformat='ps'
endif
exists('printout','parameter','global'):$expo
if not $expo then
   create('printout','string','global')
   printout='printer'
endif

$tmpps='/vnmr/tmp/tmpplot.ps'
exists($tmpps,'file'):$eold
if $eold then shell('rm -f '+$tmpps) endif

if $arg='setup' then return endif

if plotfile=''       then write('error','Please define a filename first!') return endif
if plotfileformat='' then write('error','Please choose a format first!') return endif

exists('pltmod','parameter'):$ep
if $ep then
  if pltmod='off' then 
    pltmod='fixed'
    write('line3','pltmod was set to "off" - reset to "fixed"!')
  endif
endif

Svfname:$next_saved_fid length($next_saved_fid):$ln
substr($next_saved_fid,$ln-4,1):$numstr
substr($next_saved_fid,1,$ln-5):$current_fid
$num=1      format($numstr,'lower'):$num $num=$num-1
$numstr2='' format($num,'lower'):$numstr2
$current_fid=$current_fid+$numstr2+'.fid'
  
exists($current_fid,'directory'):$ef
exists(file,'directory'):$efile

if file='exp' and $ef then
  $plotfile=$current_fid+'/'+plotfile                 // Plot to FID_directory.fid/myplot.xxx
elseif file<>'exp' and $efile then
  $plotfile=file+'/'+plotfile
else
  $plotfile=$pldir+'/'+plotfile           // Else plot to userdir/plots/myplot.xxx
endif

$ret=1
checkplotter:$new,$ret
if $ret=0 then
  return
else 
  if plotter<>$new then plotter=$new endif
endif

if $arg='manual' then page else plot endif

exists($tmpps,'file'):$eold
if not $eold then write('error','Error while printing - '+$tmpps+' does not exist!') return endif

if plotfileformat='pdf' then
  shell($ps2pdf+' '+$tmpps+' '+$plotfile+'.pdf')
elseif plotfileformat='ps' then
  shell('cp '+$tmpps+' '+$plotfile+'.ps')
elseif plotfileformat='jpg' then
  shell('convert -rotate 90 -density 300 -quality 95 '+$tmpps+' '+$plotfile+'.jpg')
elseif plotfileformat='png' then
  shell('convert -rotate 90 -density 300 -quality 95 '+$tmpps+' '+$plotfile+'.png')
elseif plotfileformat='pcl' then
  shell('cp '+$tmpps+' '+$plotfile+'.pcl')
elseif plotfileformat='hpgl' then
  shell('cp '+$tmpps+' '+$plotfile+'.hpgl')
else
  write('error','Please choose a format first!')
endif
write('line3','%s.%s written', $plotfile, plotfileformat)

resetplotter
