"macro P31sens_process"

$phase='dophase'
$activestudy='no'

$x = 1 
while ($x <= $#) do
 $val = ${$x}
 if $val = 'nophase' then $phase=$val
 elseif $val = 'activestudy' then $activestudy=$val
 endif
 $x=$x+1
endwhile

$date='' $time='' $snrs='' $lws='' $pass_threshold=0
$arraydims='' $avesnr=0 $sumsnr=0 $deltas=''
$minsnr=1e6 $maxsnr=-1e6 $minsnrspecno=0 $maxsnrspecno=0
$minsnrs='' $maxsnrs='' $minsnrspecnos='' $maxsnrspecnos=''

lb=1 wft(1) 
lb=5.5
fn=128k
wft
ai
f
full
setref
if $phase='dophase' then lp=0 aph0 endif
vp=0
vsadj
axis='h'

$i=1
repeat
	select($i)
	if $phase='dophase' then aph0 endif
	delta=2000
	cr=-rfl+2000
	dsnmax(2000)
	dsn:$snr
	if ($snr<$minsnr) then $minsnr=$snr $minsnrspecno=$i endif
	if ($snr>$maxsnr) then $maxsnr=$snr $maxsnrspecno=$i endif

	res
	dres:$lw
	write('line3','Weighted linewidth of TPP signal at half-height=%f Hz',$lw)
	if ($lw > 9) then write('error','Warning: Line is broad, check shimming') endif

	write('line3','rms signal-to-noise ratio=%d:1',$snr)
	$sumsnr=$sumsnr+$snr
	$i=$i+1
until ($i>arraydim)

$avesnr=$sumsnr/arraydim
$snr=$avesnr

format($minsnr,1,0):$minsnrs
format($minsnrspecno,1,0):$minsnrspecnos
format($maxsnr,1,0):$maxsnrs
format($maxsnrspecno,1,0):$maxsnrspecnos

VPtext
$str='31P signal-to-noise ratio measurement'
atext($str)
format($snr,1,0):$snrs
format(arraydim,1,0):$arraydims
format(delta,1,0):$deltas
$str='rms signal-to-noise ratio (average of '+$arraydims+' measurements) = '+$snrs+':1'
atext($str)
$str=$deltas+' Hz wide noise region used'
atext($str)
$text='Minimum measured signal-to-noise: '+$minsnrs+' (spectrum '+$minsnrspecnos+')'
atext($text)
$text='Maximum measured signal-to-noise: '+$maxsnrs+' (spectrum '+$maxsnrspecnos+')'
atext($text)
format($lw,1,1):$lws
$text='Resolution: '+$lws+' Hz'
atext($text)


$IPteststatus='FAIL' 
$targetvalues=''
VPgettarget('P31SN'):$targetvalue
format($targetvalue,1,0):$targetvalues
$text='Target: >= '+$targetvalues+':1'
atext($text)
if ($snr > $targetvalue) then $IPteststatus='PASS' endif
atext('Test status: '+$IPteststatus)

//Redisplay spectra and text
dssh dtext

write('line3','31P Sensitivity Test'):$line
CalibrationResults=CalibrationResults,$line
write('line3','Measured RMS signal-to-noise ratio %.1f:1',$snr):$line
CalibrationResults=CalibrationResults,$line
write('line3','Target >= %.1f:1',$targetvalue):$line
CalibrationResults=CalibrationResults,$line
write('line3','Test status: %s',$IPteststatus):$line
CalibrationResults=CalibrationResults,$line
VPgetspinrate:$line
atext($line)
CalibrationResults=CalibrationResults,$line

if ($activestudy<>'activestudy') then return endif

execprocess='wft dssh darray(\'CalibrationResults\')'
setvalue('execprocess',execprocess,'processed')

VPhistory($snr)

if ($IPteststatus='PASS') then
	"we passed so clear any retestfile counter info"
	$retestfile=userdir+'/persistence/ReTest'
	exists($retestfile,'file'):$et
	if ($et>0.5) then
		rm($retestfile)
	endif

elseif ($IPteststatus='FAIL') then
	DoPlanB($0)
endif
