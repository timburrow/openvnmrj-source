"macro C13ETBsens_process"
$phase='dophase'
$activestudy='no'

$x = 1 
while ($x <= $#) do
 $val = ${$x}
 if $val = 'nophase' then $phase=$val
 elseif $val = 'activestudy' then $activestudy=$val
 endif
 $x=$x+1
endwhile

$snrs='' $lws='' $deltas=''
$sumsnr=0 $avesnr=0 $arraydims=''
$minsnr=1e6 $maxsnr=-1e6 $minsnrspecno=0 $maxsnrspecno=0
$minsnrs='' $maxsnrs='' $minsnrspecnos='' $maxsnrspecnos=''

ft
ai
f
full
setref
abortoff
if $phase='dophase' then lp=0 aph0 endif
aborton
vp=0
axis='h'

$i=1
repeat
	ft
	select($i)
	if $phase='dophase' then aph0 endif	
	dc res
	dres(cr,0.5):$lw50
	write('line3','Half-height linewidth of tallest aromatic signal = %f Hz',$lw50)
	if ($lw50 > 0.15) then write('error','Warning: Line is broad, check shimming') endif

	//Use matched filtration
	lb=$lw50
	wft
	select($i)	
	cr=sp+10p
	delta=10p
	dsnmax(10p)
	dsn:$snr
	if ($snr<$minsnr) then $minsnr=$snr $minsnrspecno=$i endif
	if ($snr>$maxsnr) then $maxsnr=$snr $maxsnrspecno=$i endif
	$sumsnr=$sumsnr+$snr
	$i=$i+1
until ($i>arraydim)

$avesnr=$sumsnr/arraydim
$snr=$avesnr

format($minsnr,1,0):$minsnrs
format($minsnrspecno,1,0):$minsnrspecnos
format($maxsnr,1,0):$maxsnrs
format($maxsnrspecno,1,0):$maxsnrspecnos

write('line3','rms signal-to-noise ratio=%d:1',$snr)
VPtext
$str='13C sensitivity measurement (10% ETB)'
atext($str)
format($snr,1,0):$snrs
format(arraydim,1,0):$arraydims
format(delta,1,0):$deltas
$str='rms signal-to-noise ratio (average of '+$arraydims+' measurements) = '+$snrs+':1'
atext($str)
$str=$deltas+' Hz wide noise region used'
atext($str)
$text='Minimum measured signal-to-noise: '+$minsnrs+' (spectrum '+$minsnrspecnos+')'
atext($text)
$text='Maximum measured signal-to-noise: '+$maxsnrs+' (spectrum '+$maxsnrspecnos+')'
atext($text)
format($lw50,1,1):$lws
$text='Resolution (unweighted): '+$lws+' Hz'
atext($text)

vsadj(0.8*wc2max-vp)

write('line3','13C Sensitivity Test \(10%% Ethyl benzene\)'):$line
CalibrationResults=$line
write('line3','Measured RMS signal-to-noise ratio %.1f:1',$snr):$line
CalibrationResults=CalibrationResults,$line

$targetvalue=0
$targetvalues='NONE'
$IPteststatus='PASS'
VPgettarget('C13ETBSN'):$targetvalue

   format($targetvalue,1,0):$targetvalues
   $text='Target: >= '+$targetvalues+':1'
   atext($text)
   if ($snr < $targetvalue) then
	   atext('Test result: FAIL')
	   $IPteststatus='FAIL'
   else 
	   atext('Test result: PASS')
   endif
   write('line3','Target >= %.1f:1',$targetvalue):$line
   CalibrationResults=CalibrationResults,$line
   write('line3','Test status: %s',$IPteststatus):$line
   CalibrationResults=CalibrationResults,$line

VPgetspinrate:$line
atext($line)
CalibrationResults=CalibrationResults,$line

if ($activestudy<>'activestudy') then return endif

execprocess='wft dssh darray(\'CalibrationResults\')'
setvalue('execprocess',execprocess,'processed')

VPhistory($snr)

if ($IPteststatus='PASS') then
	"we passed so clear any retestfile counter info"
	$retestfile=userdir+'/persistence/ReTest'
	exists($retestfile,'file'):$et
	if ($et>0.5) then
		rm($retestfile)
	endif

elseif ($IPteststatus='FAIL') then
	DoPlanB($0)
endif
