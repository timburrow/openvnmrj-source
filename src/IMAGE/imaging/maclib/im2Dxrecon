"macro im2Dxrecon"
"**********************************************************"
" Parse input                                              "
"**********************************************************"
"Default options"
$action = 'prep'
$do     = ''

if ($# > 0) then
  $action = $1
endif
if ($# > 1) then
  $do = $2
endif

exists('profile','parameter'):$profile
exists('recon','parameter'):$exrecon
if ($exrecon) then $recon = recon else $recon = 'internal' endif

"**********************************************************"
" Is this FSE or multi-echo type of experiment?            "
"**********************************************************"
$is_fse = 0
$is_ne  = 0
$nfids  = 1
exists('etl','parameter'):$ex_etl
exists('ne','parameter'):$ex_ne

if ($ex_etl>0) then $is_fse = (etl > 1) endif
if ($ex_ne >0) then $is_ne  = (ne  > 1) endif

if ($is_fse AND $is_ne) then
  write('error','Cannot handle data with both etl and ne > 1')
  abort
endif
if ($is_fse) then $nfids = etl endif
if ($is_ne) then  $nfids = ne  endif

exists('navigator','parameter'):$nav
if ($nav>0) then
  if (navigator='y') then
    exists('nav_echo','parameter'):$ex
    if ($ex>0) then $nfids=$nfids+size('nav_echo') endif
  endif
endif

"**********************************************************"
" Do reconstruction                                        "
"**********************************************************"
if ($action = 'recon') then
  if (recon='sense') then imIR='n' endif
  write('line3','ADVISORY: Using external recon ...')
  flush
  "Clear old data"
  aipXrecon('cleardata')
  $cmd = 'Xrecon -v '+curexp+' >'+curexp+'/Xrecon.txt 2>&1'
  shell($cmd):$trash
  im2Dxrecon('displayall')
  clear
  cat(curexp+'/Xrecon.txt')
endif

"**********************************************************"
" Do profile reconstruction                                "
"**********************************************************"
if ($action = 'reconprofile') then
  write('line3','ADVISORY: Using external recon ...')
  "Do ft so VnmrJ thinks one is done"
  vs=1e-6 ft flush
  "Now write data and phasefile"
  $cmd = 'Xrecon -v '+curexp+' >'+curexp+'/Xrecon.txt 2>&1'
  shell($cmd):$trash
  clear
  cat(curexp+'/Xrecon.txt')
  aipXrecon('nrcvrs'):$nr
  if (arraydim = $nr) then
    av ds dscale f full vsadj
    if ($nfids > 1) then dssh(1,$nfids,1) else dscale endif
  else
    av f full vsadj dscale im2Dxrecon('displayprofiles','slice')
  endif
endif

"**********************************************************"
" Display All                                              "
"**********************************************************"
if ($action = 'displayall') then
  aipDeleteData('all')              "clear images"
  aipRQcommand('set','sort','yes')  "enable sorting"
  rqsort=1                          "sort by slices"
  rqsort2=1                         "sort by slices"
  "Figure the number of receivers"
  aipXrecon('nrcvrs'):$nr
  "Find the number of images and split window accordingly"
  $cmd='a="" ; for i in '+curexp+'/recon/*.fdf ; do a=$a"y" ; done ; echo $a'
  shell($cmd):$nimages
  length($nimages):$nframes
  aipSplitWindow($nframes,2,1)
  "Load and display data"
  $ext=''
  $reconflag=0	    

  "Magnitude output is treated differently: "
  "There is the option to display combined receiver output"
  im2Dxrecon('displayMGdir','recon','imMG','imIR',$nr):$reconflag
  im2Dxrecon('displaydir','reconPH','imPH',$nr):$reconflag
  im2Dxrecon('displaydir','reconRE','imRE',$nr):$reconflag
  im2Dxrecon('displaydir','reconIM','imIM',$nr):$reconflag
  im2Dxrecon('displayMGdir','rawMG','rawMG','rawIR',$nr):$reconflag
  im2Dxrecon('displaydir','rawPH','rawPH',$nr):$reconflag
  im2Dxrecon('displaydir','rawRE','rawRE',$nr):$reconflag
  im2Dxrecon('displaydir','rawIM','rawIM',$nr):$reconflag

  if ($reconflag) then
    aipRQcommand('load',curexp+'/recon',-1)
  endif
  aipDisplay('all')                 "display"

endif

"**********************************************************"
" Display Directory                                        "
"**********************************************************"
if ($action = 'displaydir') then

  if ($# > 4) OR ($# < 4) then return endif

  $dir=$2
  $flag=$3
  $nr=$4

  exists($flag,'parameter'):$e
  if ($e > 0) then
    if ({$flag} = 'y') then
      if ($nr > 1) then
        $i=1
	$ext=''
        while ($i <= $nr) do
          format($i+1000,4,0):$ext
          substr($ext,2,4):$ext
          exists(curexp+'/'+$dir+$ext,'directory'):$e
	  if ($e < 1) then return endif
          aipRQcommand('load',curexp+'/'+$dir+$ext,-1)
          $i=$i+1
        endwhile
      else	    
        exists(curexp+'/'+$dir,'directory'):$e
	if ($e < 1) then return endif
        aipRQcommand('load',curexp+'/'+$dir,-1)
      endif
      return(0)		
    endif
  endif

endif

"**********************************************************"
" Display Magnitude Directory                              "
"**********************************************************"
if ($action = 'displayMGdir') then

  if ($# > 5) OR ($# < 5) then return endif

  $dir=$2
  $flag1=$3
  $flag2=$4
  $nr=$5

  exists($flag1,'parameter'):$e
  if ($e > 0) then
    if ({$flag1} = 'y') then
      if ($nr > 1) then
        exists($flag2,'parameter'):$e
        if ($e > 0) then
          if ({$flag2} = 'y') then
            $i=1
            $ext=''
            while ($i <= $nr) do
              format($i+1000,4,0):$ext
              substr($ext,2,4):$ext
              exists(curexp+'/'+$dir+$ext,'directory'):$e
	      if ($e < 1) then return endif
              aipRQcommand('load',curexp+'/'+$dir+$ext,-1)
              $i=$i+1
            endwhile
          else
            exists(curexp+'/'+$dir,'directory'):$e
	    if ($e < 1) then return endif
            aipRQcommand('load',curexp+'/'+$dir,-1)
          endif
        else
          exists(curexp+'/'+$dir,'directory'):$e
          if ($e < 1) then return endif
          aipRQcommand('load',curexp+'/'+$dir,-1)
        endif
      else	    
        exists(curexp+'/'+$dir,'directory'):$e
        if ($e < 1) then return endif
        aipRQcommand('load',curexp+'/'+$dir,-1)
      endif
      if ($dir <> 'recon') then return(0) endif		
    else
      if ($dir = 'recon') then return(1) endif		    
    endif
  else
    if ($dir = 'recon') then return(1) endif		    
  endif

endif

"**********************************************************"
" Number of Receivers Acquiring Data                       "
"**********************************************************"
if ($action = 'nrcvrs') then
  $nr=0
  length(rcvrs):$len
  $i=1
  while ($i <= $len) do
    $str=''
    substr(rcvrs,$i,1):$str
    if ($str = 'y') then $nr=$nr+1 endif
    $i=$i+1
  endwhile
  return($nr)
endif

"**********************************************************"
" Display Profiles                                         "
"**********************************************************"
if ($action = 'displayprofiles') then
  seqcon('slice'):$slice
  if ($do = 'scale') then
    aipXrecon('nrcvrs'):$nr 
    if (slices='all') and ($slice='c') then
      if ($recon = 'internal') OR ($recon = '') then 
        ft('nf',rc) dssh
      else "external recon"
        $start=(rc-1)*ns*$nfids+1 $end=(rc-1)*ns*$nfids+ns*$nfids $step=1
        dssh($start,$end,$step)
        if ($start=$end) then dscale endif
      endif
    elseif (arraydim>$nr) then
      im2Dxrecon('displayprofiles','echo')
    else
      if ($recon = 'internal') OR ($recon = '') then 
        ft('nf',rc) 
        if (slices='all') then dssh 
        else dssh(sl*$nfids-$nfids+1,sl*$nfids,1) endif
      else "external recon"
        if (slices='all') then $start=(rc-1)*ns*$nfids+1 $end=(rc-1)*ns*$nfids+ns*$nfids
        else $start=(rc-1)*ns*$nfids+sl*$nfids-$nfids+1 $end=(rc-1)*ns*$nfids+sl*$nfids endif
        $step=1
        dssh($start,$end,$step)
        if ($start=$end) then dscale endif
      endif
    endif
  endif
  if ($do = 'slice') then
    if (sl<1) then sl=1 endif 
    if (sl>ns) then sl=ns endif 
    if (slices='single') then
      aipXrecon('nrcvrs'):$nr
      if (arraydim>$nr) then
        im2Dxrecon('displayprofiles','echo')
      else
        if ($slice='c') then
          if ($recon = 'internal') OR ($recon = '') then 
            ft('nf',rc) f full dssh(sl*$nfids-$nfids+1,sl*$nfids,1)
          else "external recon"
            $start=(rc-1)*ns*$nfids+sl*$nfids-$nfids+1 $end=(rc-1)*ns*$nfids+sl*$nfids $step=1
            f full dssh($start,$end,$step)
            if ($start=$end) then dscale endif
          endif
        endif
      endif
    endif
  endif
  if ($do = 'allslices') then
    if ($slice='c') then 
      if ($recon = 'internal') OR ($recon = '') then 
        ft('nf',rc) dssh
      else "external recon"
        $start=(rc-1)*ns*$nfids+1 $end=(rc-1)*ns*$nfids+ns*$nfids $step=1
        dssh($start,$end,$step)
        if ($start=$end) then dscale endif
      endif
    endif
  endif
  if ($do = 'echo') then
    if (ec<1) then ec=1 endif 
    if (ec>$nfids) then ec=$nfids endif 
    if (slices='single') or ($slice='s') then
      aipXrecon('nrcvrs'):$nr 
      if ($recon = 'internal') OR ($recon = '') then 
        cf=(sl-1)*$nfids+ec
        ft dssh(rc,arraydim,$nr)
      else "external recon"
        $start=(rc-1)*ns*$nfids+sl*$nfids-$nfids+ec $end=ns*$nfids*arraydim $step=ns*$nfids*$nr
        dssh($start,$end,$step)
        if ($start=$end) then dscale endif
      endif
    endif
  endif
  if ($do = 'rcvr') then
    if (rc<1) then rc=1 endif 
    aipXrecon('nrcvrs'):$nr 
    if (rc>$nr) then rc=$nr endif 
    if (slices='all') and ($slice='c') then 
      im2Dxrecon('displayprofiles','allslices')
    else
      im2Dxrecon('displayprofiles','slice')      
    endif
  endif
endif
