"macro prescan_params"
"********************************************************************
"
"
" NAME:
"    prescan_params
"
" DESCRIPTION:
"    Save/Load experiment parameters
"
"  Ex. prescan_params(<argument>)
"
"    <argument> = 'load'  -- experiment and associated parameters are
"                            restored.
"    <argument> = 'save'  -- experiment and associated parameters are
"                            saved.
"
"********************************************************************
"---------------------------------"
" Define and initialize variables "
"---------------------------------"
$macro_name = $0

if ($# <> 1) then
  write('error',`Use: %s(<mode>) -- <mode> is 'save'|'load'`,$0)
  prescan_exit('abort')
endif

"----------------------------"
" Save experiment parameters "
"----------------------------"
if ($1 = 'save') then
  
  if ((activestudy <> 'null') AND (actionid <> 'null')) then
    $status = ''
    xmgetatts(actionid,'status'):$status
    if ($status <> 'Completed') and ($status <> 'Error') then
      Prescan_activeid = actionid
      xmselect('savepar')  "Save parameters in <nodeid>/save.par and set status='Customized'"
    else "just save it in curexp"
      Prescan_activeid = 'null'
      $file = curexp+'/save'
      exists($file+'.par','file'):$e
      if $e > 0.5 then
        delete($file)
      endif
      svp($file,'nodb')
    endif
  else
    $file = curexp+'/save'
    exists($file+'.par','file'):$e
    if $e > 0.5 then
      delete($file)
    endif
    svp($file,'nodb')
  endif
  prescan_log('save',$macro_name,'save parameter')

  actionid = 'null'
     
     
"----------------------------"
" Load experiment parameters "
"----------------------------"
elseif ($1 = 'load') then
  prescan_log('restore',$macro_name,'restore parameter')

  if (activestudy <> 'null') then
    if (Prescan_activeid <> 'null') then
      actionid = Prescan_activeid
      ds('exit')
      xmselect('prescan',Prescan_activeid)
      xmsetattr('status','Active')
    else "Prescan_activeid is null, try file in curexp"
      $file = curexp +'/save'
      exists($file+'.par','file'):$e
      if $e > 0.5 then
        rtp($file,'nomenu')
      endif
    endif
    wdone=''
  else "no active study"
    $file = curexp +'/save'
    exists($file+'.par','file'):$e
    if $e > 0.5 then
      rtp($file,'nomenu')
    endif
  endif
endif "save or load"
