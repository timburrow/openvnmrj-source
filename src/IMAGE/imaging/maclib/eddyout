"macro eddyout"
"***********************************************************************"
"eddyout(first,last) - plot and eddy-analyze a series of spectra	"
"***********************************************************************"


"[0] CONSTANTS AND DECLARATIONS"
$first=$1
$last=$2
$answ=' '
$answ1=' '
$answ2=' '
$outstr=' '
$1str=' '
$d2str=' '
$frq1str=' ' 
$tcstr=' '
$ampstr=' '
$frq11str=' '
$tc1str=' '
$amp1str=' '


"[1] SET DISPLAY AND TRANSMITTER REFERENCES"
rfl=0
select(arraydim)
peak:$ht,$frq
rfl=$frq					"set reference to last spectra"
  if ($frq > (sw/2+10)) or ($frq < (sw/2-10))
    then 
      write ('error','tof out of limits, move tof by %8.1f Hz  ',(rfl-sw/2))
  endif
wp=sw
sp=sw/2
wc=160


"[2] DISPLAY ALL SPECTRA ON GRAPHICS WINDOW"
dss($1,$2,1)
dscale
clear


"[3] EXTRACTION OF PEAK FREQUENCIES"
shell('echo > frqfile')
repeat
  select($1)
  peak:$ht,$frq
  shell('echo',$frq,'>>frqfile')
  $1 = $1+1
until $1 > $2

$1=$first
$2=$last


"[4] CALCULATE TIME CONSTANTS AND AMPLITUDES AND DISPLAY IN TEXT WINDOW"

shell('echo > outfile')
write('file','outfile','INDEX  DELAY    FREQUENCY     TC      AMP  ')
$count=0

repeat
  select($1)
  lookup('file','frqfile')
  lookup('skip',$count,'read'):$frq1

  if (($1+1) <= $2)
    then
      select($1+1)
      lookup('read'):$frq2

      if ($frq1 < 1.0) or ($frq2 < 1.0) or ($frq1 = $frq2) 
        then
          write('file','outfile','%3d    %3.3f %12.3f    X.XXX     XX.X  ',
                        $1,d2[$1],$frq1)
        else
	  _calctc(d2[$1],d2[$1+1],$frq1,$frq2):$tc,$amp
	  format($amp,6,1):$ampstr
          if ($tc < 0) 
	    then
              write('file','outfile','%3d    %3.3f %12.3f   %3.3f   '+$ampstr,
                             $1,d2[$1],$frq1,$tc)
            else
              write('file','outfile','%3d    %3.3f %12.3f    %3.3f   '+$ampstr,
                             $1,d2[$1],$frq1,$tc)
          endif
        endif
      else
        write('file','outfile','%3d    %3.3f %12.3f ',
		      $1,d2[$1],$frq1)
   endif
$1=$1+1
$count=$count+1
until $1>$2

clear
shell('cat outfile')
shell('rm outfile')



"[5] CALCULATION OF CORRECTION EFFECT"

input('Do you want to calculate correction effects?  [y/n]'):$answ1

if ($answ1 = 'y')
  then
    input('Enter correction time constant and amplitude:  '):$tcc,$ampc

    input('Enter local gradient strength (in Hz): '):$glc

    $alpha=$ampc/$glc
    $ampsc=($glc*$alpha)/(1-$alpha)
    $T2=$tcc
    $T1=$tcc - ($tcc/100)

    $step = $tcc/100

    repeat
      exp((-$T2/$T1)):$exp1
      $exp2 = 0.367879 * $alpha
      $trialnew = ($ampsc*$T1/($T1 - $T2) * (((($T1-($T2 * (1-$alpha)))/ 
                  $T1) * $exp1) - $exp2)) - $glc * $exp2


      if ($trialnew < 0)
        then
	  write ('error','select at ECC board: gain = %3.0fpc, timect = %6.3fs',
                         (100/$glc * $ampsc),$T1)
      endif

      $T1 = $T1 - $step
    until ($trialnew < 0)
    $T1 = $T1 + $step


    $1=$first
    $2=$last
    shell('echo > outfile') 
    write('file','outfile','INDEX  DELAY    FREQ     TC      AMP     NEWF    NEWTC ')

    $count=0
    repeat
      select($1)
      lookup('file','frqfile')
      lookup('skip',$count,'read'):$frq1
      $arg = d2[$1]/$tcc
      if ($arg < -20) then $arg =-20 endif 
      exp($arg):$mul
      $frq11=$frq1-($ampc*$mul)

      format($1,3,0):$1str
      format(d2[$1],9,3):$d2str
      format($frq1,8,0):$frq1str
      format($frq11,8,0):$frq11str	  

      if (($1+1) <= $2)
        then
          select($1+1)
	  lookup('read'):$frq2
          $arg = d2[$1+1]/$tcc
          if ($arg < -20) then $arg =-20 endif 
          exp($arg):$mul
	  $frq21=$frq2-($ampc*$mul)

	  if($frq1 < 1.0) or ($frq2 < 1.0) or ($frq1 = $frq2)
            then
	      $outstr=$1str+$d2str+$frq1str+'      XXX     XXX'
	              +$frq11str+'      XXX '
	    else
	      if ($frq11 < 1.0) or ($frq21 < 1.0) or ($frq11 = $frq21) 
	        then
	          _calctc(d2[$1],d2[$1+1],$frq1,$frq2):$tc,$amp
		  format($tc,9,3):$tcstr
		  format($amp,8,0):$ampstr
		  $outstr=$1str+$d2str+$frq1str+$tcstr+$ampstr
			  +$frq11str+'      XXX '	
		else
	          _calctc(d2[$1],d2[$1+1],$frq1,$frq2):$tc,$amp
	          _calctc(d2[$1],d2[$1+1],$frq11,$frq21):$tc1,$amp1
		  format($tc,9,3):$tcstr
		  format($amp,8,0):$ampstr
	   	  format($tc1,9,3):$tc1str
		  format($amp1,8,0):$amp1str
		  $outstr=$1str+$d2str+$frq1str+$tcstr+$ampstr
			  +$frq11str+$tc1str
	      endif	
	    endif
	  else
	    $outstr=$1str+$d2str+$frq1str+'         '+$frq11str+'         '
	endif

      write('file','outfile','%s',$outstr)

    $1=$1+1
    $count=$count+1
    until $1>$2

    clear
    shell('cat outfile')
    shell('rm outfile')
endif




"[6] PLOT ALL SPECTRA AND PARAMETERS ON PLOTTER (NO CORRECTIONS)"

input ('Do you want a hardcopy? [y/n]'):$answ2

if ($answ2 = 'y') and ($answ1 <> 'y')
  then 

    $1=$first
    $2=$last

    input ('Enter title of Plot [ret when done]  '):$answ
    title($answ)

    pl($1,$2,1)
    pscale
    pap						"plot all parameters"

    $x=5					"set evaluation plot field"
    $y=10
    $yoffset=3

    $count=0
    repeat
      $y=$y+$yoffset
      select($1)
      lookup('file','frqfile')
      lookup('skip',$count,'read'):$frq1

      if (($1+1) <= $2) 
        then 			"calc. time constants and amps"
          select($1+1)
	  lookup('read'):$frq2
	
          if ($frq1 < 1.0) or ($frq2 < 1.0) or ($frq1 = $frq2) 
            then
              write('plotter',$x,$y,'%3d    %3.3f %12.3f    X.XXX     XX.X  ',
                                     $1,d2[$1],$frq1)
            else
	      _calctc(d2[$1],d2[$1+1],$frq1,$frq2):$tc,$amp
	      format($amp,6,1):$ampstr
              if ($tc < 0) 
	        then
                  write('plotter',$x,$y,'%3d    %3.3f %12.3f   %3.3f   '
				  +$ampstr,$1,d2[$1],$frq1,$tc)
 		else
                  write('plotter',$x,$y,'%3d    %3.3f %12.3f    %3.3f   '
				  +$ampstr,$1,d2[$1],$frq1,$tc)
              endif
            endif
          else
            write('plotter',$x,$y,'%3d    %3.3f %12.3f  ',$1,d2[$1],$frq1)
       endif
    $1=$1+1
    $count=$count+1
    until $1>$2
    $y=$y+$yoffset
    write('plotter',$x,$y,'INDEX  DELAY    FREQUENCY     TC      AMP  ')
    page
    shell('rm frqfile')
endif


"[7] PLOT ALL SPECTRA AND PARAMETERS ON PLOTTER (WITH CORRECTIONS)"
	
if($answ2='y') and ($answ1='y')
  then

  $1=$first
  $2=$last

  input ('Enter title of Plot [ret when done]  '):$answ
  title($answ)

  pl($1,$2,1)
  pscale
  pap						"plot all parameters"

  $x=0					"set evaluation plot field"
  $y=2
  $yoffset=3

  write('plotter',$x,$y,
  'local gradient strength:                          %7.0fHz',$glc)

  $y=$y+$yoffset

  write('plotter',$x,$y,
  'correction calculated with:      tc=%3.3f and amp=%6.0fHz', $tcc,$ampc)

  $y=$y+$yoffset

  write('plotter',$x,$y,
  'calculated values for ECC board: tc=%3.3f and amp=%6.0fpc',
  $T1,(100/$glc*$ampsc))

  $y=$y+$yoffset

  $count=0
  repeat
    $y=$y+$yoffset
    select($1)
    lookup('file','frqfile')
    lookup('skip',$count,'read'):$frq1
    $arg = d2[$1]/$tcc
    if ($arg < -20) then $arg =-20 endif 
    exp($arg):$mul
    $frq11=$frq1-($ampc*$mul)

    format($1,3,0):$1str
    format(d2[$1],9,3):$d2str
    format($frq1,8,0):$frq1str
    format($frq11,8,0):$frq11str	  

    if (($1+1) <= $2)
      then
        select($1+1)
        lookup('read'):$frq2
        $arg = d2[$1+1]/$tcc
        if ($arg < -20) then $arg =-20 endif 
        exp($arg):$mul
        $frq21=$frq2-($ampc*$mul)

        if($frq1 < 1.0) or ($frq2 < 1.0) or ($frq1 = $frq2)
          then
            $outstr=$1str+$d2str+$frq1str+'      XXX     XXX'
	            +$frq11str+'      XXX     '
	  else
	    if ($frq11 < 1.0) or ($frq21 < 1.0) or ($frq11 = $frq21) 
	      then
	        _calctc(d2[$1],d2[$1+1],$frq1,$frq2):$tc,$amp
	        format($tc,9,3):$tcstr
		format($amp,8,0):$ampstr
		$outstr=$1str+$d2str+$frq1str+$tcstr+$ampstr
			+$frq11str+'      XXX  '	
              else
	        _calctc(d2[$1],d2[$1+1],$frq1,$frq2):$tc,$amp
	        _calctc(d2[$1],d2[$1+1],$frq11,$frq21):$tc1,$amp1
		format($tc,9,3):$tcstr
		format($amp,8,0):$ampstr
	   	format($tc1,9,3):$tc1str
		format($amp1,8,0):$amp1str
		$outstr=$1str+$d2str+$frq1str+$tcstr+$ampstr
			+$frq11str+$tc1str
	    endif	
	  endif
	else
	  $outstr=$1str+$d2str+$frq1str+'         '+$frq11str+'         '
      endif
      write('plotter',$x,$y,'%s',$outstr)
      $1=$1+1
      $count=$count+1
    until $1>$2

  $y=$y+$yoffset
  write('plotter',$x,$y,'INDEX  DELAY    FREQ     TC      AMP     NEWF    NEWTC   ')

  page
endif

shell('rm frqfile')
