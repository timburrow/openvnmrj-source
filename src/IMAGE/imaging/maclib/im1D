"macro im1D"
"Setup and Processing for 1D localized spectroscopy sequences"

"**********************************************************"
" Parse input                                              "
"**********************************************************"
"Default options"
$action = 'prep'
$do = ''

if ($# > 0) then
  $action = $1
endif
if ($# > 1) then
  $do = $2
endif

exists('recon','parameter'):$exrecon
if ($exrecon) then $recon = recon else $recon = 'internal' endif

"**********************************************************"
" Setup                                                    "
"**********************************************************"
if ($action = 'prep') then
  ldrf  //set resto=H1offset
  tof=resto-restol 
  rfl=rfl+rfladjust+rfloffset rfladjust=0 rfloffset=0 //rfl =position of the reference line
  roff=roff+roffadjust roffadjust=0  //roff is the receiver offset
  
  //check if nt needs to be reset
  if ($do='ntopt') then
    //check whether nt is used, if not then set nt=1
    if (ntopt=0) then
      create('specnt','integer','current',1) specnt=nt nt=1 
    endif
  endif

  if ($recon = 'internal') OR ($recon = '') then   "use internal ft"
    wnt='wft dssh'
  else  "external recon"
    wnt='im1D(\'proc\')'
  endif

  execseq('prep')  "Execute any sequence specific preparation"
endif 

"**********************************************************"
" Processing & Display                                     "
"**********************************************************"
if ($action = 'proc') then
  dmg='ph'
  create('refval','real','current',4.75)

  if ($recon = 'internal') OR ($recon = '') then   "use internal ft"
    vp=0 f full wft aph0 dc vsadj ds
  else  "external recon"
    write('line3','ADVISORY: Using external recon ...')
    "Do ft so VnmrJ thinks one is done"
    vp=0 vs=1e-6 ft flush
    "Now write data and phasefile"
    $cmd = 'Xrecon -v '+curexp+' >'+curexp+'/Xrecon.txt 2>&1'
    shell($cmd):$trash
    clear
    cat(curexp+'/Xrecon.txt')
    aipXrecon('nrcvrs'):$nr
    if (arraydim = $nr) then
      ds aph0 f full dc vsadj
      if (ne > 1) then dssh(1,ne,1) endif
    else
      aph0 f full vsadj dssh(1,arraydim,$nr)
    endif
  endif

  rl(refval*sfrq) axis='p'
  create('specrfl','real','current',0):$rfl
  if ($rfl=0) then rfl=specrfl endif //retrieve specrfl to set ref
  if ($do='ntopt') then
    if(ntopt=0) then
      create('specnt','integer','current',1) nt=specnt destroy('specnt')
    endif
  endif
  execseq('proc')  "Execute any sequence specific processing
endif

"**********************************************************"
" Prescan                                                  "
"**********************************************************"
if ($action = 'prescan') then
  im1D('prep')
  
  "Here could go optimization of water suppression"
  if ($do = '') then
    $do = prescan_exec[prescan_control[2]]
  endif
  
  wexp = ''
endif
