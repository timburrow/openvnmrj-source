"macro testgdelays"
/******************************************************************
*  setgradadvance: Set up standard gems sequence to calibrate the
*  gpropdelay parameter for gradient propagation delay
*
*  The optimum value of gpropdelay will be written to the gradtable
*  file for the gradient coil currently selected by gcoil
*
*  This procedure requires that the system be externally triggered.
*  The easiest way to do this is to connect the 60Hz line sync on
*  the back of the system power supply to the External Trigger
*  input
*  
*  To use this setup procedure, first load the standard GEMS
*  protocol, then run setgradadvance.  The initial protocol will be
*  modified.  When the calibration is complete, discard this protocol.
*
*  ARR   Version 1.0   10/11/06 
*******************************************************************/

"-- Check for proper gcoil value --------------------------"
if (gcoil = '') or (gcoil = 'None') or (gcoil = 'none') then
  write('error','Configure gcoil before proceeding')
  return
endif

"-- Check existence of file in gradtables -----------------"
$table = systemdir+'/imaging/gradtables/'+gcoil
exists($table,'file'):$ex1
if $ex1 < 0.5 then
  write('error','%s',$prog+'Cannot locate requested gradtable file')
  return
endif

if ($# = 0) then
  "-- Read gradtables file ----------------------------------"

  "-- Measurement uses standard GEMS sequence ---------------"
  if (seqfil <> 'gems') then
    write('error','Load gems protocol before running setgradadvance')
    return  
  endif

  if ((array <> '') OR (arraydim > 1)) then
    write('error','setgradadvance: one or more parameters are arrayed')
    return
  endif

  if (ns > 1) then
    write('error','setgradadvance: set number of slices to 1')
    return
  endif

  exists('gpropdelay','parameter'):$ex1
  if ($ex1 < 0.5) then
    create('','pulse')
  endif
  _gcoil
  "$predelay=gpropdelay/2.0"
  
  wexp = 'testgdelays(\'proc\')'
  wnt = ''
  p1 = 100
  flip1 = 5
  nf = 0
  profile = 'yy'
  ticks = 1
  tr = 0.5
  ss = 2
  setarray('gpropdelay',0,1,100)
  "setarray('gpropdelay',$predelay,2,32)"
  au
  write('line3','gradadvance calibration started; make sure external trigger is connected')
endif  "Initialize and start scan"

/*---------------------------------------------------------
* Process and analyze data, update gpropdelay value in gradtables file
* This section is entered with the 'proc' option: setgradadvance('proc')
*---------------------------------------------------------*/
if ($# = 1) then
  if ($1 <> 'proc') then
    write('error','Use testgdelays(\'proc\') to process')
    abort
  endif

  wft
  f full
  
  "Find tallest profile in the array"
  findmaxpeak:$index
  select($index)  vsadj  dssh

  "gradient propagation delay is the array value at the returned index"
  $maxdelay = gpropdelay[$index]
  _gcoil
  "$predelay=gpropdelay/2.0"

  "-- Update current gradtable file -------------------------"
  "write('line3','measured delay = %g predelay = %g usec.',$maxdelay-gpropdelay,$predelay)"
  write('line3','measured delay error = %g ',$maxdelay-gpropdelay)

endif  "process and analyze"

