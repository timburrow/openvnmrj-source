"macro parimage"
"***************************************************************"
" parimage                                                      "
"                                                               "
" Uses existing parameters as starting point to create all      "
" standard imaging parameters                                   "
"                                                               "
" If called with a sequence name argument, then calls           "
" pardg(sequence_name) to set the proper dg string, and         "
" initparimage(sequence_name) to initialize the required        "
" parameters for the named sequence.                            "
"                                                               "
" Usage:  parimage<(sequence_name)>                             "
"***************************************************************"

$prog = 'parimage: '                      "program ID"

"** Define parameter lists ********************************"
" Parameters are defined in SETs. Each SET consists of:    "
"                                                          "
" 1. A list variable which defines all the parameter names "
"    in the SET.                                           "
"                                                          "
" 2. A type, group, max, min, step, and protection variable"
"    to be used in creating the parameters in the SET list."
"**********************************************************"

"** Initialize the SET counter ****************************"
$k = 0


"** Observe RF pulselengths *******************************"
$k = $k + 1  $set[$k] = 'RFpulses'
${$set[$k]} = 'p1','p2','p3','p4','p5','pw','pi','psat','pmt'

$type[$k]='pulse'   $group[$k]='acquisition'
$max[$k]=13   $min[$k]=13   $step[$k]=13   $protect[$k]=8192


"** RF pattern parameters *********************************"
$k = $k + 1  $set[$k] = 'RFpatterns'
${$set[$k]} = 'p1pat','p2pat','p3pat','p4pat','p5pat','pwpat','pipat','satpat',\
              'mtpat',\
	      'decpat','decpat1','decpat2','decpat3','decpat4','decpat5'

$type[$k]='string'   $group[$k]='acquisition'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Transmitter Power levels ******************************************"
$k = $k + 1  $set[$k] = 'Xmtr_Powers'
${$set[$k]} = 'tpwr','tpwr1','tpwr2','tpwr3','tpwr4','tpwr5','tpwri','satpwr',\
              'mtpwr'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=17   $min[$k]=17   $step[$k]=17   $protect[$k]=8192


"** Decouple Power levels ******************************************"
$k = $k + 1  $set[$k] = 'Dec_Powers'
${$set[$k]} = 'dpwr','dpwr1','dpwr2','dpwr3','dpwr4','dpwr5'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=9   $min[$k]=9   $step[$k]=9   $protect[$k]=8192


"** Gradient levels ***************************************"
$k = $k + 1  $set[$k] = 'Gradients'
${$set[$k]} = 'gmax','gro','gror','gss','gssr','gpe','gpe2','gpe3',\
	      'gvox1','gvox2','gvox3','gdiff','gflow','gspoil','gcrush',\
	      'gtrim','g0','g1','g2','g3','g4','g5',\
	      'gx','gy','gz','gpemult','gradcon'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=1000   $min[$k]=-1000   $step[$k]=0   $protect[$k]=0


"** Gradient pattern parameters ***************************"
$k = $k + 1  $set[$k] = 'Grad_patterns'
${$set[$k]} = 'gropat','gpepat','gsspat','gpat','gpat1','gpat2','gpat3'

$type[$k]='string'   $group[$k]='acquisition'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Delays ************************************************"
$k = $k + 1  $set[$k] = 'Delays'
${$set[$k]} = 'd1','d2','d3','d4','d5','tr','te','ti','tm','tpe','tpe2',\
	      'tpe3','tdelta','tDELTA','tflow','tspoil','tcrush','hold',\
	      'trelax','trise','tau','tau1','tau2'

$type[$k]='delay'   $group[$k]='acquisition'
$max[$k]=14   $min[$k]=14   $step[$k]=14   $protect[$k]=8192


"** Frequency offsets *************************************"
$k = $k + 1  $set[$k] = 'Offsets'
${$set[$k]} = 'resto','satfrq','wsfrq','chessfrq','mtfrq'

$type[$k]='frequency'   $group[$k]='acquisition'
$max[$k]=8   $min[$k]=8   $step[$k]=8   $protect[$k]=8192


"** Positions and Dimensions ******************************"
$k = $k + 1   $set[$k] = 'Positions'
${$set[$k]} = 'pro','pss','ppe','ppe2','ppe3','pos1','pos2','pos3',\
	      'lro','lpe','lpe2','lpe3','lss','vox1','vox2','vox3','thk',\
	      'fovcon','thkcon','B0','boresize','rfphase'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=1e6   $min[$k]=-1e6   $step[$k]=0   $protect[$k]=0


"** Bandwidths ********************************************"
$k = $k + 1   $set[$k] = 'Bandwidths'
${$set[$k]} = 'sw1','sw2','sw3'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=2e6   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Orientation string parameters *************************"
$k = $k + 1  $set[$k] = 'orientstrings'
${$set[$k]} = 'seqcon','orient','orient2','vorient','dorient','sorient'

$type[$k]='string'   $group[$k]='acquisition'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Main Euler angles *************************************"
$k = $k + 1   $set[$k] = 'Main_angles'
${$set[$k]} = 'psi','phi','theta'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=1e6   $min[$k]=-1e6   $step[$k]=0   $protect[$k]=8


"** Secondary Euler angles ********************************"
$k = $k + 1   $set[$k] = 'Other_angles'
${$set[$k]} = 'vpsi','vphi','vtheta','dpsi','dphi','dtheta',\
	      'spsi','sphi','stheta','psi2','phi2','theta2'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=1e6   $min[$k]=-1e6   $step[$k]=0   $protect[$k]=0


"** Low level acquisition counters ************************"
$k = $k + 1   $set[$k] = 'Acquisition_counters'
${$set[$k]} = 'ni','nf','nD','ticks'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=1e6   $min[$k]=0   $step[$k]=1   $protect[$k]=1


"** High level acquisition counters **********************"
$k = $k + 1   $set[$k] = 'View_counters'
${$set[$k]} = 'ns','ne','nv','nv2','nv3','ssc'

$type[$k]='real'   $group[$k]='acquisition'
$max[$k]=1e6   $min[$k]=0   $step[$k]=1   $protect[$k]=9


"** Sequence flags ***************************************"
$k = $k + 1   $set[$k] = 'Flags'
${$set[$k]} = 'ir','ws','mt','pilot','rfspoil','verbose','presig'

$type[$k]='flag'   $group[$k]='acquisition'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Misc. string parameters *******************************"
$k = $k + 1  $set[$k] = 'Misc_string'
${$set[$k]} = 'rfcoil','petable','exptype','acqtype','apptype','t_array',\
	      'gcoil','gradunit','fovunit','thkunit'

$type[$k]='string'   $group[$k]='acquisition'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=1

                        
"** RF pulse lists ***************************************"
$k = $k + 1   $set[$k] = 'RFlists'
${$set[$k]} = 'plist','patlist','pwrlist','sslist'

$type[$k]='string'   $group[$k]='processing'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Target positions, dimensions, fliplist ***************"
$k = $k + 1   $set[$k] = 'Targets'
${$set[$k]} = 't_pro','t_pss','t_ppe','t_ppe2','t_ppe3','t_pos1',\
	      't_pos2','t_pos3','t_lro','t_lpe','t_lpe2','t_lpe3',\
	      't_vox1','t_vox2','t_vox3','t_thk','t_gap','t_psi',\
	      't_phi','t_theta','t_vpsi','t_vphi','t_vtheta',\
	      't_mark','fliplist'

$type[$k]='real'   $group[$k]='processing'
$max[$k]=1e6   $min[$k]=-1e6   $step[$k]=0   $protect[$k]=0


"** Processing indeces ************************************"
$k = $k + 1   $set[$k] = 'Indeces'
${$set[$k]} = 'slice','echo','element','t_ns','cf'

$type[$k]='real'   $group[$k]='processing'
$max[$k]=1e6   $min[$k]=0   $step[$k]=1   $protect[$k]=1


"** Weighting constants ***********************************"
$k = $k + 1   $set[$k] = 'Apodization'
${$set[$k]} = 'lb','lb1','lb2','lb3','gf','gf1','gf2','gf3','gfs','gfs1',\
	      'gfs2','gfs3','sb','sb1','sb2','sb3','sbs','sbs1','sbs2',\
	      'sbs3','awc','awc1','awc2','awc3','rfl2','rfp2'

$type[$k]='real'   $group[$k]='processing'
$max[$k]=10000   $min[$k]=-10000   $step[$k]=1e-6   $protect[$k]=1


"** Scratch real parameters *******************************"
$k = $k + 1   $set[$k] = 'Real_scratch'
${$set[$k]} = 'zero','r1','r2','r3','r4','r5','r6','r7'

$type[$k]='real'   $group[$k]='processing'
$max[$k]=1e17   $min[$k]=-1e17   $step[$k]=0   $protect[$k]=0


"** Scratch string parameters *****************************"
$k = $k + 1   $set[$k] = 'String_scratch'
${$set[$k]} = 'n1','n2','n3'

$type[$k]='string'   $group[$k]='processing'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** Grayscale parameters **********************************"
$k = $k + 1   $set[$k] = 'grayscale'
${$set[$k]} = 'grayctr','graysl'

$type[$k]='real'   $group[$k]='display'
$max[$k]=1e6   $min[$k]=-1e6   $step[$k]=0   $protect[$k]=3


"** Phasing parameters ************************************"
$k = $k + 1   $set[$k] = 'Phasing'
${$set[$k]} = 'lp','lp1'

$type[$k]='real'   $group[$k]='processing'
$max[$k]=1e6   $min[$k]=-1e6   $step[$k]=0   $protect[$k]=3


"** dg screen parameters **********************************"
$k = $k + 1  $set[$k] = 'dgscreens'
${$set[$k]} = 'dg','dg1','dggrad','dgdelay','dgfov','dgpulse','dgseq',\
	      'dgproc','dgslice','dgtarg','t_dgvox','dconi','dcrmv'

$type[$k]='string'   $group[$k]='display'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=0


"** dg title parameters ***********************************"
$k = $k + 1  $set[$k] = 'dgtitles'
${$set[$k]}='gradtitle','fovtitle','thktitle','voxtitle','vpostitle','spostitle'

$type[$k]='string'   $group[$k]='display'
$max[$k]=0   $min[$k]=0   $step[$k]=0   $protect[$k]=3


"** Create parameters *************************************"
" Each parameter is checked for existence.  If it already  "
" exists, the limits and protection are reset.             "
"**********************************************************"
$k = 1
$klim = size('$set')
repeat

    $j = 1
    $jlim = size('$'+$set[$k])
    repeat

        $name = ${$set[$k]}[$j]
        exists($name,'parameter'):$ex

        if ($ex < 0.5) then
            create($name,$type[$k])
        endif

        setlimit($name,$max[$k],$min[$k],$step[$k])
        setgroup($name,$group[$k])
        setprotect($name,'set',$protect[$k])
        $j = $j + 1

    until ($j > $jlim)
    $k = $k + 1

until ($k > $klim)


"** Set enumeral values for required parameters ***********"
setenumeral('orient',11,'trans','sag','cor','oblique','3orthogonal','xyz','xzy','yxz','yzx','zxy','zyx')
setenumeral('dorient',5,'trans','sag','cor','oblique','orient')
setenumeral('presig',3,'h','l','n')


"** Set limits and protection for some special cases ******"
setprotect('gcoil','set',9)
setprotect('seqcon','set',9)
setprotect('nD','set',9)
setprotect('slice','set',9)
setprotect('echo','set',9)
setprotect('orient','set',8)
setprotect('gradunit','set',8)
setprotect('fovunit','set',8)
setprotect('thkunit','set',8)
setprotect('pss','set',264)
setprotect('ssc','set',1)
setprotect('zero','set',39)
setgroup('t_array','processing')
setlimit('gpemult',1.0,0,0)
setlimit('grayctr',64,0,1)
setlimit('graysl',100,-100,.01)


"** Compute value of B0 ***********************************"
$protongamma = 234.94   "Gauss/MHz"
B0 = $protongamma*h1freq


"** Set values for some special cases *********************"
gcoil = sysgcoil
if (seqcon='')  then  seqcon='ncsnn'  else  seqcon=seqcon  endif
"if (gradunit = '')  then  gradunit = 'g'  endif"
"if (fovunit = '')  then  fovunit = 'c'  endif"
"if (thkunit = '')  then  thkunit = 'm'  endif"
pilot = 'y'
rfspoil= 'n'
verbose= 'n'
if (t_ns < 1)  then  t_ns = 1  endif
dconi = 'dcon,gray,linear'
slice = 1
echo = 1
grayctr = 32
graysl = 1


"** Setup for normal imaging mode *************************"
solvent = 'none'
alock = 'u'
wshim = 'n'
homo = 'n'
gain = 20
load = 'n'
spin = 0
spin = 'n'
ai
av
av1
axis = 'cc'
cf = 1
pad = 0
np = 512
nt = 1
dp = 'y'
pw = 0
presig = 'l'
dcrmv='y'


"** Define dg strings *************************************"

$k=0  dgdelay = ''
$k=$k+1 dgdelay[$k] = '1(zero):label Delays :zero;'
$k=$k+1 dgdelay[$k] = '1:SEQUENCE:at:3,tr:3,te:3,ti:3,tm:3,;'
$k=$k+1 dgdelay[$k] = '1:PHASE ENCODING:tpe:3,tpe2:3,tpe3:3;'
$k=$k+1 dgdelay[$k] = '2:GENERAL:d1:3,d2:3,d3:3,d4:3,d5:3,,tau:3,tau1:3,tau2:3;'
$k=$k+1 dgdelay[$k] = '3:CARDIAC GATING:ticks:0,hold:3,;'
$k=$k+1 dgdelay[$k] = '3:SPECIAL:trelax:3,tdelta:3,tDELTA:3,tflow:3,tspoil:3;'
setprotect('dgdelay','set',6)

$k=0  dggrad = ''
$k=$k+1 dggrad[$k] = '1(zero):label Gradients :zero;'
$k=$k+1 dggrad[$k] = '1:COIL CALIBRATION:gcoil,gmax:3,trise:5,gradunit,boresize:1,B0:0,;'
$k=$k+1 dggrad[$k] = '1:VOXEL SELECTION:gvox1:3,gvox2:3,gvox3:3;'
$k=$k+1 dggrad[$k] = '2:READ OUT:gro:3,gror:3,;'
$k=$k+1 dggrad[$k] = '2:PHASE ENCODE:gpe:3,gpe2:3,gpe3:3,gpemult:3,;'
$k=$k+1 dggrad[$k] = '2:SLICE SELECT:gss:3,gss2:3,gss3:3,gssr:3;'
$k=$k+1 dggrad[$k] = '3:GENERAL:g0:3,g1:3,g2:3,g3:3,g4:3,g5:3,,gx:3,gy:3,gz:3;'
$k=$k+1 dggrad[$k] = '4:SPECIAL:gdiff:3,gflow:3,gspoil:3,gcrush:3,gtrim:3;'
$k=$k+1 dggrad[$k] = '4:PATTERNS:gropat,gpepat,gsspat;'
setprotect('dggrad','set',6)

$k=0  dg1 = ''
$k=$k+1 dg1[$k] = '1:DISPLAY:sp:1,wp:1,sp1:1,wp1:1,vs:0,th:0;'
$k=$k+1 dg1[$k] = '1:REFERENCE:rfl:1,rfp:1,rfl1:1,rfp1:1;'
$k=$k+1 dg1[$k] = '1:PHASE:rp:1,lp:1,rp1:1,lp1:1;'
$k=$k+1 dg1[$k] = '2:PLOT SIZE:sc:1,wc:1,sc2:1,wc2:1;'
$k=$k+1 dg1[$k] = '2:PLOT OFFSETS:vp:1,vo:2,ho:2;'
$k=$k+1 dg1[$k] = '2:AXIS DISPLAY:axis,trace;'
$k=$k+1 dg1[$k] = '2:CURSORS:cr:1,delta:1,cr1:1,delta1:1;'
$k=$k+1 dg1[$k] = '3:INTEGRAL:intmod,is:0,ins:3,io:0,,lvl:3,tlt:3;'
$k=$k+1 dg1[$k] = '3:GRAY SCALE:graysl:3,grayctr:0;'
$k=$k+1 dg1[$k] = '3:FLAGS:aig*,dcg*,dmg*;'
$k=$k+1 dg1[$k] = '4:FID DISPLAY:sf:3,wf:3,vf:0,vpf:0,vpfi:0,lsfid:0,phfid:1,dotflag;'
$k=$k+1 dg1[$k] = '4:FID CURSORS:crf:3,deltaf:3;'
setprotect('dg1','set',6)

$k=0  dgfov = ''
$k=$k+1 dgfov[$k] = '1(zero):label FOV :zero;'
$k=$k+1 dgfov[$k] = '1:FIELD OF VIEW:lro:3,lpe:3,lpe2:3,lpe3:3,fovunit,;'
$k=$k+1 dgfov[$k] = '1:FOV POSITION:pro:3,ppe:3,pss:3,;'
$k=$k+1 dgfov[$k] = '1:FOV BANDWIDTHS:sw:1,sw1:1,sw2:1,sw3:1;'
$k=$k+1 dgfov[$k] = '2:FOV ORIENTATION:orient,psi:1,phi:1,theta:1,;'
$k=$k+1 dgfov[$k] = '2:VOXEL ORIENTATION:vorient,vpsi:1,vphi:1,vtheta:1;'
$k=$k+1 dgfov[$k] = '3:VOXEL POSITION:pos1:3,pos2:3,pos3:3,;'
$k=$k+1 dgfov[$k] = '3:VOXEL SIZE:vox1:3,vox2:3,vox3:3,thkunit,;'
$k=$k+1 dgfov[$k] = '4:ACQ CONTROL:seqcon,nD:0,ni:0,nf:0,cf:0,,ne:0,ns:0,,np:0,nv:0,nv2:0,nv3:0;'
setprotect('dgfov','set',6)

$k=0  dgpulse = ''
$k=$k+1 dgpulse[$k] = '1(zero):label Pulses :zero;'
$k=$k+1 dgpulse[$k] = '1:OBS PULSE LENGTHS:p1:1,p2:1,p3:1,p4:1,p5:1,pw:1,pi:1,psat:1;'
$k=$k+1 dgpulse[$k] = '1:OBS PULSE PATTERNS:p1pat,p2pat,p3pat,p4pat,p5pat,pwpat,pipat,satpat;'
$k=$k+1 dgpulse[$k] = '2:OBS PULSE POWERS:tpwr1:0,tpwr2:0,tpwr3:0,tpwr4:0,tpwr5:0,tpwr,tpwri,satpwr:0;'
$k=$k+1 dgpulse[$k] = '2:OBS FREQUENCIES:tn,sfrq:3,tof:1,resto:1,wsfrq:1,satfrq:1;'
$k=$k+1 dgpulse[$k] = '2:OBS CALIBRATION:rfcoil;'
$k=$k+1 dgpulse[$k] = '3:DEC FREQUENCIES:dn,dfrq:3,dof:1;'
$k=$k+1 dgpulse[$k] = '3:DEC CONTROL:dm,dmm,dmf;'
$k=$k+1 dgpulse[$k] = '3:DEC POWERS:dpwr1:0,dpwr2:0,dpwr3:0,dpwr4:0,dpwr5:0,dpwr:0;'
$k=$k+1 dgpulse[$k] = '4:DEC PULSE PATTERNS:decpat,decpat1,decpat2,decpat3,decpat4,decpat5;'
setprotect('dgpulse','set',6)

$k=0  dgproc = ''
$k=$k+1 dgproc[$k] = '1(zero):label Processing :zero;'
$k=$k+1 dgproc[$k] = '1:1D FFT:np:0,fn:0,;'
$k=$k+1 dgproc[$k] = '1:2D FFT:nv:0,ni:0,nf:0,fn1:0,;'
$k=$k+1 dgproc[$k] = '1:CURRENT IMAGE:slice:0,echo:0,element:0;'
$k=$k+1 dgproc[$k] = '2:1D APODIZATION:lb:3,gf:3,gfs:3,sb:3,sbs:3,awc:3;'
$k=$k+1 dgproc[$k] = '3:2D APODIZATION:lb1:3,gf1:3,gfs1:3,sb1:3,sbs1:3,awc2:3;'
$k=$k+1 dgproc[$k] = '4:ACQ PROCESSING:werr,wexp,wnt,wbs;'
setprotect('dgproc','set',6)

$k=0  dgslice = ''
$k=$k+1 dgslice[$k] = '1(zero):label Slice&Voxel :zero;'
$k=$k+1 dgslice[$k] = '1:SLICE PARAMETERS:ns:0,ne:0,thk:3,pro:3,ppe:3;'
$k=$k+1 dgslice[$k] = '2:SLICE POSITION:[pss];'
$k=$k+1 dgslice[$k] = '3:VOXEL SIZE:[vox1],,[vox2],,[vox3];'
$k=$k+1 dgslice[$k] = '4:VOXEL POSITION:[pos1],,[pos2],,[pos3];'
setprotect('dgslice','set',6)

$k=0  dgseq = ''
$k=$k+1 dgseq[$k] = '1(zero):label Sequence :zero;'
$k=$k+1 dgseq[$k] = '1:SEQUENCE NAME:seqfil,pslabel,;'
$k=$k+1 dgseq[$k] = '1:SEQUENCE TYPE:nD:0,exptype,;'
$k=$k+1 dgseq[$k] = '1:LOOP CONTROL:seqcon,acqtype;'
$k=$k+1 dgseq[$k] = '2:ACTIVE PULSES:[plist],;'
$k=$k+1 dgseq[$k] = '2:ACTIVE PATTERNS:[patlist];'
$k=$k+1 dgseq[$k] = '3:ACTIVE POWERS:[pwrlist],;'
$k=$k+1 dgseq[$k] = '3:FLIP ANGLES:[fliplist];'
$k=$k+1 dgseq[$k] = '4:CONJUGATE GRAD:[sslist];'
setprotect('dgseq','set',6)

$k=0  dgtarg = ''
$k=$k+1 dgtarg[$k] = '1(zero):label Target :zero;'
$k=$k+1 dgtarg[$k] = '1:VOXEL POSITION:t_pos1:3,t_pos2:3,t_pos3:3,;'
$k=$k+1 dgtarg[$k] = '1:VOXEL SIZE:t_vox1:3,t_vox2:3,t_vox3:3,;'
$k=$k+1 dgtarg[$k] = '1:VOXEL ORIENT:t_vpsi:1,t_vphi:1,t_vtheta:1,;'
$k=$k+1 dgtarg[$k] = '1:ARRAY STRING:t_array;'
$k=$k+1 dgtarg[$k] = '2:FIELD OF VIEW:t_lro:3,t_lpe:3,t_lpe2:3,t_lpe3:3,;'
$k=$k+1 dgtarg[$k] = '2:FOV POSITION:t_pro:3,t_ppe:3,t_ppe2:3,t_ppe3:3,;'
$k=$k+1 dgtarg[$k] = '2:ORIENTATION:t_psi:1,t_phi:1,t_theta:1;'
$k=$k+1 dgtarg[$k] = '3:SLICE POSITION:t_ns:0,t_thk:1,t_gap:1,,[t_pss];'
setprotect('dgtarg','set',6)


"** Set dg and other imaging parameters if called with ****"
"** a sequence name argument ******************************"
if ($# = 1) then
  pardg($1)
  initparimage($1)
endif


"** End of parameter creation *****************************"
write('line3','Parameter creation and update complete')
