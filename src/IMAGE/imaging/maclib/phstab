"macro phstab"
"***************************************************************"
" phstab                                                        "
"                                                               "
" Determines phase stability by measuring the variation of the  "
" peak height in an array of dispersion mode spectra            "
" An optional argument may be used to discard the first skipval "
" spectra (useful when full relaxation equilibrium has not been "
" reached in the first spectrum).                               "
"                                                               "
" Usage:  phstab(<skipval>)                                     "
" ARR 6/1/09                                                    "
"***************************************************************"

"get ratio of peak height to phase change"
peak:$ht0
rp = rp + 5
peak:$ht5
rp = rp - 5
$deght = 5/($ht0 - $ht5)
write('line3','degrees/height = %5.2f',$deght)

$sum = 0
$sumsquared = 0
$min = 1e6
$max = -1e6

if ($# = 1) then
  $skip = $1
else
  $skip = 0
endif
$nvals = arraydim - $skip

$i = $skip
repeat

  $i = $i + 1
  select($i)
  peak:$height
  $sum = $sum + $height
  $sumsquared = $sumsquared + $height*$height

  if ($height < $min)  then  $min = $height  endif
  if ($height > $max)  then  $max = $height  endif

until ($i = arraydim)

$mean = $sum/$nvals
$range = $max - $min
$std_dev = sqrt($sumsquared/$nvals - $sum*$sum/($nvals*$nvals))

write('line3','Mean: %10.2f    Range: %10.2f%s   Std Deviation: %10.2f%s',
  $mean, $range/$mean*100, '%', $std_dev/$mean*100, '%')

$phrange = $range*$deght
$phrmsd = $std_dev*$deght

write('line3','Phase range: %5.2f deg     Phase RMSD: %4.2f deg',$phrange,$phrmsd)

