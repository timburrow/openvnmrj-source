"macro ATc30_10usec"
"ATc30_10usec" "Find C13 pw90 ,power level, and BB amplifier compression"
"        pwxlvl is found for 10 usec pw90 for power <= at_max_pwxlvl"

"new globals: at_max_pwxlvl   Maximum pulse power"
"             at_max_dpwr  Maximum decoupling power"

"new versions of existing parameters: results of this test are stored in :  "              
"             at_pwx90_10usec_c        High power 13C pwx90"
"             at_pwx90lvl_10usec_c     High power 13C level"
"             at_pwx90Lowpower_10usec_c  Low power 13C pwx90"
"             at_pwx90Lowpowerlvl_10usec_c Lower power level
"             at_LBampl_compr_10usec_c   Low Band Amplifier Compression"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if ($#=0) then

   ATrtp('standard')  
   at=1 fn=4*np lb='n' 
   tpwr=at_tpwr
   pw=1
   wnt=''
   wexp='ATc30_10usec(`PART1`)'
   au

elseif ($1='PART1') then

   ft f full ph aph0 vsadj peak:$ht,cr 
   if ($ht<0) then rp=rp+180 endif
   movetof 
   at_tof=trunc(10*tof)/10
   rtp(autotestdir+'/parameters/standard') "resets curpar to standard values"
   wdone=''                            "do not want wdone in autotest"
   tof=at_tof                          "makes sure tof is on water"
   fsave(autotestdir+'/parameters/standard.par/procpar') "updates standard.par"

   ATrtp('standard')  
   at_currenttest='c13pw90c_10usec_1'
   dn='C13' 
   tpwr=at_tpwr pw=at_pw90 pw90=pw
   seqfil='ATrfhomo' 
   text('Finding Null for 10 usec 13C Pulse (vary 13C Pulse Power)')

   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   write('line3','Finding Power Level(below Max.Power) for pw(C13)=10 usec')
   pwx=10 array('pwxlvl',21,at_max_pwxlvl-20,1)
   lb='n' sb=at/4 s1 av
   r1=1
      
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'
   wexp='ATc30_10usec(`PART2`)'
   text 
   ATcycle au
 
elseif ($1='PART2') then
   ATsvf wft
   $pwxlvlmax=pwxlvl[21]
   write('line3','Finding C13 pwx90 (Channel 2)')

   ATpwx90cal('pwxlvl'):$pwxlvl
   pwxlvl=$pwxlvl
   if ((pwxlvl=$pwxlvlmax) and (r1=1))then
    pwx=pwx*1.12            " increase pwx once (typ. for 8mm/10mm probes) "
    array('pwxlvl',21,at_max_pwxlvl-20,1)
    at_currenttest='c13pw90c_10usec_1a'
    r1=4
    ATcycle au return
   endif
   at_currenttest='c13pw90c_10usec_2'
   at_pwx90lvl_10usec_c=pwxlvl

   text('Finding 90 Degree 13C Pulse Width')
   array('pwx',31,0.5*pwx,0.03*pwx)
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'

   wexp='ATc30_10usec(`PART3`)'
   ATcycle au
 
elseif ($1='PART3') then
   ATsvf wft
   at_currenttest='c13pw90c_10usec_3'
   write('line3','Determining C13 pwx90 at Reduced Power (Channel 2)')
   av
   ATpwx90cal('pwx'):pwx r4=pwx
   text('Finding 90 Degree 13C Pulse Width at lower power')

   at_pwx90_10usec_c=trunc(10*pwx)/10
   pwx=pwx*4.0 pwxlvl=pwxlvl-12
   array('pwx',31,0.5*pwx,0.03*pwx)
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'
   wexp='ATc30_10usec(`PART4`)'
   ATcycle au
 
elseif ($1='PART4') then
   ATsvf wft
   at_currenttest='c13pw90c_10usec_4'

   ATpwx90cal('pwx'):pwx 
   array('pwx',21,pwx-0.1*pwx,0.01*pwx)
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'
   wexp='ATc30_10usec(`PART5`)'
   ATcycle au

elseif ($1='PART5') then
   av wft
   ATpwx90cal('pwx'):$pwx 
   pwx=$pwx

   at_pwx90Lowpower_10usec_c=trunc(10*pwx)/10
   at_pwx90Lowpowerlvl_10usec_c=pwxlvl
   at_LBampl_compr_10usec_c=trunc(100*pwx/(4.0*at_pwx90_10usec_c))/100 

   write('line3','C13 pw90 (Channel 2) =  %3.2f at power level  %2.0f',pwx,pwxlvl)

   if (at_plotauto='y') then
      printon
         echo('Pulse Calibration Data for 13C on Channel 2')
         echo('For the probe   ',at_probetype)
         echo('Based on Finding a 90 Degree Pulse  =  ',pwx)
         echo('At Power Level',pwxlvl) 
         echo('Amplifier Compression =',at_LBampl_compr_10usec_c)
         echo('At Power Level',pwxlvl+12)
         echo('Done on    ',date)
         ATatten(pwx,pwxlvl)
      printoff
   else
      ATatten(pwx,pwxlvl)
   endif
   ATrecord('LOAMPCOMPc_10usec','Low-Band Amplifier Compression','compression',at_LBampl_compr_10usec_c,'at_power',pwxlvl+12)
   write('file',autotestdir+'/REPORT','Low-Band Amplifier Compression = %1.2f at power level: %2.0f ',at_LBampl_compr_10usec_c,pwxlvl+12)
   ATsvf
   ATrecord('C13PW90c_10usec','C13 Pulse Width Calibration and Amplifier Compression for 10usec C13 pulse','pw90_hi',at_pwx90_10usec_c,'power1',at_pwx90lvl_10usec_c,'pw90_low',pwx,'power2  ',pwxlvl,'comp_power1',at_LBampl_compr_10usec_c)
   write('file',autotestdir+'/REPORT','C13 pw90 = %2.1f  at power level: %2.0f ',r4,pwxlvl+12)

ATnext
endif
