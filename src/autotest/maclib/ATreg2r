"macro ATreg2r"
" ATreg2r - attenuator regression analysis"
" takes log of nmr response (dB) and correlates with power level in pwpwr"
 "usage attreg(start,end)  -  default values: 1, arraydim"
if $#>1 then $end=$2 else $end=arraydim endif
if $#>0 then $i=$1 else $i=1 endif
$filename=curexp+'/regression.inp'
write('reset',$filename)
write('file',$filename,'attenuator value')
write('file',$filename,'nmr response in dB')
write('file',$filename,'1 %4.0f',$end-$i+1)
repeat
select($i) dc
peak:$hgt
if $hgt>0 then ln(100*$hgt):$lnhgt else ln(-100*$hgt):$lnhgt endif
$attv=tpwr[$i]
$resp=20*$lnhgt/2.303
write('file',$filename,'%10s  %5.4f',$attv,$resp)
$i=$i+1
until $i>$end
full
exists(curexp+'/analyze.out','file'):$e
if ($e) then
  delete(curexp+'/analyze.out')
endif
expl('regression') analyze('expfit','regression','poly1','list')
exists(curexp+'/analyze.out','file'):$e
if ($e) then
  expl
  lookup('file',curexp+'/analyze.list')
  lookup('skip',23,'read',1):$corrcoef
  lookup('skip',3,'read',1):$corrcoefdev
  $corrcoef=trunc(1000*$corrcoef)/1000
  $corrcoefdev=trunc(1000*$corrcoefdev)/1000
else
  write('error','No analysis done for attn test. Check your data.')
  $corrcoef=0
  $corrcoefdev=-1
endif
sc=100 wc=100
 
return($corrcoef,$corrcoefdev)
