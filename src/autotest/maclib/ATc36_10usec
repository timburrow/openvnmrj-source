"macro ATc36_10usec"
"ATc36_10usec" "Test C13 Amplifier Linearity"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if ($#=0) then
   ATrtp('standard')  
    exists(curexp+'/expl.out','file'):$e
    if $e then
     rm(curexp+'/expl.out') endif
   at_currenttest='c13pw90_vs_pwxlvl_11c'
   dn='C13' 
   tpwr=at_tpwr pw90=at_pw90 pw(90)
   seqfil='ATrfhomo' 
   pwxlvl=at_pwx90lvl_10usec_c j=142  "this power level ok (was set rel. to at_max_pwxlvl)"

    n1=curexp+'/regression.inp.pwx90'
    write('reset',n1)
    write('file',n1,'pwxlvl')
    write('file',n1,'pwx90 (usec)')
    write('file',n1,'1 %4.0f',15)

    autodir=curexp+'/regression.inp.B1'   "borrow global parameter"
    write('reset',autodir)
    write('file',autodir,'pwxlvl')
    write('file',autodir,'B1 (Hz)')
    write('file',autodir,'1 %4.0f',15)

    spinsys=curexp+'/regression.inp.logB1'  "borrow global parameter"
    write('reset',spinsys)
    write('file',spinsys,'pwxlvl')
    write('file',spinsys,'logB1')
    write('file',spinsys,'1 %4.0f',15)

   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   lb='n' sb=at/4 s1 av
   wnt=''
   wexp='ATc36_10usec(`PART1`)'
   ATcycle au
 
elseif ($1='PART1') then

   wft  
   sp=3p wp=0.2p vsadj
   th=100
   axis='h'
   s1
   axis='h'
   $pwx360=at_pwx90_10usec_c*4
   r3=at_pwx90_10usec_c/5
   pwx=0
   pwx[2]=$pwx360-r3*4,$pwx360-r3*3.6,$pwx360-r3*3.2,$pwx360-r3*2.8,$pwx360-r3*2.4,$pwx360-r3*2,$pwx360-r3*1.6,$pwx360-r3*1.2,$pwx360-r3*.8,$pwx360-r3*.4,$pwx360,$pwx360+r3*.4,$pwx360+r3*.8,$pwx360+r3*1.2,$pwx360+r3*1.6,$pwx360+r3*2,$pwx360+r3*2.4,$pwx360+r3*2.8,$pwx360+r3*3.2,$pwx360+r3*3.6,$pwx360+r3*4
   text('C13 RF homogeneity: 0 degree, 360 array')
   wnt='ATwft  s1 select(1) vsadj dssh '
   r1=15
   wexp='ATc36_10usec(`MORE`)'
   ATcycle au

elseif ($1='MORE') then

   wft
   ATxrfhomocal2c:$pwx90,$pwx360,$ratio1
   $pwx360=trunc($pwx360*10)/10
   $B1=1e6/$pwx360
   ln($B1):$lnB1 $lnB1=20*$lnB1/2.303

   write('file',n1,'%2.0f %4.1f',pwxlvl,$pwx360/4)
   write('file',spinsys,'%2.0f %6.2f',pwxlvl,$lnB1)
   write('file',autodir,'%2.0f %4.0f',pwxlvl,$B1)

   write('line3','C13 pw90 = %2.1f (B1= %6.0f Hz) at power level =%2.0f ',$pwx360/4,$B1,pwxlvl)
   av

   $pwx360=$pwx360*1.25 pwxlvl=pwxlvl-2
   pwx=0 r3=r3*1.25
   pwx[2]=$pwx360-r3*4,$pwx360-r3*3.6,$pwx360-r3*3.2,$pwx360-r3*2.8,$pwx360-r3*2.4,$pwx360-r3*2,$pwx360-r3*1.6,$pwx360-r3*1.2,$pwx360-r3*.8,$pwx360-r3*.4,$pwx360,$pwx360+r3*.4,$pwx360+r3*.8,$pwx360+r3*1.2,$pwx360+r3*1.6,$pwx360+r3*2,$pwx360+r3*2.4,$pwx360+r3*2.8,$pwx360+r3*3.2,$pwx360+r3*3.6,$pwx360+r3*4
   write('graphics',0,175,'C13 RF homogeneity: 0 degree, 360 array at power level %2.0f',pwxlvl)

   wnt='ATwft select(1) vsadj dssh '

   r1=r1-1
   if r1=1 then
    wexp='ATc36_10usec(`LAST`)'
   else
    wexp='ATc36_10usec(`MORE`)'
   endif
   ATcycle au

elseif ($1='LAST') then

   wft
   ATxrfhomocal2c:$pwx90,$pwx360,$ratio1
   $pwx360=trunc($pwx360*10)/10
   $B1=1e6/$pwx360
   ln($B1):$lnB1 $lnB1=20*$lnB1/2.303

   write('file',n1,'%2.0f %4.1f',pwxlvl,$pwx360/4)
   write('file',spinsys,'%2.0f %6.2f',pwxlvl,$lnB1)
   write('file',autodir,'%2.0f %4.0f',pwxlvl,$B1)

   write('line3','C13 pw90 = %2.1f (B1= %6.0f Hz) at power level =%2.0f ',$pwx360/4,$B1,pwxlvl)

   if (at_plotauto='y') then
    pap
    ATpltext(0,80)
    cp(curexp+'/regression.inp.pwx90',curexp+'/regression.inp')
    right
    exists(curexp+'/expl.out','file'):$e
    if $e then
     rm(curexp+'/expl.out') endif
    sc=0 wc=50
    expl('regression') 
    right
    pexpl('regression')
    ATpage
    pap
    ATpltext(0,80)
    cp(curexp+'/regression.inp.B1',curexp+'/regression.inp')
    rm(curexp+'/expl.out')
    pexpl('regression')
    sc=80 wc=50
    expl('regression','noclear') 
    ATpage
    pap
    ATpltext(0,80)
    cp(curexp+'/regression.inp.logB1',curexp+'/regression.inp')
    sc=160 wc=50
    rm(curexp+'/expl.out') 
    expl('regression','noclear') 
    analyze('expfit','regression','poly1','list') expl('noclear')
    right
    pexpl
    ATpage
   else
    cp(curexp+'/regression.inp.pwx90',curexp+'/regression.inp')
    right sc=0 wc=50
    rm(curexp+'/expl.out')
    expl('regression') 
    cp(curexp+'/regression.inp.B1',curexp+'/regression.inp')
    sc=80 wc=50
    rm(curexp+'/expl.out') 
    expl('regression','noclear') 
    cp(curexp+'/regression.inp.logB1',curexp+'/regression.inp')
    sc=160 wc=50
    rm(curexp+'/expl.out') 
    expl('regression','noclear')
    analyze('expfit','regression','poly1','list') expl('noclear')
   endif
  
   lookup('file',curexp+'/analyze.list')
   lookup('skip',22,'read',1):$corrcoef
   lookup('skip',3,'read',1):$corrcoefdev
   $corrcoef=trunc(1000*$corrcoef)/1000
   if $corrcoef>1 then $corrcoef=1 endif
   $corrcoefdev=trunc(1000*$corrcoefdev)/1000

   write('line3','C13 B1 Regression: Correlation Coefficient = %2.3f ; Standard Deviation = %2.3f',$corrcoef,$corrcoefdev)
   ATrecord('C13AMPc_10usec','B1 and pwx90 as function of power','Corr.Coeff',$corrcoef,'  Std.Dev',$corrcoefdev,'  Max_Power',at_pwx90lvl_10usec_c)

   write('file',autotestdir+'/REPORT','C13 Amp.Linearity: Corr.Coeff= %2.3f Std. Dev.= %2.3f ',$corrcoef,$corrcoefdev)

    cp(curexp+'/regression.inp.pwx90',userdir+'/autotest/data/c13pw90_vs_power_regression.inp')
    cp(curexp+'/regression.inp.B1',userdir+'/autotest/data/c13B1_vs_power_regression.inp')
    cp(curexp+'/regression.inp.logB1',userdir+'/autotest/data/c13logB1_vs_power_regression.inp')
spinsys='' autodir=''
ATnext
endif
