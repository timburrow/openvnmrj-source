"macro ATc33_cryo1D"
"        Using user-defined power limits. Efficiency may be"
"        compromised"
"        Tests intensity of first modulation sideband of H2O peak"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

   substr(rfwg,1,1):$wfg1exists
   substr(rfwg,2,1):$wfg2exists

   if (rfchtype[2]='U+ Direct Synthesis') then    "Dual broadband system"
    if ($wfg2exists='n') then
     write('line3','No Waveform Generator Present. WFG Required for Adiabatic Decoupling')
     ATnext return 
    endif
   else
    if ($wfg1exists='n') then    "C13 on channel 1 uses wfg on channel 1"
     write('line3','No Waveform Generator Present. WFG Required for Adiabatic Decoupling')
     ATnext return 
    endif
   endif

if ($#=0) then
   ATrtp('standard')  
   seqfil='s2pul'
   dn='C13' at=.1 d1=1 ss=2 
   tpwr=at_tpwr pw90=at_pw90 pw(90)
   at_currenttest='wurst_sideband_test'
   lb='n' sb=at/4 s1 av

   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   wnt=''
   wexp='ATc33_cryo1D(`PART1`)'
   au
 
elseif ($1='PART1') then

   wft f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj
   lb='n' sb=at sbs=-sb
   opx('ATwurst.DEC')
   setwave('WURST2 14000')
   pboxpar('$1 adb', '2.0')   "add 2dB for better intensities"
   cpx(at_pwx90Lowpowerc,at_pwx90Lowpowerlvlc)

   pbox_pwr('ATwurst.DEC'):dpwr

   $n3=n3
   format(at_max_dpwr,2,0):n3
   n3=n3+'d'
   $attn=n3

   if dpwr>at_max_dpwr then
       pboxpar('attn',$attn)
       echo($attn)
       shell('Pbox')
       pbox_pwr('ATwurst.DEC'):dpwr
       write('line3','Decoupling Power reduced to Maximum (at_max_dpwr)')
       n3='Decoupling Power reduced to Maximum (at_max_dpwr)'
       atext(n3)
   endif

   pbox_dmf('ATwurst.DEC'):dmf
   wexp='ATc33_cryo1D(`PART2`)'
   text('Decoupling H2O Sideband Test: Upper- wurst decoupled    Lower-no decoupling')
   ph dm='nnn','nny' dmm='ccc','ccp'
   array='(dm,dmm)' dres=9.0 
   dseq='ATwurst'
   au 
elseif ($1='PART2') then

   wft
   ATsvf select(1)
   f aph0 vsadj(10000) wp=1500 sp=-300 dc 
   select(2) dc
   peak(300,1500):$int,$freq
   if $int<0 then $int=$int endif
   $int=trunc($int*100)/100
   $freq=trunc($freq*10)/10
   $percent=trunc($int/10)/10
   full vp=0 ho=0 vo=30 dss('dodc') dscale
   if (at_plotauto='y') then
      write('plotter',0,50,'WURST sideband percent = %2.1f at power= %2.0f and dmf=%5.0f',$percent,dpwr,dmf)
      write('plotter',0,45,'(measured by using most intense peak in 1200Hz range downfield from the water signal)')
      pl('all','dodc') pscale pap ATpage 
   endif
   ATrecord('WURST1Dc','First Sideband Test (Water peak is 10000)','   %',$percent,' dpwr',dpwr,' dmf',dmf,' Freq.',$freq,' Int.',$int)
   write('file',autotestdir+'/REPORT','WURST sideband percent = %2.1f at power= %2.0f and dmf=%5.0f',$percent,dpwr,dmf)

ATnext
endif
