"macro AT34a"
"AT34a" "C13 Decoupling Heating Test Using Channel 3 "  

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if ($#=0) then
   ATrtp('standard')
   text('C13 Decoupling Heating Test Using Channel 3')
   at_currenttest='Decoupler_heating_ch3' 
   create('rfchannel','string')
   dn='C13'
   if (numrfch>4) then rfchannel='13245' endif
   if (numrfch=4) then rfchannel='1324' endif
   if (numrfch=3) then rfchannel='132' endif
   if (numrfch<3) then ATnext return endif   "no 3rd channel to test"
   nt=1 f full 
   wnt=''
   wexp='AT34a(`PART1`)'
   ATcycle
   au

elseif ($1='PART1') then


 wft av f  
 lb='n' sb=.205 at=.8 
 sp=2.5p wp=0.8p vsadj th=40 nll
 getll(1):$int,cr  sp=cr-0.02p wp=.1p
 d1=1.5-at-0.07 ss=1 wp=wp/2
 vp=0 wc=80 sc=0 fn=2*np axis='h' 
 create('scalesw','real') scalesw=100/(1.11696*sfrq)
 vo=1 th=10

 at=.82 d2=.075 d1=.605 sb=.205 fn='n'
 lb='n' 
 ss=2
 dn='C13' 



   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

 $pwx=1e6/(4.0*7000)  "7 kHz Field is appropriate at 600MHz"
 ln($pwx/at_pwx90Lowpower_ch3):$ln
 dpwr=at_pwx90Lowpowerlvl_ch3-20.0*$ln*0.43429
 dpwr=trunc(dpwr)
 dmf=1e6/$pwx

 dmm='ccc' 
 if ((sfrq>198) and (sfrq< 298)) then   " reduced power for lower fields"
  dpwr=dpwr-10 dmf=dmf/3
 endif
 if ((sfrq>298) and (sfrq< 398)) then
  dpwr=dpwr-6 dmf=dmf/2
 endif
 if ((sfrq>398) and (sfrq< 498)) then
  dpwr=dpwr-4 dmf=dmf/(3*1.12)
 endif
 if ((sfrq>498) and (sfrq< 598)) then
  dpwr=dpwr-2 dmf=dmf/(1.12*1.12)
 endif
 
 r1=1 repeat
  dm[r1]='nyn'
  r1=r1+1
 until r1>100
 r1=101 repeat
  dm[r1]='nnn' r1=r1+1
 until r1>200
 text('Test of 13C Decoupler Heating Using an RF Field sufficient to decouple 13C \\over a 160 ppm. One spectrum every 1.5 seconds with 75 msec\\13C decoupling for first 100 spectra and no decoupling in last 100 spectra\\Scale in Degrees Centigrade\\Using Channel 3 for 13C RF')
 
 
 wnt='ATdec_heat'
 wexp='AT34a(`PART2`)' 
 ATcycle dtext au

elseif ($1='PART2') then
  wft

 select(arraydim/2) peak:$ht,r3
 if r3<0 then re=0 endif
 r3=trunc(100*r3*scalesw)/100
 ATrecord('DEC_HEATING_ch3','C13 Decoupler Heating Test (Channel 3)','Temp_Rise',r3)
 write('file',autotestdir+'/REPORT','Temperature Increase in Channel 3 Decoupler Heating Test= %1.1f ',r3)

 if (at_plotauto='y') then
  if (at_printparams='y') then
   dn2='' pap(0,wc2max-40) ATpltext 
   pps(180,0,wcmax-180,90)
   ATpage 
  endif
  pltext(0,wc2max-5)
  ATpltext
  r1 sc=sc+30 plww(1,100) pscale
  write('plotter',wcmax-sc,vp,'<--Time =  0.0 sec')
  write('plotter',wcmax-sc,vp+99,'<--Time =  150 sec')
  write('plotter',wcmax-sc-wc,wc2max-55,'Heating:  13C Decoupling')
  write('plotter',wcmax-sc-wc,wc2max-60,'First 100 Spectra')
  write('plotter',wcmax-sc-wc/2,wc2max-63,'Maximum Heating From 13C Decoupling = %2.1f Degrees',r3)
  r2  sc=sc+60 plww(101,200) pscale
  write('plotter',wcmax-sc,vp,'<--Time =  151.5 sec')
  write('plotter',wcmax-sc,vp+99,'<--Time =  300 sec')
  write('plotter',wcmax-sc-wc,wc2max-55,'Cooling: No 13C Decoupling')
  write('plotter',wcmax-sc-wc,wc2max-60,'Last 100 Spectra')
  ATpage 
 endif

 ATsvf

ATnext
endif
