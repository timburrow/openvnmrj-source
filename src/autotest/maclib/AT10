"macro AT10"
"AT10"   " Find 1H pw360"

if ($#=0) then
   ATrtp('standard')
   text('Finding 1H pw90')
   gain=10    av
   at_currenttest='pw90_ch1_1'
   array('pw',30,at_pw90/7,at_pw90/10)
   vs=vs/2
   wnt='ATwft select(1) vsadj(10) dssh' 
   write('line3','Determining estimate of pw90')
   vp=0 
   wexp='AT10(`PART1`)'
   ATcycle
   au

elseif ($1='PART1') then
   wft
   ATsvf
   ATpw90cal2('pw'):$bb

   write('line3','finding first estimate of pw360')
   pw=3.0*$bb,3.2*$bb,3.4*$bb,3.6*$bb,3.8*$bb,4.0*$bb,4.2*$bb,4.4*$bb,4.6*$bb,4.8*$bb
   wnt='ATwft dssh'
   at_currenttest='pw90_ch1_2'
   wexp='AT10(`PART2`)'
   ATcycle
   au

elseif ($1='PART2') then
   wft
   write('line3','calculating first estimate of pw360')
   ATsvf
 
   ATpw90cal('pw'):pw90

   write('line3','finding second estimate of pw360 ')
   wnt='ATwft dssh'
   array('pw',10,3.6*pw90,0.08*pw90)
   at_currenttest='pw90_ch1_3'
   wexp='AT10(`PART3`)'
   ATcycle
   au

elseif ($1='PART3') then
   wft

   write('line3','calculating final estimate of pw360')
   ATsvf
   
   ATpw90cal('pw'):pw90
   pw=pw90 pw90=pw "force 0.1usec stepsize"
   pw90=trunc(100*pw90)/100
   ATrecord('H1PW90ch1','1H pw360/4','pw360/4',pw90,'at_tpwr',tpwr)
   write('file',autotestdir+'/REPORT','H1 pw360/4 = %3.2f at power = %2.0f  ',pw90,tpwr)
   write('line3','pw90  = %2.2f',pw90)
  ATnext
endif
