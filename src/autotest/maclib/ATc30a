"macro ATc30a"
"ATc30a" "Find C13 pw90 ,power level, and BB amplifier compression"
"         using 3rd channel for 13C"

"new globals: at_max_pwxlvl   Maximum pulse power"
"             at_max_dpwr  Maximum decoupling power"
"new versions of existing parameters:   "              
"             at_pwx90c_ch3        High power 13C pwx90"
"             at_pwx90lvlc_ch3     High power 13C level"
"             at_pwx90Lowpowerc_ch3  Low power 13C pwx90"
"             at_pwx90Lowpowerlvlc_ch3 Lower power level
"             at_LBampl_comprc_ch3   Low Band Amplifier Compression"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if ($#=0) then
   ATrtp('standard')  
   at_currenttest='c13pw90c_1_ch3'
   dn='C13' 
   tpwr=at_tpwr pw=at_pw90 pw90=pw
   create('rfchannel','string')
   if (numrfch>4) then rfchannel='13245' endif
   if (numrfch=4) then rfchannel='1324' endif
   if (numrfch=3) then rfchannel='132' endif
   if (numrfch<3) then ATnext return endif   "no 3rd channel to test"
   seqfil='ATrfhomo' 
   text('Finding Null for 15 usec 13C Pulse Using\\Channel 3 (vary 13C Pulse Power)')

   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   write('line3','Finding Proper Power Level for pw(C13)=15 usec (Channel 3)')

   exists('maxattench3','parameter','global'):$e
   if ($e=1) then
    if (at_max_pwxlvl<maxattench3) then
      $maxpower=at_max_pwxlvl
    else
      $maxpower=maxattench3
    endif
   else
      $maxpower=at_max_pwxlvl
   endif



   pwx=15 array('pwxlvl',21,$maxpower-20,1)
   lb='n' sb=at/4 s1 av
   r1=1
      
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'
   wexp='ATc30a(`PART1`)'
   text 
   ATcycle au
 
elseif ($1='PART1') then
   ATsvf
   $pwxlvlmax=pwxlvl[21]
   write('line3','Finding C13 pwx90 (Channel 3)')


   exists('maxattench3','parameter','global'):$e
   if ($e=1) then
    if (at_max_pwxlvl<maxattench3) then
      $maxpower=at_max_pwxlvl
    else
      $maxpower=maxattench3
    endif
   else
      $maxpower=at_max_pwxlvl
   endif
   ATpwx90cal('pwxlvl'):$pwxlvl
   pwxlvl=$pwxlvl
   if ((pwxlvl=$pwxlvlmax) and (r1=1))then
    pwx=pwx*1.12            " increase pwx once (typ. for 8mm/10mm probes "
    array('pwxlvl',21,$maxpower-20,1)
    at_currenttest='c13pw90c_1a_ch3'
    r1=4
    ATcycle au return
   endif
   at_currenttest='c13pw90c_2_ch3'
                          "use old value if close"
   if ((pwxlvl=at_pwx90lvlc_ch3+1.0) or (pwxlvl=at_pwx90lvlc_ch3-1.0)
    or (pwxlvl=at_pwx90lvlc_ch3+2.0) or (pwxlvl=at_pwx90lvlc_ch3-2.0))
     then pwxlvl=at_pwx90lvlc_ch3
   else
     at_pwx90lvlc_ch3=pwxlvl
   endif

   text('Finding 90 Degree 13C Pulse Width (Channel 3)')
   array('pwx',31,0.5*pwx,0.03*pwx)
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'

   wexp='ATc30a(`PART2`)'
   ATcycle au
 
elseif ($1='PART2') then
   ATsvf
   at_currenttest='c13pw90c_3_ch3'
   write('line3','Determining C13 pwx90 at Reduced Power (Channel 3)')
   av
   ATpwx90cal('pwx'):pwx r4=pwx
   text('Finding 90 Degree Channel 3 13C Pulse Width at lower power')

   at_pwx90c_ch3=trunc(10*pwx)/10
   pwx=pwx*4.0 pwxlvl=pwxlvl-12
   array('pwx',31,0.5*pwx,0.03*pwx)
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'
   wexp='ATc30a(`PART3`)'
   ATcycle au
 
elseif ($1='PART3') then
   ATsvf
   at_currenttest='c13pw90c_4_ch3'

   ATpwx90cal('pwx'):pwx 
   array('pwx',21,pwx-0.1*pwx,0.01*pwx)
   wnt='ATwft select(1) f vsadj peak:$ht,cr rl(0) sp=-1.8p wp=.7p vsadj dssh dtext'
   wexp='ATc30a(`PART4`)'
   ATcycle au

elseif ($1='PART4') then
   av
   ATpwx90cal('pwx'):$pwx 
   pwx=$pwx

   at_pwx90Lowpowerc_ch3=trunc(10*pwx)/10
   at_pwx90Lowpowerlvlc_ch3=pwxlvl

   write('line3','C13 pw90 (Channel 3) =  %3.2f at power level  %2.0f',pwx,pwxlvl)

   if (at_plotauto='y') then
      printon
         echo('Pulse Calibration Data for 13C on Channel 3')
         echo('For the probe   ',at_probetype)
         echo('Based on Finding a 90 Degree Pulse  =  ',pwx)
         echo('At Power Level',pwxlvl) 
         echo('Done on    ',date)
         ATatten(pwx,pwxlvl)
      printoff
   else
      ATatten(pwx,pwxlvl)
   endif
   at_LBampl_comprc_ch3=trunc(100*pwx/(4.0*at_pwx90c))/100 
   ATrecord('LOAMPCOMPc_ch3','Low-Band Amplifier Compression (Channel 3)','compression',at_LBampl_comprc_ch3,'at_power',pwxlvl+12)
   write('file',autotestdir+'/REPORT','Low-Band Amplifier Compression (channel 3) = %1.2f at power level: %2.0f ',at_LBampl_comprc_ch3,pwxlvl+12)
   ATsvf
   ATrecord('C13PW90c_ch3','C13 Pulse Width Calibration and Amplifier Compression using Channel 3','pw90_hi',at_pwx90c_ch3,'power1',at_pwx90lvlc_ch3,'pw90_low',pwx,'power2  ',pwxlvl,'comp_power1',at_LBampl_comprc_ch3)
   write('file',autotestdir+'/REPORT','C13 pw90 (Channel 3) = %2.1f  at power level: %2.0f ',r4,pwxlvl+12)

ATnext
endif
