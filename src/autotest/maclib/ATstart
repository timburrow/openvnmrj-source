"macro ATstart"
"ATstart"

slw=0.01 "initialize"

if Console='vnmrs' then
 at_new='y'   "Only for Varian NMR System"
endif

rm('-f',autotestdir+'/atdb/at_last_selected_tests'):$dum
copy(autotestdir+'/atdb/at_selected_tests',autotestdir+'/atdb/at_last_selected_tests')
shell('chmod 666 '+ autotestdir+'/atdb/at_last_selected_tests')
if at_new='n' then
 at_fsq=fsq    "memorize starting fsq value"
 fsq='n'       "disable fsq for autotest run, reset back at end (in ATnext)"
 if (dsp='i') then dsp='n' endif    "only use dsp=n or dsp=r"
else
 dsp='n'
endif
at_dsp=dsp   "memorize starting dsp value"
exists(curexp+'/expl.out','file'):$explexists
if $explexists then rm(curexp+'/expl.out') endif
exists(userdir+'/shapelib/hard.RF','file'):$shapeexists
if $shapeexists then rm(userdir+'/shapelib/hard.RF') endif
exists(userdir+'/shapelib/eburp1.RF','file'):$shapeexists
if $shapeexists then rm(userdir+'/shapelib/eburp1.RF') endif
exists(userdir+'/shapelib/gauss32.RF','file'):$shapeexists
if $shapeexists then rm(userdir+'/shapelib/gauss32.RF') endif
exists('probeConnect','parameter','global'):$e
if ($e>0) then at_probeConnect=probeConnect probeConnect=''
   else at_probeConnect=''
endif

if (at_plotauto='y') then plotter=plotter endif  "causes error if plotter not defined"
if (at_graphhist='y') then plotter=plotter endif  "causes error if plotter not defined"
printer=printer  "causes error if printer not defined"

at_lockpower=lockpower
at_pfgon=pfgon
at_vttype=vttype               "VT unit will be left in current mode"

"REPORT is a compact file generated by 'write' command in experiment macros"
"atrecord_report is an automatically-generated file produced by the ATrecord"
" macro. It includes Pass/Fail results as determined by atdb/at_spec_table"

$report=autotestdir+'/REPORT'
$report2=autotestdir+'/prefacefile'
$atrecord_report=autotestdir+'/atrecord_report'
exists($report,'file'):$e
 if $e then mv($report,autotestdir+'/LASTREPORT') endif
exists($atrecord_report,'file'):$e
 if $e then mv($atrecord_report,autotestdir+'/LASTATRECORDREPORT') endif

lookup('file',autotestdir+'/atdb/at_selected_tests')
lookup('count','AT40'):$vttests  "VT test asked for"

if (($vttests>0) and (at_vttest='y')) then
 if (vttype=0) then
  write('line3','The VT unit is disabled (vttype=0). Reset vttype=2 and setup desired temperature before restarting AutoTest')
  abort
 else
  vnmrinfo('get','tempExpControl'):at_tempcontrol
  if at_tempcontrol=0 then   "temp under control of "temp" tcl/tk window"
   vnmrinfo('set','tempExpControl',1)
   write('line3','Temp. Control Now Set by Experiment. Reset Desired VT Mode After Test' )
    "control changed to allow VT change by experiment control(only if VT test requested"
  endif
 endif
endif

if vttype=0 then
 at_temp='n'  "tests will be done without VT control"
else
 ATgetVT:at_temp  "tests will be done at current temperature"
endif

if (at_gradtests='y') then
 substr(pfgon,3,1):$zon
 if ($zon)='n' then
  write('line3','pfgon has "z-axis" off. Re-set pfgon to desired value and restart')
  abort
 endif
endif
if (at_graphhist='y') then
 exists(autotestdir+'/history/SENSITIVITY','file'):$e
 if ($e=0) then
   write('line3','Not enough trials for graphs output. No graphs will be plotted')
   at_graphhist='n'
 else
  nrecords(autotestdir+'/history/SENSITIVITY'):$lines
  if ($lines<3) then
   write('line3','Not enough trials for graphs output. No graphs will be plotted')
   at_graphhist='n'
  endif
 endif
endif


cp(autotestdir+'/texts/reportform',$report)
cp(autotestdir+'/texts/reportform2',$report2)
cp(autotestdir+'/texts/reportform',$atrecord_report)

shell('hostname'):$hostname
shell('date'):$date

write('file',$report,'%s %s %s',$date,rev,revdate)
write('file',$report,'Run by %s on %s using %s probe at %2.1f C',at_user,$hostname,at_probetype,at_temp)
if at_new='y' then
 write('file',$report,'WFG Config.: rfwg = %s   PFG Amplifier: gradtype = %s pfgon = %s console = %s ',rfwg,gradtype,pfgon,at_consolesn)
else
 write('file',$report,'WFG Config.: rfwg = %s   PFG Amplifier: gradtype = %s pfgon = %s dsp = %s',rfwg,gradtype,pfgon,dsp)
endif
write('file',$report,'   ')

write('file',$report2,'%s %s %s',$date,rev,revdate)
write('file',$report2,'Run by %s on %s using %s probe at %2.1f C',at_user,$hostname,at_probetype,at_temp)
if at_new='y' then
 write('file',$report2,'WFG Config.: rfwg = %s   PFG Amplifier: gradtype = %s pfgon = %s console = %s ',rfwg,gradtype,pfgon,at_consolesn)
else
 write('file',$report2,'WFG Config.: rfwg = %s   PFG Amplifier: gradtype = %s pfgon = %s dsp = %s',rfwg,gradtype,pfgon,dsp)
endif
write('file',$report2,'   ')

write('file',$atrecord_report,'%s %s %s',$date,rev,revdate)
write('file',$atrecord_report,'Run by %s on %s using %s probe at %2.1f C',at_user,$hostname,at_probetype,at_temp)
if at_new='y' then
 write('file',$atrecord_report,'WFG: rfwg = %s   PFG: gradtype = %s pfgon = %s console = %s ',rfwg,gradtype,pfgon,at_consolesn)
else
 write('file',$atrecord_report,'WFG: rfwg = %s   PFG: gradtype = %s pfgon = %s  DSP: dsp = %s',rfwg,gradtype,pfgon,dsp)
endif
write('file',$atrecord_report,'   ')

 $report=autotestdir+'/REPORT'
  $day=''
  $month=''
  $daynumber=''
  $time=''
  $zone=''
  $year=''
  lookup('file',autotestdir+'/REPORT')
  lookup('file',autotestdir+'/REPORT','REPORT','skip',1,'read',6):$day,$month,$daynumber,$time,$zone,$year
  $date=$day+'_'+$month+'_'+$daynumber+'_'+$time+'_'+$zone+'_'+$year

"For Varian Probe Testing on 800"
lookup('file',autotestdir+'/atdb/at_selected_tests') 
lookup('readline'):$nextone
lookup('readline'):$nextone
if ($nextone='ATc30_10usec')  "this macro should be the second in the list"
then
  exists(autotestdir+'/previous_overnights','file'):$exists
  if $exists=0 then mkdir(autotestdir+'/previous_overnights') endif
  mv(autotestdir+'/data',autotestdir+'/history')   "save current data files in history file"
  mv(autotestdir+'/REPORT',autotestdir+'/history')

  mv(autotestdir+'/history',autotestdir+'/previous_overnights/history_stored_on_'+$date)

  exists(autotestdir+'/data.failed','file'):$exists
  if $exists=0 then
   mv(autotestdir+'/data.failed',autotestdir+'/previous_overnights/history_stored_on_'+$date)
  endif

  exists(autotestdir+'/FAILREPORT','file'):$e
  if $e then
    mv(autotestdir+'/FAILREPORT',autotestdir+'/previous_overnights/history_stored_on_'+$date)
  endif

  mkdir(autotestdir+'/data')
  mkdir(autotestdir+'/history')

  $newname='probe_used_was_'+at_probetype+'_on_'+$date
  $newfile=autotestdir+'/history/'+$newname
  write('file',$newfile,'Maximum 1H Pulse Power Was Set to       %2.0f',at_tpwr)
  write('file',$newfile,'Maximum 13C Pulse Power Was Set to      %2.0f',at_max_pwxlvl)
  write('file',$newfile,'Maximum 15N Pulse Power Was Set to      %2.0f',at_max_pwx2lvl)
  write('file',$newfile,'Maximum 13C Decoupling Power Was Set to %2.0f',at_max_dpwr)
  write('file',$newfile,'Maximum 13C Decoupling Power Was Set to %2.0f',at_max_dpwr2)
  cp($newfile,autotestdir+'/data')

endif
exists(autotestdir+'/FAILREPORT','file'):$e
if $e then
 mv(autotestdir+'/FAILREPORT',autotestdir+'/reports/FAILREPORT_stored_on_'+$date)
endif
AT01
