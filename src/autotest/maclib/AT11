"macro AT11"
"AT11" "90 Degree Stability"

if ($#=0) then
   ATrtp('standard')
   text('90-Degree Pulse Stability ')
   at_currenttest='90stability_ch1'
   array('nt',20,1,0) full
   sw=20000                " large enough to measure s/n accurately"
   crl rfl=sw/2 rfp=0
   wnt='ATwft f select(1) av vsadj dssh dtext'
   wexp='AT11(`PART1`)'
   ATcycle
   au
   write('line3','Determining 90-Degree Pulse Stability ')
   dps
elseif ($1='PART1') then

   wft crl rfl=sw/2 rfp=0 f
   ATsvf
   sp=-1p wp=2p
   ATplot:$avht,$stddev
   $avht=trunc(10*$avht)/10
   $stddev=trunc(1000*$stddev)/1000
   ATrecord('STAB90ch1','90 Degree Pulse Stability ','stability',100-$stddev,'height',$avht,'std_dev.',$stddev)
   write('file',autotestdir+'/REPORT','90 Degree Pulse Stability : %3.2f percent',100-$stddev)

   fn=4*np
   ft cz ph select(1) aph
   f peak:$int,cr rl(0) z(2000) z(-2000)
   cr=7000 delta=1000
   $snav=0 $spectrum=1
   repeat
      select($spectrum)
      bc dsn:$sn
      $snav=$snav+$sn
      $spectrum=$spectrum+1
   until $spectrum>arraydim
   $snav=trunc($snav*1.393/(arraydim))
   r3=$snav  "1.393 corrects to s/n with lb=10"
   write('graphics',100,175,' Average S/N : %6.0f ',$snav)
   ATrecord('SENSITIVITY','Average S/N for 20 Trials:','sensitivity',$snav,' oversamp',oversamp,' Gain',gain)
   if at_new='y' then
    write('file',autotestdir+'/REPORT','Average S/N for 20 Trials = %5.0f ',$snav)
   else
    write('file',autotestdir+'/REPORT','Average S/N for 20 Trials = %5.0f with dsp=%s',$snav,dsp)
   endif
   setlimit('pw',100,0,0.05)  "permits more accurate value"
   pw(10)
   pw=pw
   text('Determining 10-Degree Pulse Stability ')
   write('line3','Determining 10-Degree Pulse Stability ')
   dps gain=gain+12   "higher gain for more accurate s/n measurement for cryogenic probes"
   at_currenttest='10stability_ch1'
   wexp='AT11(`PART2`)'
   ATcycle
   au

elseif ($1='PART2') then

   wft crl rfl=sw/2 rfp=0 f
   ATsvf
   sp=-1p wp=2p
   ATplot:$avht,$stddev
   $avht=trunc(10*$avht)/10
   $stddev=trunc(1000*$stddev)/1000
   ATrecord('STAB10ch1','10 Degree Pulse Stability ','stability',100-$stddev,'height',$avht,'std_dev.',$stddev)
   write('file',autotestdir+'/REPORT','10 Degree Pulse Stability : %3.2f percent',100-$stddev)

   ft cz ph select(1) aph
   f peak:$int,cr rl(0) z(2000) z(-2000)
   cr=7000 delta=1000
   $snav=0 $spectrum=1
   repeat
      select($spectrum)
      bc dsn:$sn
      $snav=$snav+$sn
      $spectrum=$spectrum+1
   until $spectrum>arraydim
   $snav=trunc($snav*2.177/(arraydim)) "2.177 corrects to s/n with lb=10" 
   $ratio9010=r3/$snav
   $ratio9010=trunc($ratio9010*100)/100
   write('graphics',100,175,' Average S/N : %6.0f ',$snav)
   ATrecord('SENSITIVITY_10deg','Average S/N for 20 Trials (10degree flip) and Ratio of SN(90deg) to SN(10deg):','sensitivity',$snav,' Ratio',$ratio9010,' oversamp',oversamp,' Gain',gain)
   if at_new='y' then
    write('file',autotestdir+'/REPORT','Average S/N for 20 10-degree pulse Trials = %5.0f at gain=%2.0f',$snav,gain)
   else
    write('file',autotestdir+'/REPORT','Average S/N for 20 10-degree pulse Trials = %5.0f with dsp=%s, gain=%2.0f',$snav,dsp,gain)
   endif
   write('file',autotestdir+'/REPORT','Ratio of S/N for 90-degree to 10-degree Flip Angles = %1.2f and gain=%2.0f',$ratio9010, gain)
 ATnext
endif
