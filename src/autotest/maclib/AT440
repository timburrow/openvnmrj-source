"macro AT440"
"AT440 Calibrates 2H pw90 using channel 4. The 2H rf is routed"   
"      to 2H Automation Relay which sends it to the diplexer box and"
"      then to lock BNC on the probe. Thus the calibration is valid for 2H "
"      pw90 or decoupling rf field when using channel 4 for 2H"

"      Use AT430 for channel 3 calibrations "
"      For 2H observe calibrations, use AT410"

"      IMPORTANT: ampmode must be 'p' for the amplifier used for 2H."

"  For INOVA or UNITYplus:"
"  Move the Decoupler cable from the top of relay K5022 at the back of the magnet "
"  leg to the bottom position of relay K5022. This will permit the RF to proceed to
"  the lock diplexer box when tn=lk"
"  Remember to restore the cable to normal position after the test"

if ($#=0) then
   ATrtp('standard')
   text('Finding 2H pulse power on channel 4 ')
   gain=10 sw=4000 np=8192
   at_currenttest='H2pw90_ch4_1'
   if sfrq>600 then pw=200 else pw=125 endif 
   tn='lk' sethw('lock','off'):$xxx
   exists('ampmode','parameter'):$e
   if $e=0 then create('ampmode','string') endif
   if at_new='y' then
    seqfil='ATddec'
    dn3='H2' d2=.001 dof3=0
    n1='dpwr3'
    dpwr=0 dpwr2=0 dpwr3=40  rof2=300
   else
    seqfil='ATd2pul'
    exists('rfchannel','parameter'):$e
    if $e=0 then create('rfchannel','string') endif
    if numrfch=5 then rfchannel='14325' ampmode='dddpd' endif
    if numrfch=4 then rfchannel='1432' ampmode='dddp' endif
    if numrfch<4 then ATnext return endif   "macro used incorrectly"
    dn='H2' d2=.001 dof=0
    n1='dpwr'
    tpwr=40  rof2=300 dpwr=0 dpwr2=0 dpwr3=1
   endif
   r4=z0 lockpower=0
   sethw('nowait','lockpower',lockpower):$x
   wnt=''
   wexp='AT440(`PART1`)'
   au

elseif ($1='PART1') then

   wft f av vsadj(100) peak:$ht,cr movetof
   at_H2tof=tof
   if at_new='y' then dof3=tof else dof=tof endif
   exists('maxattench4','parameter','global'):$e
   if $e=1 then
    array(n1,30,maxattench4-29,1)
   else
    array(n1,30,30,1)
   endif
   d1=6*d1   "account for 2H relaxation ~6x longer than 1H"
   wnt='wft sp=3p wp=3p select(1) vsadj(10) av ATfind2Hpwr' 
   write('line3','Determining estimate of 2H pw90 power using Channel 4 ')
   vp=0 
   wexp='AT440(`PART2`)'
   au

elseif ($1='PART2') then
   wft
   ATsvf
  if (at_plotauto='y') then
      dn2='' pap
      pps(120,90,wcmax-120,90)
      pl('all') ATpage
  endif
   if at_new='y' then
    ATfind2Hpw90:$best dpwr3=dpwr3[$best]
    if ((dpwr3=at_H2tpwr_ch4+1.0) or (dpwr3=at_H2tpwr_ch4-1.0)
     or (dpwr3=at_H2tpwr_ch4+2.0) or (dpwr3=at_H2tpwr_ch4-2.0))
    then dpwr3=at_H2tpwr_ch4
    endif
   else
    ATfind2Hpw90:$best tpwr=tpwr[$best]
    if ((tpwr=at_H2tpwr_ch4+1.0) or (tpwr=at_H2tpwr_ch4-1.0)
     or (tpwr=at_H2tpwr_ch4+2.0) or (tpwr=at_H2tpwr_ch4-2.0))
    then tpwr=at_H2tpwr_ch4
    endif
   endif
   select($best) av  vsadj(80) vp=80
   write('line3','finding first estimate of 2H pw360 using Channel 4')
   text('Finding estimate of 2H pw360 using Channel 4')
   array('pw',16,3.0*pw,0.1*pw)
   wnt='ATwft dssh'
   at_currenttest='H2pw90_ch4_2'
   wexp='AT440(`PART3`)'
   au

elseif ($1='PART3') then
   wft
   write('line3','calculating first estimate of pw360  using Channel 4')
   ATsvf
  if (at_plotauto='y') then
      dn2='' pap
      pps(120,0,wcmax-120,90)
      pl('all') ATpage
  endif
   AT2Hpw90cal('pw'):pw
   write('line3','finding second estimate of 2H pw360 using channel 4 ')
   array('pw',10,3.8*pw,0.04*pw)
   at_currenttest='H2pw90_ch4_3'
   wexp='AT440(`PART4`)'
   au

elseif ($1='PART4') then
   sethw('z0',r4):$x
   wft
   write('line3','calculating final estimate of H2 pw360 using Channel 4')
   text('Determination of 2H pw360 using Channel 4')
   ATsvf
  if (at_plotauto='y') then
      dn2='' pap ATpltext
      pps(120,0,wcmax-120,90)
      pl('all') ATpage
  endif
   
   sethw('lockpower',at_lockpower):$xx
   AT2Hpw90cal('pw'):at_H2pw90_ch4
   if at_new='y' then at_H2tpwr_ch4=dpwr3 else at_H2tpwr_ch4=tpwr endif
   pw=at_H2pw90_ch4 at_H2pw90_ch4=pw  "force 0.1usec stepsize"
   at_H2pw90_ch4=trunc(100*at_H2pw90_ch4)/100
   ATrecord('H2PW90_ch4','2H pw360/4','pw360/4',at_H2pw90_ch4,' at_H2tpwr_ch4',at_H2tpwr_ch4)
   write('file',autotestdir+'/REPORT','H2 pw360/4 (CH4) = %3.2f at power = %2.0f  ',at_H2pw90_ch4,at_H2tpwr_ch4)
   write('line3','H2pw90 (CH4) = %2.2f',at_H2pw90_ch4)
   tn='H1' sethw('lock','on'):$xxx
  ATnext
endif
