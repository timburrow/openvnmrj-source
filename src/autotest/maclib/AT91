"macro AT91"
"AT91" "Field Recovery Stability (y-axis) "

if (at_gradtests='n') then ATnext return endif  "optional skipping"


   substr(pfgon,2,1):$yon
   if ($yon)='n' then
     write('line3','Y-axis is disabled. Re-set "pfgon". Gradient Tests Skipped') 
     ATnext return
   endif

if ($#=0) then
   ATrtp('standard')
   text('Field Recovery Stability\\ following a 10G/cm 1msec Y-axis Gradient')
    exists('sysgcoil','parameter','systemglobal'):$present
    if $present=1 then
       at_sysgcoil=sysgcoil
       sysgcoil='' gcoil=''
    endif          "memorize existing sysgcoil value (restored at end by ATnext)"

   at_currenttest='grecstab_y'
   array('nt',20,1,0)
   seqfil='ATg2pul' gt1=.001 gzlvl1=10/at_gycal
   vchannelmap='0','y'
   rof1=.5 d2=.0001 shaped='n' gradaxis='y'
   sp=3.8p wp=2p
   wnt='ATwft select(1) av vsadj dssh dtext'
   wexp='AT91(`PART1`)'
   ATcycle
   au
   dps
elseif ($1='PART1') then
   wft
   ATsvf
   ATplot:$avht,$stddev
   $avht=trunc(10*$avht)/10
   $stddev=trunc(1000*$stddev)/1000
   ATrecord('YRECOVSTAB','Field Recovery Stability 100usec after a 10G/cm Y Gradient Pulse','stabiltiy',100-$stddev,'height',$avht,'std_dev.',$stddev)
   write('file',autotestdir+'/REPORT','Y-axis Recovery Stability =  %3.3f ',100-$stddev)

   exists('sysgcoil','parameter','systemglobal'):$present
   if $present=1 then  sysgcoil=at_sysgcoil gcoil=sysgcoil endif "restore sysgcoil value"
ATnext
endif
