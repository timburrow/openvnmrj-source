"macro AT10salt100mM"
"AT10salt100mM"   " Find 1H pw360 using 100mM NaCl AutoTest Sample"

if ($#=0) then
   ATrtp('standard')
   text('Finding 1H pw90 using 100mM NaCl Sample')
   gain=10    av
   at_currenttest='pw90_ch1_1_salt100mM'
   array('pw',30,pw90/7,pw90/10)
   vs=vs/2
   wnt='ATwft select(1) vsadj(10) dssh' 
   write('line3','Determining estimate of pw90 ')
   vp=0 
   wexp='AT10salt100mM(`PART1`)'
   ATcycle
   au

elseif ($1='PART1') then
   wft
   ATsvf
   ATpw90cal2('pw'):$bb

   write('line3','finding first estimate of pw360')
   pw=3.0*$bb,3.2*$bb,3.4*$bb,3.6*$bb,3.8*$bb,4.0*$bb,4.2*$bb,4.4*$bb,4.6*$bb,4.8*$bb
   wnt='ATwft dssh'
   at_currenttest='pw90_ch1_2_salt100mM'
   wexp='AT10salt100mM(`PART2`)'
   ATcycle
   au

elseif ($1='PART2') then
   wft
   write('line3','calculating first estimate of pw360 ')
   ATsvf
 
   ATpw90cal('pw'):pw90

   write('line3','finding second estimate of pw360 ')
   wnt='ATwft dssh'
   array('pw',10,3.6*pw90,0.08*pw90)
   at_currenttest='pw90_ch1_3_salt100mM'
   wexp='AT10salt100mM(`PART3`)'
   ATcycle
   au

elseif ($1='PART3') then
   wft

   write('line3','calculating final estimate of pw360 ')
   ATsvf
   
   ATpw90cal('pw'):$pw90
   pw=$pw90 $pw90=pw  "force 0.1usec stepsize"
   $pw90=trunc(100*$pw90)/100
   $ratio=$pw90/at_pw90
   $ratio=trunc(100*$ratio)/100
   ATrecord('H1PW90ch1salt100mM','1H pw360/4','pw360/4',$pw90,'at_tpwr',tpwr,' salt/nosalt ratio',$ratio)
   write('file',autotestdir+'/REPORT','H1 pw360/4 (salt100mM) = %3.2f at power = %2.0f. Ratio salt/nosalt = %1.2f',$pw90,tpwr,$ratio)
   write('line3','pw90_salt100mM = %2.2f',$pw90)
  ATnext
endif
