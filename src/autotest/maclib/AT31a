"macro AT31a"
"AT31a" "Find C13 RF Homogeneity Using Channel 3"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if ($#=0) then
   ATrtp('standard')  
   at_currenttest='c13rfhomo1_ch3'
   create('rfchannel','string')
   if (numrfch>4) then rfchannel='13245' endif
   if (numrfch=4) then rfchannel='1324' endif
   if (numrfch=3) then rfchannel='132' endif
   if (numrfch<3) then ATnext return endif   "no 3rd channel to test"
   dn='C13' 
   seqfil='ATrfhomo' 
   pwxlvl=at_pwx90lvl_ch3 j=142

   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   lb='n' sb=at/4 s1 av
   wnt=''
   wexp='AT31a(`PART1`)'
   dtext
   ATcycle au
 
elseif ($1='PART1') then

   wft f vsadj peak:$ht,cr rl(0)
   sp=-1500 wp=1300 vsadj 
   th=100 nll
   getll(1):$int,cr 
   cr=cr-71
   movetof
   axis='h'
   sp=-175 wp=350
   array('pwx',55,0,3)
   text('X Coil RF Homogeneity:\\ Channel 3 13C pulses incremented in \\3 usec steps \\from 0 to 162usec\\ (From 0 to > 899 degrees in Rotation)')
   wexp='AT31a(`PART2`)' 
   wnt='ATwft crl rfl=sw/2 rfp=0 sp=-175 wp=350 select(1) vsadj dssh dtext'
   dtext
   ATcycle au

elseif ($1='PART2') then

   wft ATsvf
   select(1) vsadj th=50 nll
   dssh('dodc')
   s1
   if ((at_plotauto='y') and (at_printparams='n'))
    then ATpltext(wcmax-95,wc2max-6)
   endif
   ATplot
   av
   ATxrfhomocal:$pwx90,$pwx360,$pwx720,$ratio1,$ratio2
   pwx=0
   pwx[2]=$pwx360-4,$pwx360-3.6,$pwx360-3.2,$pwx360-2.8,$pwx360-2.4,$pwx360-2,$pwx360-1.6,$pwx360-1.2,$pwx360-.8,$pwx360-.4,$pwx360,$pwx360+.4,$pwx360+.8,$pwx360+1.2,$pwx360+1.6,$pwx360+2,$pwx360+2.4,$pwx360+2.8,$pwx360+3.2,$pwx360+3.6,$pwx360+4
   pwx[23]=$pwx720-4,$pwx720-3.6,$pwx720-3.2,$pwx720-2.8,$pwx720-2.4,$pwx720-2,$pwx720-1.6,$pwx720-1.2,$pwx720-.8,$pwx720-.4,$pwx720,$pwx720+.4,$pwx720+.8,$pwx720+1.2,$pwx720+1.6,$pwx720+2,$pwx720+2.4,$pwx720+2.8,$pwx720+3.2,$pwx720+3.6,$pwx720+4,$pwx720+4.4,$pwx720+4.8,$pwx720+5.2,$pwx720+5.6,$pwx720+6
   text('Channel 3 C13 RF homogeneity: 0 degree, 360 array, 720 array')
   wnt='ATwft select(1) vsadj dssh dtext'
   at_currenttest='c13rfhomo2_ch3'
   wexp='AT31a(`PART3`)'
   ATcycle au

elseif ($1='PART3') then

   wft
   ATxrfhomocal2:$pwx90,$pwx360,$pwx720,$ratio1,$ratio2
   ATrecord('C13RFHOMO_ch3','C13 RF Homogeneity','power',pwxlvl,'pwx90',$pwx90,'pwx360',$pwx360,'pwx720',$pwx720,'360/0',$ratio1,'720/0',$ratio2)
   write('file',autotestdir+'/REPORT','C13 RF HOMO_ch3: pw90=%2.1f pw360=%2.1f 360/0=%1.2f pw720=%3.1f 720/0=%1.2f power=%2.0f',$pwx90,$pwx360,$ratio1,$pwx720,$ratio2,pwxlvl)
   write('line3','C13 pw90_ch3 = %2.1f ',$pwx90)
   ATsvf
   if ((at_plotauto='y') and (at_printparams='n'))
    then ATpltext(wcmax-95,wc2max-6)
   endif
   ATplot

ATnext
endif
