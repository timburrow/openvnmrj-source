"macro ATc32"
"ATc32" "C13 Decoupling using Built-in Phase Modulators"
"        decoupling power is limited by user-defined upper limit"
"        if upper limit is exceeded, dpwr and dmf are reduced appropriately"
"        decoupling efficiency may be compromised"

if (at_c13tests='n') then ATnext return endif  "optional skipping"

if (at_cycletest='yy') then ATnext return endif  "no need to repeat "

if ($#=0) then
   ATrtp('standard')  
   dn='C13' 
   tof=at_tof tpwr=at_tpwr pw=at_pw90 pw90=pw
   seqfil='s2pul'
   at_currenttest='Decouplingprofiles_c'
   lb='n' sb=at/4 s1 av



   "proper sfrq/dfrq for on-resonance HDO/13CH3OD is 3.97675 at 20C"
   "observed 13C shift is 0.01675 ppm per degree"

   $dfrqcorrect=sfrq/3.9767612
   dof=($dfrqcorrect-dfrq)*1e6
   dof=dof+(temp-20)*(0.01675*dfrq)   "correct for temperature change"

   wnt=''
   wexp='ATc32(`PART1`)'
   ATcycle au
 
elseif ($1='PART1') then

   wft sp=2.8p wp=.8p vsadj
   text('Phase Modulator Decoupling Profiles')
   lb=10 sb=-.07 gf='n' sbs='n'

   ln(150.0/at_pwx90Lowpowerc):$ln
   dpwr=at_pwx90Lowpowerlvlc-20.0*$ln*0.43429
   $dpwr=dpwr+0.5
   dpwr=trunc($dpwr)
   dmf=1e6/150.0
   dmfadj

 $count=1
 if (dpwr>at_max_dpwr) then
  write('line3','Decoupling Power Requested Exceeds Defined Power Limit (at_max_dpwr)')
  write('line3',' Power Reduced to Permitted Level (at_max_dpwr)')
  n1='Decoupling Power Requested Exceeds Defined Power Limit (at_max_dpwr)'
  n2=' Power Reduced to Permitted Level (at_max_dpwr)'
  atext(n1) atext(n2)
  $diff=dpwr - at_max_dpwr
  repeat
    dmf =dmf/1.12 
    dpwr=dpwr - 1
    $count=$count+1
  until $count>$diff
  dmfadj
 endif

   dmm='ccm','ccx','ccw','ccg' dm='nny'
   dres=90,90,90,1 
   array('dof',41,dof-80*dfrq,4*dfrq)
   array='(dmm,dres),dof'
   wnt='ATdmms'
   wexp='ATc32(`PART2`)'
   ATcycle au

elseif ($1='PART2') then

   wft 
   ATsvf
   ATpldecs

ATnext
endif
