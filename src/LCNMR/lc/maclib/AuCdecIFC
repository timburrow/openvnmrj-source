"macro AuCdecIFC"
"********************************************************"
"AuCdecIFC - Proton observe Autocalibration               "
"********************************************************"

IF ($# < 1) THEN

$fileex=0 $file=''
AutoCheck('H1ref'):$fileex,$file
$explist=explist
if ($fileex) then RT($file) else return endif
wft
$pwxlvl=C13pwxlvl
$c13pwx=C13pwx
$archivedir=archivedir
$sample=sample
$pltopt=pltmod

f vsadj peak(1.5p,3p):$ht,cr
movetof sw=500

hmqc

exists('C13pwxlvl','parameter'):$e1
if not $e1 then create('C13pwxlvl','integer') endif
exists('C13pwx','parameter'):$e2
if not $e2 then create('C13pwx','pulse') endif
exists('archivedir','parameter'):$e3
if not $e3 then create('archivedir','string') endif
exists('sample','parameter'):$e4
if not $e4 then create('sample','string') endif
exists('explist','parameter'):$e6
if not $e6 then create('explist','string') endif
C13pwxlvl=$pwxlvl
C13pwx=$c13pwx
archivedir=$archivedir
sample=$sample
explist=$explist

ni=1 nt=4 d1=2 null=0 phase=1 dm='n' hs='nn' np=512
getparam('tpwr',tn):tpwr
getparam('pw90',tn):pw
pwxlvl=C13pwxlvl

av vp=10
j=137 setoffset(dn,1.38):dof

array=''
array('pwx',30,2,2)
ai fn=8k
wnt='array_display_C13'
wexp='AuCdecIFC(2)'
alock='n' wshim='n' r1=1 r3=1
clear(2)
sinebell
au('wait','next')

ENDIF

"********************************************************"

IF ($1 = 2) THEN

wft select(1) av 
$counter=1
repeat
  select($counter)
  peak(sp,wp+sp):$ht
  if $counter=1 then $maxht = $ht $pwx=pwx[1] else
     if $ht>$maxht then $maxht=$ht $pwx=pwx[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

write('alpha','1: $pwx= %5.1f pwxlvl=%5.1f',$pwx,pwxlvl)

if ($pwx > 2*C13pwx) then
  if (r1 < 2) then
    r1 = r1+1
    pwxlvl=pwxlvl+6
    au('next','wait')
    return
  else
    write('error','Autocalibration failed on second try -- determine manually')
    abort
  endif
endif

write('alpha','2: $pwx= %5.1f pwxlvl=%5.1f',$pwx,pwxlvl)

 $factor = C13pwx/$pwx
 ln($factor):$lnfactor
 $pwxlvldiff = trunc(20*0.43429*$lnfactor)
 if ((pwxlvl-$pwxlvldiff) > 63) then
   $pwxlvldiff = pwxlvl-63
 endif
if ($pwx = C13pwx) then AuCdecIFC(3,$pwx) return endif
if ($pwx < C13pwx) then 
 if (($pwxlvldiff > 0) and (r3 < 2)) then
  pwxlvl=pwxlvl - $pwxlvldiff r3 = r3+1
  au('next','wait')
  return
 else
  AuCdecIFC(3,$pwx) return 
 endif
endif

pwxlvl=pwxlvl - $pwxlvldiff
array('pwx',2.5*C13pwx,1,1)
clear(2) 
wexp='AuCdecIFC(4)'
au('next','wait') 

ENDIF

"********************************************************"

IF ($1 = 3) THEN

$pwx=$2
array('pwx',20,1.0*$pwx,0.1*$pwx)
clear(2) full
da
wexp='AuCdecIFC(5)' 
au('next','wait')  

ENDIF

"********************************************************"

IF ($1 = 4) THEN

clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $maxht = $ht $pwx=pwx[1] else
     if $ht>$maxht then $maxht=$ht $pwx=pwx[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)
select(1)

if (($pwx < C13pwx) or ($pwx = C13pwx)) then AuCdecIFC(3,$pwx) return endif

if (pwxlvl < 63) then
 $factor = C13pwx/$pwx
 ln($factor):$lnfactor
 $pwxlvldiff = trunc(20*0.43429*$lnfactor)
 $newpwxlvl = pwxlvl - $pwxlvldiff
 if ($newpwxlvl <> pwxlvl) then 
  wexp='AuCdecIFC(4)' 
  pwxlvl=$newpwxlvl
  clear(2)
  au('next','wait')
 else
  AuCdecIFC(3,$pwx) return 
 endif

else
 AuCdecIFC(3,$pwx)
endif

ENDIF

"********************************************************"

IF ($1 = 5) THEN

clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $minht = $ht $pw180=pwx[1] else
     if $ht<$minht then $minht=$ht $pw180=pwx[$counter]
     endif
  endif
  $counter=$counter+1
until $counter>arraydim
$pwx=$pw180/2.0

if ($pwx > C13pwx) then
 if (pwxlvl < 63) then
  pwxlvl=pwxlvl+1
  array('pwx',20,1.0*$pwx,0.1*$pwx)
  au('next','wait')
  return
 endif
endif

$diff = pwx[2] - pwx[1]
if ($diff > 0.5) then
 array('pwx',21,$pw180-4,0.4)
 au('next','wait')
 return
endif

C13pwx=$pwx

clear(2)
pwxlvl=pwxlvl-12 $pwx=$pwx*4
array('pwx',41,1*$pwx,0.05*$pwx)
clear(2) full
da
wexp='AuCdecIFC(6)' 
au('next','wait')  

ENDIF

"********************************************************"

IF ($1 = 6) THEN

clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $minht = $ht $pw180=pwx[1] else
     if $ht<$minht then $minht=$ht $pw180=pwx[$counter]
     endif
  endif
  $counter=$counter+1
until $counter>arraydim

array('pwx',21,$pw180-8,0.8)
wexp='AuCdecIFC(7)'
au('next','wait')

ENDIF

"********************************************************"

IF ($1 = 7) THEN

clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $minht = $ht $pw180=pwx[1] else
     if $ht<$minht then $minht=$ht $pw180=pwx[$counter]
     endif
  endif
  $counter=$counter+1
until $counter>arraydim

$ref_pwx90=$pw180/2.0
$ref_pwxlvl=pwxlvl
$pwxlvl_cf = $ref_pwx90/4/C13pwx

pwxlvl=pwxlvl+12
pwx=C13pwx

wnt='array_display_C13' 
wexp='dssh Autooutput Autocalsave(`C13pwx`) AuCdecIFC(8)'
$strpwx='' $strpwxlvl='' $strref_pwxlvl='' $strref_pwx90=''
$strdmf='' $strdpwr='' $sdres='' $strpwxlvl_cf=''
format(C13pwx,0,1):$strpwx
format(pwxlvl,0,0):$strpwxlvl
format($ref_pwxlvl,0,0):$strref_pwxlvl
format($ref_pwx90,0,1):$strref_pwx90
format($pwxlvl_cf,0,4):$strpwxlvl_cf
setparams('pwx',$strpwx,'C13')
setparams('pwxlvl',$strpwxlvl,'C13')
setparams('pwxlvl_cf',$strpwxlvl_cf,'C13')


$dmm=''
AuCdecIFC(9):$dmm

format(dpwr,0,0):$strdpwr
format(dmf,0,0):$strdmf
format(dres,0,0):$sdres
setparams('dmf',$strdmf,'C13')
setparams('dpwr',$strdpwr,'C13')
setparams('dseq',dseq,'C13')
setparams('dres',$sdres,'C13')
setparams('dmm',$dmm,'C13')
$text='C13 dec. 90 degree pw: '+$strpwx
$atext='  at power level: '+$strpwxlvl
$atext1 = '  compression factor: '+$strpwxlvl_cf
atext($text)
atext($atext)
atext($atext1)
$pwinc= trunc((pwx/6)+0.5)
array('pwx',50,$pwinc,$pwinc)
sinebell

if (pltmod = 'fixed') then
  au('next','wait')
else
  wexp='Autooutput Autocalsave(`C13pwx`)'
  Autocalsave('C13pwx','par')
  AuCdecIFC(8)
endif

ENDIF

"********************************************************"

IF ($1 = 8) THEN

select(1)
seqfil='s2pul'
pwx=C13pwx
vp=0
dm='nny' dmm='ccw' wp=600 sp=sp-250
setdecpars
" wnt='wft(`acq`) select(1) vsadj(20) dssh' "
wnt='array_display_C13'
wexp='dssh Autooutput Autocalsave(`C13dec_profile`)'
$dof=dof+(125*dfrq)
array('dof',51,$dof,-5*dfrq)
$strdmf='' $strdpwr=''
format(dpwr,0,0):$strdpwr

getparam('pwxlvl_cf','C13'):$attcf
getparam('pwx','C13'):$pwx
getparam('pwxlvl','C13'):$pwxlvl
exp(($pwxlvl-dpwr)*2.303/20):$mult
$pwx=$pwx*$mult*$attcf
format(250000/$pwx,0,0):$strdmf

$file=archivedir+'/'+sample+'/H1ref'
gettxt($file)
$text=dseq+' C13 decoupling profile:'
$atext='  +/- 125ppm from on-resonance'
$atext0='    in 5ppm steps'
$atext1='C13 dec. gammah2: '+$strdmf
$atext2 ='   at power level: '+$strdpwr
atext($text)
atext($atext) atext($atext0) atext($atext1) atext($atext2)
Autocalsave('C13dec_profile','par')
AutoDelCAL('CdecIFC')
write('alpha','done with Cdec calibrations')
AutoDelCAL('CdecIFC')
Autocalnext

ENDIF

"********************************************************"

IF ($1 = 9) THEN

$ampcf=1
$shapefile=''
$ref_pw90=pwx $ref_pwr=pwxlvl
$ampcf=0
getparam('pwxlvl_cf',dn):$ampcf
if ($ampcf = 0) then $ampcf=1 endif
$ref_pw90 = $ref_pw90*$ampcf
$shapefile='W40_'+probe+'.DEC'

$rfwg2=''
if (Console <> 'mercury') then
 substr(rfwg,2,1):$rfwg2
 if (rfchtype[2] = 'U+ H1 Only') then
   substr(rfwg,1,1):$rfwg2
 endif
else
 $rfwg2='n'
endif

if ($rfwg2 = 'y') then
 $decband=200*dfrq
 $sdecband=''
 format($decband,0,0):$sdecband
 $wave='WURST40 0.0014/'+$sdecband
 opx($shapefile)
 setwave($wave)
 cpx($ref_pw90,$ref_pwr)
 pbox_pwr($shapefile):dpwr
 pbox_dmf($shapefile):dmf
 dpwr=dpwr+2
 dseq='W40_'+probe
 dres=9
 $dmm='p'
 $dum=''
 exists(userdir+'/probes/'+probe+'/'+probe,'file'):$e
 if $e>0 then
  cp(userdir+'/shapelib/'+$shapefile,userdir+'/probes/'+probe)
 else
  cp(userdir+'/shapelib/'+$shapefile,systemdir+'/probes/'+probe)
  cp(userdir+'/shapelib/'+$shapefile,systemdir+'/shapelib')
 endif
else

  if ((Console='unity') or (Console='vxrs') or (Console='g2000')) then
    $gammab2=80*dfrq 
    $dmm='w'
    dseq='waltz16'
    dres=90
  else
    $gammab2=40*dfrq 
    $dmm='g'
    dseq='garp1'
    dres=1
  endif
  if ($gammab2 > 4800) then
   $gammab2 = 4800
  endif
  setpower($gammab2,dn):dpwr,$dmf90
  dmf=1e6/$dmf90 dmfadj(dres)
endif


return($dmm)

ENDIF

"********************************************************"
