"macro sortPeakNodes"
is_submitmode:$submit
if ($submit=0) then
  $eq=autodir+'/enterQ.macdir/cursqexp/tmpstudy/enterSQ'
  $elist=autodir+'/enterQ.macdir/cursqexp/EXPLIST'
  exists($eq,'file'):$fex
  if ($fex=0) then return endif
else
  $eq=cursqexp+'/tmpstudy/enterSQ'
  $elist=cursqexp+'/EXPLIST'
endif

$peak=0 $expnodes='' 
$pnodes='' $peaktime=0

$explist='' $s1='' $exptime=0
readfile($elist,'$explist','$s1','','local'):$tot
if ($tot=0) then return endif
$i=1
repeat
   strstr($s1[$i],'Queued'):$q
   if ($q) then substr($s1[$i],1):$t else $t=0 endif
   $exptime[$i]=$t
   $i=$i+1
until $i > $tot

$j=1
$key=$eq
$done=0
while $done<1 do
   $a1='' $a2='' $line=''
   lookup('mfile',$key,'read',2,'readline',1,'filekey'):$a1,$a2,$line,$key,$ret
   if ($ret<4) then $done=1 
   else
	if ($a2<>'parent') then
	   teststr('$explist',$a2,'local'):$isexp
	   if ($isexp) then 
		$expnodes[$j]=$expnodes[$j]+' '+$a2 
		$peaktime[$j]=$peaktime[$j]+$exptime[$isexp]
	   endif
	else
	   strstr($line,'Peak_'):$ispeak
	   if ($ispeak) then
		$j=$j+1
		$peak[$j]=$j-1
		$pnodes[$j]=$a1
		$peaktime[$j]=0
		$expnodes[$j]=''
	   endif
	endif
   endif
endwhile

$tot=size('$peak')
if ($tot>1) then
   $p2=0 $e2='' $t2=0 $n2=''
   $i=2 $j=1
   repeat
	$p2[$j]=$peak[$i]
	$t2[$j]=$peaktime[1]+$peaktime[$i]
	$n2[$j]=$pnodes[$i]
	$e2[$j]=$expnodes[1]+' '+$expnodes[$i]
	$j=$j+1
	$i=$i+1
   until $i > $tot
   $peak=$p2 $expnodes=$e2
   $peaktime=$t2 $pnodes=$n2
endif
return($peak,$expnodes,$peaktime,$pnodes)
