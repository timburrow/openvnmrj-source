"macro AuHobsIFC"
"********************************************************"
"AuHobsIFC - Proton observe Autocalibration               "
"********************************************************"

IF ($# < 1) THEN

$1 = 1

$fileex=0 $file=''
AutoCheck('H1ref'):$fileex,$file
$explist=explist
if ($fileex) then RT($file) else write('error','H1 reference FID not found') return endif
wft
explist=$explist

if (solvent = 'D2O') then
 sp=4.0p wp=1p
endif

ds ph aph vsadj(20) vp=wc2max/2
d1=15
array('pw',25,1,1)
ai fn=8k at=0.5 lb=1/at
wnt='array_display_H1_1'
"wnt='wft(`acq`) select(1) vsadj(10) dssh' "
wexp='AuHobsIFC(2)'
alock='n' wshim='n' r1=1 r3=1
clear(2)
au('wait','next')

ENDIF

"********************************************************"

IF ($1 = 2) THEN


wft ph 
$counter=1
repeat
  select($counter)
  peak(sp,wp+sp):$ht
  if $counter=1 then $maxht = $ht $pw90=pw[1] else
     if $ht>$maxht then $maxht=$ht $pw90=pw[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)

if ($pw90 > 2*H1pw90) then
  if (r1 < 2) then
    r1 = r1+1
    tpwr=tpwr+6
    au('next','wait')
    return
  else
    write('error','Autocalibration failed on second try -- determine manually')
    abort
    AutoDelCal('HobsIFC')
  endif
endif

 $factor = H1pw90/$pw90
 ln($factor):$lnfactor
 $tpwrdiff = trunc(20*0.43429*$lnfactor)
 if ((tpwr-$tpwrdiff) > 63) then
   $tpwrdiff = tpwr-63
 endif
if ($pw90 = H1pw90) then AuHobsIFC(3,$pw90) return endif
if ($pw90 < H1pw90) then 
 if (($tpwrdiff > 0) and (r3 < 2)) then
  tpwr=tpwr - $tpwrdiff r3 = r3+1
  au('next','wait')
  return
 else
  AuHobsIFC(3,$pw90) return 
 endif
endif

tpwr = tpwr - $tpwrdiff
array('pw',4*H1pw90,1,1)
clear(2) 
wexp='AuHobsIFC(4)' au('next','wait') 

ENDIF

"********************************************************"

IF ($1 = 3) THEN

d1=2 sp=1.5p wp=1.5p
$pw90=$2
ph ds(3) aph
array('pw',17,3.2*$pw90,0.1*$pw90)
clear(2) full
da
wnt='array_display_H1_2'
wexp='AuHobsIFC(5)' 
au('next','wait')  

ENDIF

"********************************************************"

IF ($1 = 4) THEN

clear(2)
wft ph 
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $maxht = $ht $pw90=pw[1] else
     if $ht>$maxht then $maxht=$ht $pw90=pw[$counter] 
     endif
  endif
  $counter=$counter+1
until ($counter>arraydim)
select(1)

if (($pw90 < H1pw90) or ($pw90 = H1pw90)) then AuHobsIFC(3,$pw90) return endif

if (tpwr < 63) then
 $factor = H1pw90/$pw90
 ln($factor):$lnfactor
 $tpwrdiff = trunc(20*0.43429*$lnfactor)
 $newtpwr = tpwr - $tpwrdiff
 if ($newtpwr <> tpwr) then 
  wexp='AuHobsIFC(4)' 
  tpwr=$newtpwr
  clear(2)
  au('next','wait')
 else
  AuHobsIFC(3,$pw90) return 
 endif

else
 AuHobsIFC(3,$pw90)
endif

ENDIF

"********************************************************"

IF ($1 = 5) THEN

d1=2 sp=1.5p wp=1.5p
clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $minht = $ht $pw360=pw[1] else
     if $ht<$minht then $minht=$ht $pw360=pw[$counter]
     endif
  endif
  $counter=$counter+1
until $counter>arraydim
pw90=$pw360/4.0

if (pw90 > H1pw90) then
 if (tpwr < 63) then
  tpwr=tpwr+1
  array('pw',17,3.2*pw90,0.1*pw90)
  au('next','wait')
  return
 endif
endif

$diff = pw[2] - pw[1]
if ($diff > 0.5) then
 array('pw',21,$pw360-4,0.4)
 au('next','wait')
 return
endif

clear(2)
tpwr=tpwr-12 $pw90=pw90*4
ph
array('pw',41,3*$pw90,0.05*$pw90)
clear(2) full
da
wexp='AuHobsIFC(6)' 
au('next','wait')  

ENDIF

"********************************************************"

IF ($1 = 6) THEN

clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $minht = $ht $pw360=pw[1] else
     if $ht<$minht then $minht=$ht $pw360=pw[$counter]
     endif
  endif
  $counter=$counter+1
until $counter>arraydim

array('pw',21,$pw360-8,0.8)
wexp='AuHobsIFC(7)'
au('next','wait')

ENDIF

"********************************************************"

IF ($1 = 7) THEN

clear(2)
wft av
$counter=1
repeat
  select($counter)
  peak(sp,sp+wp):$ht
  if $counter=1 then $minht = $ht $pw360=pw[1] else
     if $ht<$minht then $minht=$ht $pw360=pw[$counter]
     endif
  endif
  $counter=$counter+1
until $counter>arraydim
$ref_pw90=$pw360/4.0
$ref_pwr=tpwr
$tpwr_cf = $ref_pw90/4/pw90
tpwr=tpwr+12

wnt='wft(`acq`) dssh(`dodc`)' 
wexp='ph select(5) aph0 vsadj(50) dssh(`dodc`) Autooutput Autocalsave(`H1pw90`) AutoDelCAL(`HobsIFC`) Autocalnext'
$pwinc= trunc((pw90/5)+0.5)
array('pw',50,$pwinc,$pwinc)
$strpw90='' $strtpwr='' $strref_pwr='' $strref_pw90=''
$strtpwr_cf = ''
format(pw90,0,1):$strpw90
format(tpwr,0,0):$strtpwr
format($ref_pwr,0,0):$strref_pwr
format($ref_pw90,0,1):$strref_pw90
format($tpwr_cf,0,4):$strtpwr_cf
setparams('pw90',$strpw90)
setparams('tpwr',$strtpwr)
setparams('tpwr_cf',$strtpwr_cf)
"setparams('ref_pwr',$strref_pwr)"
"setparams('ref_pw90',$strref_pw90)"
$text='H1 obs. 90 degree pw: '+$strpw90
$atext='  at power level: '+$strtpwr
$atext1 = '  compression factor: '+$strtpwr_cf
atext($text)
atext($atext)
atext($atext1)

d1=15 sp=4p wp=1p
wnt='array_display_H1_1'
if (pltmod = 'fixed') then
   au('next','wait')
else
   wexp='dssh(`dodc`) Autooutput Autocalsave(`H1pw90`)' 
   Autocalsave('H1pw90','par')
   AutoDelCAL('HobsIFC')
   write('alpha',' done with Hobs calibrations')
   Autocalnext
endif

ENDIF

"********************************************************"
